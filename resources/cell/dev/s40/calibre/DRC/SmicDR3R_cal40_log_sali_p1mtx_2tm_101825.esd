
//$Date: 2011/01/12 05:50:17 $
//==================================================================================================|
//|                                                                                                 |
//|  40nm 1PXM double topmetals Calibre ESD rule file for :                                         |
//|       SMIC:       40nm Logic 1P10M Salicide 0.9/1.1/1.2/1.8/2.5V Low Leakage                    |
//|                   and Generic Design Rule                                                       |
//|                                                                                                 |
//|       Doc. No:       TD-LO40-DR-2001         Rev: 3R       Tech Dev Rev: 1.0                    |
//|
//| 
//|       Calibre Tool Version : 2009.4_16.13                                                       |
//|                                                                                                 ||
//|=================================================================================================|
//| RELEASE NOTE !                                                                                  |
//|                                                                                                 |
//|                                                                                                 |
//|      Following Guidelines are not implemented :                                                 |
//|                                                                                                 |
//|         (1)  ESDLO.1                                                                            |
//|         (2)  ESDLO.10 ~ ESDLO.13                                                                |
//|         (3)  ESDLO.18 ~ ESDLO.21                                                                |
//|         (4) LU.5, LU.6                                                                          |
//|                                                                                                 |
//|												    |
//|                                                 |
//|                                                                                                 |
//|=================================================================================================|
//|                                                                                                 |
//|    DISCLAIMER                                                                                   |
//|                                                                                                 |   
//|   SMIC hereby provides the quality information to you but makes no claims,                      |   
//| promises or guarantees about the accuracy, completeness, or adequacy of the                     |
//| information herein. The information contained herein is provided on an "AS IS"                  |
//| basis without any warranty, and SMIC assumes no obligation to provide support                   |
//| of any kind or otherwise maintain the information.                                              |
//|                                                                                                 |
//|   SMIC disclaims any representation that the information does not infringe any                  |
//| intellectual property rights or proprietary rights of any third parties.SMIC                    |
//| makes no other warranty, whether express, implied or statutory as to any                        |
//| matter whatsoever, including but not limited to the accuracy or sufficiency of                  |
//| any information or the merchantability and fitness for a particular purpose.                    |
//| Neither SMIC nor any of its representatives shall be liable for any cause of                    |
//| action incurred to connect to this service.                                                     |
//|                                                                                                 |
//|=================================================================================================|
//|                                                                                                 |
//|    STATEMENT OF USE AND CONFIDENTIALITY                                                         |
//|                                                                                                 |
//|   The following/attached material contains confidential and proprietary                         |
//| information of SMIC. This material is based upon information which SMIC                         |
//| considers reliable, but SMIC neither represents nor warrants that such                          |
//| information is accurate or complete, and it must not be relied upon as such.                    |
//| This information was prepared for informational purposes and is for the use by                  |
//| SMIC's customer only. SMIC reserves the right to make changes in the                            |
//| information at any time without notice.                                                         |
//|   No part of this information may be reproduced, transmitted, transcribed,                      |
//| stored in a retrieval system, or translated into any human or computer                          |
//| language, in any form or by any means, electronic, mechanical, magnetic,                        |
//| optical, chemical, manual, or otherwise, without the prior written consent of                   |
//| SMIC. Any unauthorized use or disclosure of this material is strictly                           |
//| prohibited and may be unlawful. By accepting this material, the receiving                       |
//| party shall be deemed to have acknowledged, accepted, and agreed to be bound                    |
//| by the foregoing limitations and restrictions. Thank you.                                       |
//|                                                                                                 |
//|=================================================================================================|
//|                                                                                                 |
//|   This file includes SVRF/TVF Technology under license by Mentor Graphics Corporation.          |
//| "SVRF/TVF Technology" shall mean Mentor Graphics' Standard Verification Rule Forma ("SVRF")     |
//| and Tcl Verification Format ("TVF") proprietary syntaxes for expressing process rules.          |
//| You shall not use SVRF/TVF Technology unless you are a Mentor Graphics customer with a license  |
//| to use the applicable Mentor Graphics software. The exact terms of your obligations and rights  |
//| are governed by your respective license. You shall not use SVRF/TVF Technology except: (a)      |
//| for your internal business purposes and (b) for use with Mentor Graphics' Calibre tools.        |
//| All SVRF/TVF Technology constitutes or contains trade secrets and confidential information of   |
//| Mentor Graphics or its licensors. You shall not make SVRF/TVF Technology available in any form  |
//| to any person other than your employees and on-site contractors, excluding Mentor Graphics      |
//| competitors, whose job performance requires access and who are under obligations of             |
//| confidentiality.                                                                                |
//|=================================================================================================|
//|                                                                                                 |
//| Revision History :                                                                              |
//|                                                                                                 |
//|     Rev        Date            Who                           What                               |
//|   -------  -------------  --------------  ---------------------------------------------------   |
//|  V1.0_2R    09-06-2013       Liu Li          Change rules name from ESDLO_5~ESDLO_17 to ESDLO_5_G~ESDLO_17_G 
//|  V1.0_1R    09-26-2012       Liu Li          update some layers definition                        |
//|                                            update ESDLO.3b/3d/3f/3h/3j,ESDLO.4c,ESDLO.5,ESDLO.6,|   
//|                                                 ESDLO.8,ESDLO.16,LU_2_G,LU_4a_G,LU_4b_G         | 
//|                                             Add LU_1_N_G,LU_1_P_G,LU_3a_G~LU_3c_G               |
//|
//| V1.1=V0.0_0T 07-16-2012      Liu Li        Rename file and version number.                      | 
//|                                                                                                 |
//|                                                                                                 | 
//|	1.0	06-Jan-2011	Carolyn Y      DR update to 1T.					    |
//|												    |
//|*************************************************************************************************|
//| Old file name : SmicDR0T_cal40_log_sali_p1mt8_2tm_101825.esd				    |
//|     1.0.a   10-Aug-2010     Seek Rui       Initial Version					    |
//|                                                                                                 |
//|=================================================================================================|

//#################################################
//#     Environmental Variables Setting           #
//#################################################

/* 1. TOP Metal select. The value can be 10,9,8,7,6(include double top metals(TM1,TM2)).
  Default value is 10. M1~M8, TM1, TM2. */

#DEFINE TOPMETAL 10




//|=======================================
//|               Header   
//|=======================================

LAYOUT PATH "xxx.gds"
LAYOUT PRIMARY "topcell" 
LAYOUT SYSTEM GDSII 

DRC SUMMARY REPORT "esd_CAL.SUM" HIER 
DRC RESULTS DATABASE "esd_CAL.OUT" ASCII 

LAYER	AAO		10
LAYER   NWO             14
LAYER	MAP	29  DATATYPE 0  290
LAYER	DGO		290
LAYER	MAP	125  DATATYPE 0  1250
LAYER   TGO             1250
LAYER	GT		30
LAYER	SN		40
LAYER	SP		43
LAYER MAP 48 DATATYPE 0 4800
LAYER SAB0      4800
LAYER	CT		50
LAYER	M1		61
LAYER	M2		62

LAYER	M3		63
LAYER	M4		64
LAYER	M5		65
LAYER   M6              66
LAYER   M7              67
LAYER   M8              68
LAYER   TV1             121  
LAYER   TV2             123
LAYER   TM1             120
LAYER   TM2i             122
LAYER	MAP		133  DATATYPE 3   1000
LAYER	ESDIO2	1000 
LAYER	MAP		133  DATATYPE 1   1001
LAYER	ESDHV	1001
LAYER	MAP		133  DATATYPE 2   1002
LAYER	POWRING	1002
LAYER	ESDO 	        41 
LAYER	PAD		80  
LAYER	EXCLU     	132
LAYER   CAPBP           137
LAYER MAP	95 DATATYPE 0 950
LAYER RESNW     950     // Dummy Pattern for NWELL Resistor
LAYER MAP	96 DATATYPE 0 960
LAYER RESP1     960     // Dummy Pattern for Poly-1 Resistor
LAYER MAP	97 DATATYPE 0 970
LAYER RESAA     970     // Dummy Pattern for AA Resistor
LAYER   MAP             125  DATATYPE 1   1251
LAYER   TGV          1251
LAYER   MAP	125  DATATYPE 3   1253
LAYER   OVERDR	1253
LAYER MAP       83 DATATYPE 0 830
LAYER ALPA      830
LAYER MAP 130 DATATYPE 0 1300
LAYER MD	1300

LAYER MAP       70 DATATYPE 0 700
LAYER V1i	700
LAYER MAP	70 DATATYPE 1 701
LAYER V1DUMi	701
LAYER MAP	70 DATATYPE 6 706
LAYER MAP       71 DATATYPE 0 710
LAYER V2i	710
LAYER MAP	71 DATATYPE 1 711
LAYER V2DUMi	711
LAYER MAP       72 DATATYPE 0 720
LAYER V3i	720
LAYER MAP	72 DATATYPE 1 721
LAYER V3DUMi	721
LAYER MAP	73 DATATYPE 0 730
LAYER V4i	730
LAYER MAP	73 DATATYPE 1 731
LAYER V4DUMi	731
LAYER MAP	74 DATATYPE 0 740
LAYER V5i	740
LAYER MAP	74 DATATYPE 1 741
LAYER V5DUMi	741
LAYER MAP       75 DATATYPE 0 750
LAYER V6i	750
LAYER MAP	75 DATATYPE 1 751
LAYER V6DUMi	751
LAYER MAP       76 DATATYPE 0 760
LAYER V7i	760	
LAYER MAP	76 DATATYPE 1 761
LAYER V7DUMi	761
LAYER   V1DOP   7070
LAYER   MAP     71   DATATYPE 7 7170
LAYER   V2DOP   7170
LAYER   MAP     72   DATATYPE 7 7270
LAYER   V3DOP   7270
LAYER   MAP     73   DATATYPE 7 7370
LAYER   V4DOP   7370
LAYER   MAP     74   DATATYPE 7 7470
LAYER   V5DOP   7470
LAYER   MAP     75   DATATYPE 7 7570
LAYER   V6DOP   7570
LAYER   MAP     76   DATATYPE 7 7670
LAYER   V7DOP   7670
LAYER MAP       231 DATATYPE 0 2310
LAYER MTT2i     2310
LAYER MAP       216 DATATYPE 150 516
LAYER LDBK      516 



LAYER	GT_TXT  	164
LAYER	M1_TXT  	141
LAYER	M2_TXT	        142
LAYER	M3_TXT  	143
LAYER	M4_TXT  	144
LAYER	M5_TXT  	145
LAYER   M6_TXT          146
LAYER   M7_TXT          147
LAYER   M8_TXT          148
LAYER   MAP             122  DATATYPE 3   1003
LAYER   TM1_TXT         1003
LAYER   MAP             122  DATATYPE 3   1004
LAYER   TM2_TXT         1004


#IFDEF TOPMETAL 10
TEXT LAYER GT_TXT M1_TXT M2_TXT M3_TXT M4_TXT M5_TXT M6_TXT M7_TXT M8_TXT TM1_TXT TM2_TXT
ATTACH M1_TXT M1
ATTACH M2_TXT M2
ATTACH M3_TXT M3
ATTACH M4_TXT M4
ATTACH M5_TXT M5
ATTACH M6_TXT M6
ATTACH M7_TXT M7
ATTACH M8_TXT M8
ATTACH TM1_TXT TM1
ATTACH TM2_TXT TM2
#ENDIF
#IFDEF TOPMETAL 9
TEXT LAYER GT_TXT M1_TXT M2_TXT M3_TXT M4_TXT M5_TXT M6_TXT M7_TXT TM1_TXT TM2_TXT
ATTACH M1_TXT M1
ATTACH M2_TXT M2
ATTACH M3_TXT M3
ATTACH M4_TXT M4
ATTACH M5_TXT M5
ATTACH M6_TXT M6
ATTACH M7_TXT M7
ATTACH TM1_TXT TM1
ATTACH TM2_TXT TM2
#ENDIF
#IFDEF TOPMETAL 8
TEXT LAYER GT_TXT M1_TXT M2_TXT M3_TXT M4_TXT M5_TXT M6_TXT TM1_TXT TM2_TXT
ATTACH M1_TXT M1
ATTACH M2_TXT M2
ATTACH M3_TXT M3
ATTACH M4_TXT M4
ATTACH M5_TXT M5
ATTACH M6_TXT M6
ATTACH TM1_TXT TM1
ATTACH TM2_TXT TM2
#ENDIF
#IFDEF TOPMETAL 7
TEXT LAYER GT_TXT M1_TXT M2_TXT M3_TXT M4_TXT M5_TXT TM1_TXT TM2_TXT
ATTACH M1_TXT M1
ATTACH M2_TXT M2
ATTACH M3_TXT M3
ATTACH M4_TXT M4
ATTACH M5_TXT M5
ATTACH TM1_TXT TM1
ATTACH TM2_TXT TM2
#ENDIF
#IFDEF TOPMETAL 6
TEXT LAYER GT_TXT M1_TXT M2_TXT M3_TXT M4_TXT TM1_TXT TM2_TXT
ATTACH M1_TXT M1
ATTACH M2_TXT M2
ATTACH M3_TXT M3
ATTACH M4_TXT M4
ATTACH TM1_TXT TM1
ATTACH TM2_TXT TM2
#ENDIF


AAI =COPY AAO 
NWI =COPY NWO 
NW = NWI OUTSIDE RESNW
DG =COPY DGO 
TG =COPY TGO 
POLYI =COPY GT
Res1 = RESP1 OR RESAA
Res2 = Res1 OR RESNW
AA = AAI OUTSIDE Res2
POLY = POLYI OUTSIDE Res1
NIMP =COPY SN 
PIMP =COPY SP 
SAB =COPY SAB0 
CONT =COPY CT 
ESD1 =COPY ESDO
ESDDUM5V =COPY ESDHV
V1 =( V1i OR V1DUMi) OR V1DOP  
V2 =( V2i OR V2DUMi) OR V2DOP  
V3 =( V3i OR V3DUMi) OR V3DOP
V4 =( V4i OR V4DUMi) OR V4DOP
V5 =( V5i OR V5DUMi) OR V5DOP 
V6 =( V6i OR V6DUMi) OR V6DOP
V7 =( V7i OR V7DUMi) OR V7DOP
TM2= TM2i OR MTT2i

//=================
// Derived Layers
//=================

ESD = ESD1 AND ESDIO2
EPOLY = POLY AND ESDIO2
NDIF = AA AND NIMP
PDIF = AA AND PIMP

GATE0 = (GT INTERACT CT) AND AA   
AA1 = (AA INTERACT GATE0) NOT GATE0 
AA2 = AA1 TOUCH GATE0 > 1  
AA3 = AA1 INTERACT CT     
AA4 = AA2 OR AA3   
GATE1 = GATE0 TOUCH AA4 == 2  
GATE2 = GATE0 AND LDBK  
GATE = GATE1 OR GATE2
PGATE   =  GATE AND SP 
NGATE   = SN AND GATE


ENDIF = NDIF AND ESDIO2
EPDIF = PDIF AND ESDIO2
EDIF = ENDIF OR EPDIF
ENACT = ENDIF NOT NW
EPACT = EPDIF AND NW

NTAP  = NDIF AND NW
PTAP  = PDIF NOT NW
NACT  = NDIF NOT NW
PACT  = PDIF AND NW
NMOS  = NACT ENCLOSE GATE
PMOS  = PACT ENCLOSE GATE

ENTAP = ENDIF NOT ENACT
EPTAP = EPDIF NOT EPACT
ENGUARDRING = HOLES ENTAP INNER EMPTY
EPGUARDRING = HOLES EPTAP INNER EMPTY
ENGUARDHOLE_2 = (HOLES ENTAP INNER) ENCLOSE EPGUARDRING
EPGUARDHOLE_2 = (HOLES EPTAP INNER) ENCLOSE ENGUARDRING


EGATE = GATE AND ESDIO2
ENGATE = EGATE AND ENACT
EPGATE = EGATE AND EPACT
EGATEHV = (EGATE AND TG) NOT DG
EGATEMV = (EGATE AND DG) NOT TG
EGATELV = (EGATE NOT DG) NOT TG


ENGATEHV = EGATEHV AND ENACT
EPGATEHV = EGATEHV AND EPACT
ENGATEMV = EGATEMV AND ENACT
EPGATEMV = EGATEMV AND EPACT
ENGATELV = EGATELV AND ENACT
EPGATELV = EGATELV AND EPACT

EGATE5V = ESDHV AND EGATE
EGATE5V_OVLAP = EGATE5V CUT ESAB
EGATE5V_INSID = EGATE5V INSIDE ESAB

EGATE_W = EPOLY INSIDE EDGE EDIF
EGATE_L = EDIF INSIDE EDGE EPOLY

ENGATE_W = EPOLY INSIDE EDGE ENACT
ENGATE_L = ENACT INSIDE EDGE EPOLY

EPGATE_W = EPOLY INSIDE EDGE EPACT
EPGATE_L = EPACT INSIDE EDGE EPOLY

EGATEHV_W = EGATE_W COIN EDGE EGATEHV
EGATEHV_L = EGATE_L COIN EDGE (EGATEHV NOT TGV)

EGATEHV_OD_W = EGATE_W COIN EDGE (EGATEHV AND OVERDR)
EGATEHV_OD_L = EGATE_L COIN EDGE (EGATEHV AND OVERDR)

EGATEHV_UD_W = EGATE_W COIN EDGE (EGATEHV AND TGV)
EGATEHV_UD_L = EGATE_L COIN EDGE (EGATEHV AND TGV)

EGATEMV_W = EGATE_W COIN EDGE EGATEMV
EGATEMV_L = EGATE_L COIN EDGE EGATEMV

EGATELV_W = EGATE_W COIN EDGE EGATELV
EGATELV_L = EGATE_L COIN EDGE EGATELV

ENMOS = ENACT ENCLOSE EGATE
EPMOS = EPACT ENCLOSE EGATE
ENMOSLV = ENACT ENCLOSE EGATELV
EPMOSLV = EPACT ENCLOSE EGATELV
ENMOSMV = ENACT ENCLOSE EGATEMV
EPMOSMV = EPACT ENCLOSE EGATEMV
ENMOSHV = ENACT ENCLOSE EGATEHV
EPMOSHV = EPACT ENCLOSE EGATEHV

ENMOS_SD = ENMOS NOT EGATE
EPMOS_SD = EPMOS NOT EGATE
ECO = AA AND ( CONT AND ESDIO2)

ESAB = SAB AND ESDIO2
ESAB_HO = HOLES ESAB INNER EMPTY
ECO_SRC = ECO OUTSIDE ESAB_HO
ECO_DRN = ECO INSIDE ESAB_HO

ENMOS_SRC = ENMOS_SD ENCLOSE ECO_SRC
ENMOS_DRN = ENMOS_SD ENCLOSE ECO_DRN

EPMOS_SRC = EPMOS_SD ENCLOSE ECO_SRC
EPMOS_DRN = EPMOS_SD ENCLOSE ECO_DRN

EMOS_SRC = ENMOS_SRC OR EPMOS_SRC
EMOS_DRN = ENMOS_DRN OR EPMOS_DRN

EPOLYFINGERA = EPOLY INTERACT EGATE >= 2
EPOLYFINGERB = INT EPOLYFINGERA < 0.01 ABUT==90 REGION
EPOLYFINGER = EPOLYFINGERA INTERACT EPOLYFINGERB >= 4
EGATE_FW = EGATE_W COIN EDGE EPOLYFINGER

ENGATELVF = ENGATELV AND EPOLYFINGER
EPGATELVF = EPGATELV AND EPOLYFINGER
ENGATEMVF = ENGATEMV AND EPOLYFINGER
EPGATEMVF = EPGATEMV AND EPOLYFINGER
ENGATEHVF = ENGATEHV AND EPOLYFINGER
EPGATEHVF = EPGATEHV AND EPOLYFINGER
ENGATEHVF_UD = (ENGATEHV AND TGV) AND EPOLYFINGER
EPGATEHVF_UD = (EPGATEHV AND TGV) AND EPOLYFINGER
ENGATEHVF_OD = (ENGATEHV AND OVERDR) AND EPOLYFINGER
EPGATEHVF_OD = (EPGATEHV AND OVERDR) AND EPOLYFINGER


CONNECT ENGATELVF EPOLYFINGER 
CONNECT EPGATELVF EPOLYFINGER
CONNECT ENGATEMVF EPOLYFINGER 
CONNECT EPGATEMVF EPOLYFINGER
CONNECT ENGATEHVF EPOLYFINGER
CONNECT EPGATEHVF EPOLYFINGER
CONNECT ENGATEHVF_UD EPOLYFINGER
CONNECT EPGATEHVF_UD EPOLYFINGER
CONNECT ENGATEHVF_OD EPOLYFINGER
CONNECT EPGATEHVF_OD EPOLYFINGER
CONNECT EPOLYFINGER M1 BY CT

ESD3C = EXPAND EDGE (EGATE INSIDE EDGE AA) INSIDE BY 0.001

//================
//I/O ESD Guidance
//================

// Skip ESD.Guidance.1 : Finger-type structure with uniform finger width is suggested for N/P MOS ESD protection design

ESDLO_2_G {
  @ESD.Guidance.2_a : Unit finger width of NMOS and PMOS for I/O buffer, and Vdd-to-Vss protection is between 10 and 45
  @ESD.Guidance.2_b : Unit finger width of NMOS and PMOS for I/O buffer, and Vdd-to-Vss protection is between 10 and 45
  PATH LENGTH EGATE_FW < 10
  PATH LENGTH EGATE_FW > 45
}

ESDLO_3a_G {
  @ESD.Guidance.3_a : Min channel width of 0.9/1.0/1.1/1.2V NMOS for I/O buffer and Vdd to Vss protection is 300
 X = ENGATELVF AND ESD3C
 Y = NET AREA RATIO X < 300 [AREA(X)/0.002]
 ENGATELVF INTERACT Y   
}
ESDLO_3b_G {
  @ESD.Guidance.3_b : Min channel width of 0.9/1.0/1.1/1.2V PMOS for I/O buffer and Vdd to Vss protection is 450
 X = EPGATELVF AND ESD3C
 Y = NET AREA RATIO X < 450 [AREA(X)/0.002]
 EPGATELVF INTERACT Y  
 }
ESDLO_3c_G {
  @ESD.Guidance.3_a : Min channel width of 1.8V NMOS for I/O buffer and Vdd to Vss protection is 300
 X = ENGATEMVF AND ESD3C
 Y = NET AREA RATIO X < 300 [AREA(X)/0.002]
 ENGATEMVF INTERACT Y  

  }
ESDLO_3d_G {
  @ESD.Guidance.3_b : Min channel width of 1.8V PMOS for I/O buffer and Vdd to Vss protection is 450
X = EPGATEMVF AND ESD3C
 Y = NET AREA RATIO X < 450 [AREA(X)/0.002]
 EPGATEMVF INTERACT Y  
 }

ESDLO_3e_G{
  @ESD.Guidance.3_a : Min channel width of 2.5V NMOS for I/O buffer and Vdd to Vss protection is 300
 X = ENGATEHVF AND ESD3C
 Y = NET AREA RATIO X < 300 [AREA(X)/0.002]
 ENGATEHVF INTERACT Y 
 }
ESDLO_3f_G {
  @ESD.Guidance.3_b : Min channel width of 2.5V PMOS for I/O buffer and Vdd to Vss protection is 450
 X = EPGATEHVF AND ESD3C
 Y = NET AREA RATIO X < 450 [AREA(X)/0.002]
 EPGATEHVF INTERACT Y 
}

ESDLO_3g_G {
  @ESD.Guidance.3_a : Min channel width of 2.5V Over Drive NMOS for I/O buffer and Vdd to Vss protection is 300
  X = ENGATEHVF_OD AND ESD3C
 Y = NET AREA RATIO X < 300 [AREA(X)/0.002]
 ENGATEHVF_OD INTERACT Y 
}
ESDLO_3h_G {
  @ESD.Guidance.3_b : Min channel width of 2.5V Over Drive PMOS for I/O buffer and Vdd to Vss protection is 450
 X = EPGATEHVF_OD AND ESD3C
 Y = NET AREA RATIO X < 450 [AREA(X)/0.002]
 EPGATEHVF_OD INTERACT Y 
}

ESDLO_3i_G {
  @ESD.Guidance.3_a : Min channel width of 2.5V Under Drive NMOS for I/O buffer and Vdd to Vss protection is 300
  X = ENGATEHVF_UD AND ESD3C
 Y = NET AREA RATIO X < 300 [AREA(X)/0.002]
 ENGATEHVF_UD INTERACT Y 

  }
ESDLO_3j_G {
  @ESD.Guidance.3_b : Min channel width of 2.5V Under Drive PMOS for I/O buffer and Vdd to Vss protection is 450
  X = EPGATEHVF_UD AND ESD3C
 Y = NET AREA RATIO X < 450 [AREA(X)/0.002]
 EPGATEHVF_UD INTERACT Y
}


ESDLO_4a_G {
  @ESD.Guidance.4_a : Gate Length For Protection Device Minimum Length for 0.9/1.0/1.1/1.2V I/O N/PMOS is 0.07um
  PATH LENGTH EGATELV_L < 0.07
}

ESDLO_4b_G {
  @ESD.Guidance.4_b : Gate Length For Protection Device Minimum Length for 1.8V I/O N/PMOS is 0.20um
  PATH LENGTH EGATEMV_L < 0.20
}

ESDLO_4c_G {
  @ESD.Guidance.4_c : Gate Length For Protection Device Minimum Length for 2.5V I/O N/PMOS is 0.3um
  PATH LENGTH EGATEHV_L < 0.3
}

ESDLO_4d_G {
  @ESD.Guidance.4_d : Gate Length For Protection Device Minimum Length for 2.5V Over Drive I/O N/PMOS is 0.55um
  PATH LENGTH EGATEHV_OD_L < 0.55
}

ESDLO_4e_G {
  @ESD.Guidance.4_e : Gate Length For Protection Device Minimum Length for 2.5V Under Drive I/O N/PMOS is 0.24um
  PATH LENGTH EGATEHV_UD_L < 0.24
}

DISCONNECT

ESDLO_5_G {
  @ESD.Guidance.5 : Minimum Space from poly edge to CT edge on source side (SCP) for NMOS is 0.25um
  EXT EGATE_W ECO_SRC < 0.250 ABUT<90 REGION
}

ESDLO_6_G {
  @ESD.Guidance.6 : Minimum Space from poly edge to CT edge on drain side (DCP) for NMOS and PMOS is 1.3um
  X = COPY EPOLY 
  Y = X INSIDE EDGE EDIF
  EXT Y ECO_DRN < 1.3 ABUT<90 REGION
}

ESDLO_7a_G {
  @ESD.Guidance.7_a : SAB should block at least on drain side of NMOS and PMOS
  EGATE NOT INTERACT ESAB
}

ESDLO_7b_G {
  @ESD.Guidance.7_b : Contact should be kept silicided
  ECO AND ESAB 
} 

ESDLO_8_G {
  @ESD.Guidance.8 : Minimum width of SAB on drain side (A) for N/PMOS used as an I/O device is 1.1um
  X = COPY EPOLY 
  Y = (X INSIDE EDGE EDIF) NOT COIN EDGE EMOS_SRC
  EXT ESAB_HO Y < 1.1 ABUT<90 REGION
}


// Notice!
// In rule check ESD.Guidance.8, DRC can not cover B>=A check, please be careful when you check this rule.

ESDLO_9_a_G {
  @ESD.Guidance.9 : ESD protection should be surrounded by appropriate pick-up ring
  NOT ENGATE EPGUARDRING
}
ESDLO_9_b_G {
  @ESD.Guidance.9 : ESD protection should be surrounded by appropriate pick-up ring
  NOT EPGATE ENGUARDRING
}

// Skip ESD.Guidance.10 : The NMOS/PMOS can be added after resistor R as the secondary ESD protection for better ESD immunity if there is no conflict with circuit operation

// Skip ESD.Guidance.11 : Min value of I/O resister R is 200

// Skip ESD.Guidance.12 : The suggested device size for secondary protection are 20/0.35 and 20/0.28

//EGATEHV_OVLAP = EGATEHV CUT ESAB
//EGATEMV_OVLAP = EGATEMV CUT ESAB

EGATE_OVLAP = EGATE CUT ESAB
EGATEHV_OVLAP = EGATEHV CUT ESAB
EGATEMV_OVLAP = EGATEMV CUT ESAB
EGATELV_OVLAP = EGATELV CUT ESAB

ESDLO_14a_G {
  @ESD.Guidance.14a : The minimum overlap (Sd) of the SAB mask over the drain of 1.8V/2.5V I/O I/O N/PMOS region should be larger than 0.05
  INT EGATEHV_OVLAP ESAB < 0.05 ABUT<90 SINGULAR REGION
  INT EGATEMV_OVLAP ESAB < 0.05 ABUT<90 SINGULAR REGION
}

ESDLO_14b_G {
  @ESD.Guidance.14b : The minimum overlap (Sd) of the SAB mask over the drain of 1.0V/1.2V N/PMOS region should be larger than 0.02
  INT EGATELV_OVLAP ESAB < 0.02 ABUT<90 SINGULAR REGION
}

ESDLO_15_G {
  @ESD.Guidance.15 : For high voltage tolerant I/O using cascaded 1.8/2.5V NMOS, ESD implant is required
  ESDDUM5V NOT ESD
}

ESDLO_16_G {
@ESD.Guidance.16: Spacing between active poly gate and inactive poly gate of Cascaded NMOS should be <= 0.6um
   EXT EGATE5V_OVLAP EGATE5V_INSID > 0.6 <10 ABUT <90 OPPOSITE REGION EXCLUDE SHIELDED 
}

ESDLO_17a_G {
  @ESD.Guidance.17 : For high tolerant I/O designed by 2.5/3.3V NMOS or PMOS, SAB needs to cover all top poly gates and extend to overlap the second poly gate by 0.05
  INT EGATE5V_OVLAP ESAB < 0.05 ABUT<90 SINGULAR REGION
}

EGATE5V_OVLAPSAB = EGATE5V_OVLAP AND ESAB
ESDLO_17b_G {
  @ESD.Guidance.17 : For high tolerant I/O designed by 2.5/3.3V NMOS or PMOS, SAB needs to cover all top poly gates and extend to overlap the second poly gate by 0.05
  EGATE5V NOT INTERACT ESAB
}







CONNECT M1 EMOS_DRN BY ECO_DRN
CONNECT M1 ENMOS_SD BY ECO
CONNECT M1 EPMOS_SD BY ECO
CONNECT M1  ECO
CONNECT M1 M2 BY V1
CONNECT M2 M3 BY V2
CONNECT M3 M4 BY V3
#IFDEF TOPMETAL 10
CONNECT M4 M5 BY V4
CONNECT M5 M6 BY V5
CONNECT M6 M7 BY V6
CONNECT M7 M8 BY V7
CONNECT M8 TM1 BY TV1

#ENDIF
#IFDEF TOPMETAL 9
CONNECT M4 M5 BY V4
CONNECT M5 M6 BY V5
CONNECT M6 M7 BY V6
CONNECT M7 TM1 BY TV1
#ENDIF
#IFDEF TOPMETAL 8
CONNECT M4 M5 BY V4
CONNECT M5 M6 BY V5
CONNECT M6 TM1 BY TV1
#ENDIF
#IFDEF TOPMETAL 7
CONNECT M4 M5 BY V4
CONNECT M5 TM1 BY TV1
#ENDIF
#IFDEF TOPMETAL 6
CONNECT M4 TM1 BY TV1
#ENDIF
CONNECT TM1 TM2 BY TV2
CONNECT TM2 ALPA BY PAD
CONNECT ALPA MD


//==================
// Latch-up rules
//==================

ENMOS_OV = SIZE ENMOS BY 5.000
EPMOS_OV = SIZE EPMOS BY 5.000

LU_1_N_G{
@ Additional guard-ring* is recommended if no conflict with circuit and layout constrains.
ENGATE NOT ENGUARDHOLE_2
}
LU_1_P_G{
@ Additional guard-ring* is recommended if no conflict with circuit and layout constrains.
EPGATE NOT EPGUARDHOLE_2
}

LU_2_G {
@ Minimum guard-ring width is 0.3um
INT ENTAP <0.3 ABUT<90 SINGULAR REGION
INT EPTAP<0.3 ABUT<90 SINGULAR REGION
}

PMOS1 = (PMOS NOT INTERACT CAPBP) NOT INTERACT RESP1



LU_3a_G {
@ For core N/PMOS connected to an I/O pad
@ Spacing between NMOS and PMOS is S1>= 3um
 EXT ((EPDIF INTERACT EPMOS_SD_PAD) NOT (DG OR TG)) ((ENDIF INTERACT ENMOS_SD_PAD) NOT (DG OR TG)) <3  ABUT<90 SINGULAR REGION
}
LU_3b_G {
@For 1.8V N/PMOS connected to an I/O pad
@Spacing between 1.8V NMOS and 1.8V/Core PMOS and 
@Spacing between 1.8V PMOS and 1.8V/Core NMOS S1>= 5um
 EXT ((ENDIF INTERACT ENMOS_SD_PAD) AND (DG NOT TG)) ((EPDIF INTERACT EPMOS_SD_PAD) NOT TG) <5  ABUT<90 SINGULAR REGION  
 EXT ((EPDIF INTERACT EPMOS_SD_PAD) AND (DG NOT TG)) ((ENDIF INTERACT ENMOS_SD_PAD) NOT TG) <5  ABUT<90 SINGULAR REGION
}
ENMOS_SD_PAD = NET AREA RATIO ENMOS_SD MD > 0
EPMOS_SD_PAD = NET AREA RATIO EPMOS_SD MD > 0

 
LU_3c_G {
@For 2.5V N/PMOS connected to an I/O pad
@Spacing between 2.5V NMOS and 2.5V/Core PMOS and 
@Spacing between 2.5V PMOS and 2.5V/Core NMOS	S1>= 7um
EXT ((ENDIF INTERACT ENMOS_SD_PAD) AND TG) ((EPDIF INTERACT EPMOS_SD_PAD) NOT (DG NOT TG)) <7  ABUT<90 SINGULAR REGION
EXT ((EPDIF INTERACT EPMOS_SD_PAD) AND TG) ((ENDIF INTERACT ENMOS_SD_PAD) NOT (DG NOT TG)) <7  ABUT<90 SINGULAR REGION

}


LU_4a_G {
  @Latchup.Guidance.3_a : Maximum space from any point within Source/Drain region to the nearest pickup AA region inside the same well for I/O and internal circuits is 35um
  A = INT NTAP < 0.005 ABUT INTERSECTING ONLY REGION
  B = (NTAP INTERACT A) NOT A
  C = INT B < 0.001 ABUT INTERSECTING ONLY REGION
  D = (B INTERACT C) NOT C
  NW_MINUS_NTAP = SIZE D BY 35 INSIDE OF NW STEP 0.33
  X = PMOS1 AND NW
  OUTSIDE EDGE X NW_MINUS_NTAP
}

NMOS1 = (NMOS NOT INTERACT CAPBP) NOT INTERACT RESP1
BULK = EXTENT
PW = BULK NOT NW

LU_4b_G {
  @Latchup.Guidance.3_b : Maximum space from any point within Source/Drain region to the nearest pickup AA region inside the same well for I/O and internal circuits is 35um
  A = INT PTAP < 0.005 ABUT INTERSECTING ONLY REGION
  B = (PTAP INTERACT A) NOT A
  C = INT B < 0.001 ABUT INTERSECTING ONLY REGION
  D = (B INTERACT C) NOT C
  PW_MINUS_PTAP = SIZE D BY 35 INSIDE OF PW STEP 0.33
  X = NMOS1 AND PW
  OUTSIDE EDGE X PW_MINUS_PTAP
}
/* Skip Latchup.Guidance.4 ~ 9 */













