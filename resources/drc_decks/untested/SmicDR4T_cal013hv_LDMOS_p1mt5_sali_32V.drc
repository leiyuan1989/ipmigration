//$Revision: 1.0
//$Date: 2010/04/14 09:24:54 $
//=================================================================================
//|                                                                               |
//|  0.13um HV 1P5M Calibre DRC rule file for                                     |
//|                                                                               |
//|       SMIC:  0.13um HV LDMOS 1P6M (1P5M,1P4M) Salicide 1.5V/5.5V(6V)/32V      |
//|		          (Isolated) LL Design Rule (Version B)	                  |
//|       Doc. No.:       TD-HV13-DR-2001         Rev.: 4 T                       |
//|                                                                               |
//|       SMIC DSD Technologies  		                                  |
//|                                                                               |
//|===============================================================================|
//|                                                                               |
//|    DISCLAIMER                                                                 |
//|                                                                               |
//|   SMIC hereby provides the quality information to you but makes no claims,    |
//| promises or guarantees about the accuracy, completeness, or adequacy of the   |
//| information herein. The information contained herein is provided on an "AS IS"|
//| basis without any warranty, and SMIC assumes no obligation to provide support |
//| of any kind or otherwise maintain the information.                            |
//|                                                                               |
//|   SMIC disclaims any representation that the information does not infringe any|
//| intellectual property rights or proprietary rights of any third parties.SMIC  |
//| makes no other warranty, whether express, implied or statutory as to any      |
//| matter whatsoever, including but not limited to the accuracy or sufficiency of|
//| any information or the merchantability and fitness for a particular purpose.  |
//| Neither SMIC nor any of its representatives shall be liable for any cause of  |
//| action incurred to connect to this service.                                   |
//|                                                                               |
//|===============================================================================|
//|                                                                               |
//|    STATEMENT OF USE AND CONFIDENTIALITY                                       |
//|                                                                               |
//|   The following/attached material contains confidential and proprietary       |
//| information of SMIC. This material is based upon information which SMIC       |
//| considers reliable, but SMIC neither represents nor warrants that such        |
//| information is accurate or complete, and it must not be relied upon as such.  |
//| This information was prepared for informational purposes and is for the use by|
//| SMIC's customer only. SMIC reserves the right to make changes in the          |
//| information at any time without notice.                                       |
//|   No part of this information may be reproduced, transmitted, transcribed,    |
//| stored in a retrieval system, or translated into any human or computer        |
//| language, in any form or by any means, electronic, mechanical, magnetic,      |
//| optical, chemical, manual, or otherwise, without the prior written consent of |
//| SMIC. Any unauthorized use or disclosure of this material is strictly         |
//| prohibited and may be unlawful. By accepting this material, the receiving     |
//| party shall be deemed to have acknowledged, accepted, and agreed to be bound  |
//| by the foregoing limitations and restrictions. Thank you.                     |
//|                                                                               |
//|===============================================================================|
//|                                                                               |
//|   This file includes SVRF/TVF Technology under license by Mentor Graphics     | 
//| Corporation.  "SVRF/TVF Technology" shall mean Mentor Graphics' Standard      |
//| Verification Rule Format ("SVRF") and Tcl Verification Format ("TVF")         |
//| proprietary syntaxes for expressing process rules.  You shall not use SVRF/TVF|
//| Technology unless you are a Mentor Graphics customer with a license to use the|
//| applicable Mentor Graphics software. The exact terms of your obligations and  |
//| rights are governed by your respective license.  You shall not use SVRF/TVF   |
//| Technology except: (a) for your internal business purposes and (b) for use    |
//| with Mentor Graphics' Calibre tools. All SVRF/TVF Technology constitutes or   |
//| contains trade secrets and confidential information of Mentor Graphics or its |
//| licensors. You shall not make SVRF/TVF Technology available in any form to any|
//| person other than your employees and on-site contractors, excluding Mentor    |
//| Graphics competitors, whose job performance requires access and who are under |
//| obligations of confidentiality.                                               |
//|                                                                               |
//|===============================================================================|
//| Read Me First !                                                               |
//|                                                                               |
//| 1. Skip the following rules:                                                  |
//|       1)      Metal Fuse Rules                                                |
//|       2)      Mark.1~6                                                        |
//| 2. Please pay attention to "User Choice" block to make some useful choices    |
//|                                                                               |
//|===============================================================================|
//|                                                                               |
//| Revision History :                                                            |
//|                                                                               |
//|   rev         date           who              what                            |
//| -------  -------------  --------------  --------------------------------------|
//|   1.0     13-Apr-2010     Seek Rui       Initial version                      |
//|===============================================================================|


//*****************************************************************************************
//   User Choice !  ( Recommended to make some useful choices to speed DRC running time! )
//*****************************************************************************************


   DRC UNSELECT CHECK MIM_6b MIM_6c	//; Guidelines checking choice !
					//; Open the mark is NOT to check guidelines 
					//; MIM.6b, MIM.6c are guidelines, user can decide follow or not

   DRC UNSELECT CHECK DDNW_9	        //; "Recommend add P+ pickup guard ring between DDNW and NW/MVNW" rule checking choice !
					//; Open the mark is NOT to check DDNW_9 rule                 
					//; If user want to check DDNW_9 rule, mark this command

   DRC UNSELECT CHECK GPI               //; Guideline rules for Polymide opening rule
                                        //; customer can choice check or not
                                        //; DRC default is to uncheck

   DRC UNSELECT CHECK DDNW_10A DDNW_10B1 DDNW_10B2 DDNW_10C     //;recommend rule



//*************************************
//     DIRECTIVE SECTION  
//*************************************

LAYOUT PRIMARY "topcell" 
LAYOUT PATH "xxx.gds"
LAYOUT SYSTEM GDSII 

DRC RESULTS DATABASE "drc_CAL.OUT" ASCII 
DRC SUMMARY REPORT "drc_CAL.SUM" 

PRECISION 	1000
RESOLUTION 	1	

FLAG ACUTE YES
FLAG NONSIMPLE YES
//FLAG OFFGRID YES
FLAG SKEW YES

TEXT DEPTH PRIMARY 
LVS POWER NAME "?VDD?" "?VCC?" 


//************************************                
//    INPUT LAYER STATEMENTS
//************************************                

LAYER	AAi	10 12 13 //	Active Area / SDG
LAYER	NWi	14  	//	N-Well / N-Tub
LAYER   DNWi    19      //      Deep Nwell for LV and MV device(optional)  
LAYER   PWi     20      //      P-Well
LAYER	NCi	21	//	High Vt NMOS device
LAYER 	DGi	29  	//	Dual Gate
LAYER 	GTi	30  	//	Poly Gate, Poly-1, ONO Gate
LAYER   NLLi	35  	//	NMOS LDD Implant for Low VDD
LAYER   NLHi	36  	//	NMOS LDD Implant for High VDD
LAYER   PLHi    37  	//	PMOS LDD Implant for High VDD
LAYER   PLLi    38  	//	PMOS LDD Implant for Low VDD
LAYER   HRPi    39      //      High Resistance Poly
LAYER   SNi 	40  	//	N+ S/D Implant
LAYER   SPi 	43  	//	P+ S/D Implant
LAYER   SABi	48  	//	Resist Protect Oxide / Salicide block
LAYER   CTi  	50  	//	Contact Hole
LAYER   MIMi    58      //      Top Plate of MiM capacitor
LAYER   INST    60      //      Un-DRC region
LAYER   M1i  	61  	//	Metal-1
LAYER   M2i 	62  	//	Metal-2
LAYER   M3i 	63  	//	Metal-3
LAYER   M4i 	64  	//	Metal-4
LAYER   TM2i 	122  	//	Top Metal
LAYER   V1i  	70  	//	Via-1 Hole
LAYER   V2i 	71  	//	Via-2 Hole
LAYER   V3i 	72  	//	Via-3 Hole
LAYER   TV2i 	123  	//	Top Via Hole


LAYER   PAi 	80  	//	Passivation / Pad
LAYER   MAP 95 DATATYPE 0 195
LAYER   RESNW  195  	//	Dummy Pattern for NWELL Resistor
LAYER   RESP1  96  	//	Dummy Pattern for Poly-1 Resistor
LAYER   RESAA  97 	//	Dummy Pattern for AA Resistor
LAYER	OPCBA	100  	//	Blocking Layer for OPC operation on AA; Exclude SRAM array area during DRC
LAYER   NDRFi   103     //      HV NMOS drift implant
LAYER   PDRFi   104     //      HV PMOS drift implant
LAYER   PWHTi   109     //      Define HV Pwell,must be inside DDNW
LAYER   TGi     125     //      HV transistor gate oxide area  
LAYER   MAP 131 DATATYPE 1 231
LAYER   SUBD    231     //      dummy layer for define HV device area 
LAYER   MAP 122 DATATYPE 3 320
LAYER   TM2TXT    320     //    TM2 Text layer

LAYER   PIi      82     //      polyimide
LAYER   EXCLU   132     //      Un-DRC region
LAYER   NODMF   180     //      Dummy layer to block 0.18um part if both 0.16um and 0.18um design exist together
LAYER	MAP 191 DATATYPE 1 1911
LAYER	MVDMY	1911	//
LAYER   SURNW   209     //      dummy layer for surrounding NW
LAYER   DSTR    138     //      diode marker
LAYER   DMPNP   134     //      dummy layer for BJT
LAYER   PSUBi    85     //      blocking layer for native device 
LAYER   DDNWi   217     //      Deep deep NW for 32/40V HV device version B process
LAYER   STIDMYi  215     //      on HV transistor Field plate
LAYER   HRPDMY  210      //      HRP dummy
LAYER   MIMDMY  211      //      recognize MIM used as dummy

EOI = (EXCLU OR INST) OR NODMF
PI      = PIi NOT EOI
PSUB    = PSUBi NOT EOI
DDNW    = DDNWi NOT EOI
DNW     = DNWi NOT EOI
NC	= NCi NOT EOI
AAO	= AAi NOT EOI                      
NWO	= NWi NOT EOI
PWX     = PWi NOT EOI
DG      = DGi NOT EOI
NW_DD   = NW OR DDNW
MVPW    = DG NOT NW_DD
GTO	= GTi NOT EOI
NLLO 	= NLLi NOT EOI
NLH 	= NLHi NOT EOI
PLH     = PLHi NOT EOI
PLLO	= PLLi NOT EOI
SNO  	= SNi NOT EOI
SPO  	= SPi NOT EOI
SAB 	= SABi NOT EOI
CTO  	= CTi NOT EOI
M1O  	= M1i NOT EOI
M2O  	= M2i NOT EOI
M3O  	= M3i NOT EOI
M4  	= M4i NOT EOI
TM2  	= TM2i NOT EOI
V1O  	= V1i NOT EOI
V2O  	= V2i NOT EOI
V3  	= V3i NOT EOI
TV2     = TV2i NOT EOI
PA  	= PAi NOT EOI

NDRF     =  NDRFi NOT EOI
PDRF     =  PDRFi NOT EOI
HRP     =   HRPi NOT EOI
MIM     =   MIMi NOT EOI
TG      =   TGi  NOT EOI
PWHT   =  PWHTi NOT EOI

STIDMY  = SIZE STIDMYi BY -0.13

TEXT	LAYER	61  141
TEXT	LAYER	62  142
TEXT	LAYER	63  143
TEXT	LAYER	64  144
TEXT    LAYER   65  145
TEXT    LAYER   120 320

ATTACH	141	M1i
ATTACH	142	M2i
ATTACH	143	M3i
ATTACH  144     M4i
ATTACH	320	TM2i
ATTACH	61	M1i
ATTACH	62	M2i
ATTACH	63	M3i
ATTACH  64      M4i
ATTACH	122	TM2i

DRC:1 = EXTENT
BULK = SIZE DRC:1 BY 1.0
LMV  = (NW OR (PW NOT DDNW)) OR (MVNW OR MVPW)
NWHT = DDNW NOT PWHT
NWHTi= (BULK AND DDNWi) NOT PWHTi
//PW  = ((BULK NOT DDNW)NOT NW)OR PWX
PW  = BULK NOT ((NW OR PSUB) OR NWHT)

CONNECT TM2i M4i BY TV2i
CONNECT M4i M3i BY V3i
CONNECT M3i M2i BY V2i
CONNECT M2i M1i BY V1i
CONNECT M1i GTi NTAPi PTAPi PSDi NSDi PACTi NACTi BY CTi
CONNECT NTAPi PSDi
CONNECT PTAPi NSDi
SCONNECT NTAPi NWi
SCONNECT NTAPi DDNWi
SCONNECT PTAPi PWHTi
LABEL ORDER  TM2i M4i M3i M2i M1i NTAPi PTAPi NSDi PSDi GTi NWi DDNWi PWHTi

VIRTUAL CONNECT NAME "?VDD?" "?VCC?" "?VSS?" "?GND?"
VIRTUAL CONNECT COLON YES


//*******************************************       
//    LAYER DERIVATIONS AND OPERATIONS
//*******************************************       

AA 	= NOT AAO OPCBA
NW	= NOT NWO OPCBA
GT 	= NOT GTO OPCBA
NLL	= NOT NLLO OPCBA
PLL	= NOT PLLO OPCBA
SN	= NOT SNO OPCBA
SP	= NOT SPO OPCBA
CT 	= NOT CTO OPCBA
M1 	= NOT M1O OPCBA
V1 	= NOT V1O OPCBA
M2	= NOT M2O OPCBA
V2	= NOT V2O OPCBA
M3	= NOT M3O OPCBA

//==================
// For Connectivity
//==================

PACTi   = SPi AND AAi
NACTi   = SNi AND AAi

PTAPi   = PACTi NOT (NWi OR (DDNWi NOT PWHTi))
NTAPi   = NACTi AND (NWi OR (DDNWi NOT PWHTi))

PGATEi  = PACTi AND GTi
NGATEi  = NACTi AND GTi

PSDi    = (PACTi NOT PGATEi) TOUCH PGATEi
NSDi    = (NACTi NOT NGATEi) TOUCH NGATEi

MVNW = NW AND DG
LVNW = NW NOT MVNW

PACT	= SP AND AA
NACT	= SN AND AA
ACT	= PACT OR NACT

LMOSP 	= PACT AND LVNW
LMOSN	= NACT AND (PW NOT DG)
MMOSP 	= PACT AND MVNW
MMOSN 	= NACT AND MVPW
MMOSP_55 = MMOSP NOT INTERACT MVDMY
MMOSP_60 = MMOSP INTERACT MVDMY
MMOSN_55 = MMOSN NOT INTERACT MVDMY
MMOSN_60 = MMOSN INTERACT MVDMY
HMOSP 	= (PACT AND NWHT) AND DDNW
HMOSN 	= (NACT AND PWHT) AND DDNW
LMMOSP  = LMOSP OR MMOSP
LMMOSN  = LMOSN OR MMOSN
MOSP    = (LMOSP OR MMOSP) OR HMOSP
MOSN    = (LMOSN OR MMOSN) OR HMOSN

HPSD = HMOSP NOT GT
HNSD = HMOSN NOT GT

LPTAP	= PACT AND (PW NOT DG)
LNTAP	= NACT AND LVNW
MPTAP	= PACT AND MVPW
MNTAP	= NACT AND MVNW
HPTAP   = (PACT AND PWHT) AND DDNW
HNTAP   = (NACT AND NWHT) AND DDNW
HPNTAP  = HPTAP OR HNTAP

LPGATE	= LMOSP AND GT
LNGATE	= LMOSN AND GT
MPGATE	= MMOSP AND GT
MNGATE	= MMOSN AND GT
HPGATE	= HMOSP AND GT
HNGATE	= HMOSN AND GT
LGATE   = LPGATE OR LNGATE
MGATE   = MPGATE OR MNGATE
HGATE   = HPGATE OR HNGATE

//HVGT    = (GT AND DDNW ) NOT INTERACT RESP1 
HVGT    = GT AND DDNW
HVCANAA = HMOSN INTERACT GT
HVCAPAA = HMOSP INTERACT GT
HVN_AA  = (NACT AND PWHT) NOT INTERACT GT
HVP_AA  = (PACT AND NWHT) NOT INTERACT GT
HV_AA   = ((AA AND DDNW) AND DG) NOT INTERACT TG
MV_AA  = ((MMOSP OR MMOSN) OR MPTAP) OR MNTAP
LV_AA  = AA NOT (HV_AA OR MV_AA)
LM_AA  = AA NOT HV_AA
LMPTAP = LPTAP OR MPTAP
LMNTAP = LNTAP OR MNTAP
PTAP   = LMPTAP OR HPTAP
NTAP   = LMNTAP OR HNTAP

LMMOSAA	= ((LMOSP OR LMOSN) OR MMOSP) OR MMOSN

GATE	= AA AND GT
PGATE   = PACT AND GT
NGATE   = NACT AND GT

PSD	= (AA NOT GATE) TOUCH PGATE
NSD	= (AA NOT GATE) TOUCH NGATE
SD	= PSD OR NSD
PBAA    = AA AND PSUB
PBGT    = GT INTERACT PSUB

NGATE_CAP = (NACT AND GT ) AND NW
MOSN_CAP  = NACT INTERACT NGATE_CAP
GT_MOSCAP = GT INTERACT NGATE_CAP
NSD_CAP   = (MOSN_CAP NOT GT_MOSCAP) TOUCH NGATE_CAP


NLLAA   = AA AND NLL
NLHAA   = AA AND NLH
PLLAA   = AA AND PLL
PLHAA   = AA AND PLH

INTERGT = GT NOT GATE

//NR	= (NW INSIDE AA) INTERACT RESNW
NR	= NW INTERACT RESNW
AAR 	= AA INTERACT NR
SNR 	= SN INTERACT NR

GTR	= GT INTERACT RESP1

NWDEV	= NW NOT NR 

NW_USIZE = SIZE (NW OR (DDNW NOT PWHT)) BY -0.387
PW_USIZE = SIZE PW  BY -0.387
ACT_CT   = CT AND ACT
INTERCT  = CT AND INTERGT

GT_HRP   = GT INTERACT HRP
HR_GT    = GT_HRP AND HRPDMY
SAB_GT   = GT_HRP AND SAB

//*********************************
//       HV Metal/Via Defina
//*********************************

HVCT = CT AND (HV_AA OR HVGT)
HVM1 = M1 INTERACT HVCT
HVM2 = NET AREA RATIO M2 HVM1 > 0
HVM3 = NET AREA RATIO M3 HVM1 > 0
HVM4 = NET AREA RATIO M4 HVM1 > 0
HVTM2 = NET AREA RATIO TM2 HVM1 > 0

HVV1 = V1 AND HVM1
HVV2 = V2 AND HVM2
HVV3 = V3 AND HVM3
HVTV2 = TV2 AND HVTM2

//********************************************
//         CHECK GROUP DEFINITIONS
//********************************************

GROUP GLG
  GNW
  GAA
  GDG
  GGT
  GPSUB
  GPWHT
  GTG
  GNDRF
  GPDRF
  GNLL
  GPLL
  GNLH
  GPLH
  GSN
  GSP
  GSAB
  GCT
  GM1
  GM2
  GM3
  GTM
  GV1
  GV2
  GTV
  GDDNW 
  GHRP 
  GMIM
  GPI 


//=====================
//  NW - Nwell DRC
//=====================

GROUP GNW NW_?

HOLE_INNW = HOLES NW
NW_HOLE = NW TOUCH HOLE_INNW
surnwell = NW_HOLE interact SURNW
nweldev = NW AND HOLE_INNW
nwsp_1 = ext surnwell (nweldev not DG) < 0.6 abut< 90 singular region
nwsp_2 = ext surnwell (nweldev and DG) < 0.6 abut< 90 singular region
nwsp_3 = ext surnwell (nweldev not DG) < 1.0 abut< 90 singular region
nwsp_4 = ext surnwell (nweldev and DG) < 1.8 abut< 90 singular region

NW_1 {
@ Minimum width of an NW region is 0.6um
  INT NW < 0.6 ABUT<90 SINGULAR REGION
}
NW_2 {
@ Minimum NW width for NW resistor is 1.6um
  INT NR < 1.6 ABUT<90 SINGULAR REGION
}
NW_3 {
@ Minimum space between 1.5V NW at same potential is 0.6um
  X = EXT LVNW < 0.6 ABUT<90 SINGULAR REGION CONNECTED
  X OR nwsp_1
}
NW_4 {
@ Minimum space between 1.5V NW at different potential is 1.0um
  X = EXT LVNW < 1.0 ABUT<90 SINGULAR REGION NOT CONNECTED
  X NOT nwsp_3
}
NW_6 {
@ Minimum space between 1.5V NW and 5.5V(6V) NW is 1.8um
  EXT LVNW MVNW < 1.8 ABUT<90 SINGULAR REGION
}
NW_7a {
@ Minimum space between two MV NW regions with the same potential is 0.6um
  X = EXT MVNW < 0.6 ABUT<90 SINGULAR REGION CONNECTED
  X OR nwsp_2
}
NW_7b {
@ Minimum space between two MV NW regions with the different potential is 1.8um
  X = EXT MVNW < 1.8 ABUT<90 SINGULAR REGION NOT CONNECTED
  X NOT nwsp_4
}
NW_8 {
@ Minimum NW area is 0.92
  AREA NW < 0.92
}



//==========================================
//  PSUB - Native NMOS blocked implant DRC
//==========================================

GROUP GPSUB PSUB_?

PSUB_1 {
@ Minimum PSUB width is 0.6
  INT PSUB < 0.6 ABUT<90 SINGULAR REGION
}
PSUB_2a {
@ Minimum channel length of 1.5V native device is 0.3um
  X = PBGT INSIDE EDGE (PBAA NOT DG)
  INT X < 0.3 ABUT<90 REGION 
}
PSUB_2b {
@ Minimum channel length of 5.5V native device is 1.5um
  X = PBGT INSIDE EDGE (PBAA INTERACT DG)
  INT X < 1.5 ABUT<90 REGION 
}
PSUB_3 {
@ Minimum space between PSUB with same potential is 0.6um
  EXT PSUB < 0.6 ABUT<90 SINGULAR REGION
}
PSUB_4 {
@ Fixed AA enclosure by PSUB region is 0.22um
  X1 = SIZE PBAA BY 0.22
  X2 = PSUB INTERACT PBAA
  X1 XOR X2
  PSUB NOT INTERACT AA
}
PSUB_5 {
@ Minimum space between PSUB and AA is 0.37um
  X1 = AA NOT PBAA
  EXT PSUB X1 < 0.52 ABUT<90 SINGULAR REGION
}
PSUB_6a {
@ Min space between 1.5V NMOS PSUB and NW is 1.2um
  X1 = PSUB NOT INTERACT DG
  EXT X1 NW < 1.2 ABUT<90 SINGULAR REGION
}
PSUB_6b {
@ min space between 5.5V NMOS PSUB and NW is 1.8um
  X1 = PSUB INTERACT DG
  EXT X1 NW < 1.8 ABUT<90 SINGULAR REGION
}
PSUB_7 {
@ Minimum Extension of Native NMOS poly gate outside of AA is 0.31um
  X = GT INTERACT (NGATE INSIDE PSUB)
  ENC AA X < 0.31 ABUT<90 SINGULAR REGION
}
PSUB_Note1 {
@ PSUB is to define area for native NMOS device on P-substrate, which is not NW or PW
  PSUB AND NW
}
PSUB_Note2 {
@ One AA shape per PSUB shape, except for NMOS capacitor
  X = PSUB INTERACT AA
  X NOT INTERACT AA == 1
}


//===========================
//  NC - High Vt NMOS device
//===========================

GROUP GNC NC_?

NC_1 {
@ Minimum NC width is 0.60um
  INT NC < 0.6 ABUT<90 SINGULAR REGION
}
NC_2 {
@ Minimum space between NCs is 0.6um
  EXT NC < 0.6 ABUT<90 SINGULAR REGION
}
NC_3 {
@ Distance between NC and NW is 0
  AND NC NW
}
NC_4 {
@ Minimum NC extension outside of N+AA along poly length is 0.18
  X = NACT TOUCH EDGE GATE
  ENC X NC < 0.18 ABUT<90 OPPOSITE
}
NC_5 {
@ Minimum space between NC and N+AA for nominal NMOS along poly length is 0.18
  X = NACT TOUCH EDGE (AA INSIDE EDGE GT)
  EXT X NC < 0.18 ABUT<90  REGION OPPOSITE
}
NC_6 {
@ Minimum NC extension outside of poly gate is 0.27
  X = GT INSIDE EDGE (NACT AND NC)
  Y = EXPAND EDGE X OUTSIDE BY 0.27
  Y NOT NC
}
NC_7 {
@ Space between NC and poly gate for nominal device is 0.27
  X = GT INSIDE EDGE (NACT NOT NC)
  EXT NC X < 0.27 ABUT<90 REGION OPPOSITE
  NC COIN EDGE GATE
}
NC_8 {
  @ No NC and PSUB overlap is allowed
  NC AND PSUB
}

//===========================
//  PWHT - HV Pwell area DRC
//===========================

GROUP GPWHT PWHT_?

PWHT_1 {
@ Minimum width an PWHT region is 5.0
  INT PWHT < 5.0 ABUT<90 SINGULAR REGION
}
PWHT_2a {
@ Minimum space between two PWHT regions with the same potential is 5.0
@ Merge if space is less than 5.0um
  EXT PWHT < 5.0 ABUT<90 SINGULAR REGION CONNECTED
}
PWHT_2b {
@ Minimum space between two PWHT regions with the different potential is 6.0um 
  EXT PWHT < 6.0  ABUT<90 SINGULAR REGION NOT CONNECTED
}
PWHT_3 {
@ Min space of PWHT enclosure N+ AA in PWHT is 5.50um
  ENC HMOSN PWHT < 5.50 ABUT<90 SINGULAR REGION
}
PWHT_4 {
@ Min space of PWHT enclosure P+AA in PWHT is 3.5
  ENC HPTAP PWHT < 3.5 ABUT<90 SINGULAR REGION
}
PWHT_5 {
@ Min space of PWHT to P+ AA outside PWHT is 6.5
  EXT HMOSP PWHT < 6.5 ABUT<90 SINGULAR REGION
}
PWHT_6 {
@ Min space of PWHT to N+ AA plug outside PWHT is 3.50
  EXT HNTAP PWHT < 3.50 ABUT<90 SINGULAR REGION 
}
PWHT_7 {
@ PWHT must be in DDNW
  PWHT NOT DDNW
}  

//======================================
//  DNW - Deep Nwell for LV & MV device
//======================================

GROUP GDNW DNW_?

DNW_1 {
@ DNW must be surround by LVNW and MVNW
 DNW OUTSIDE EDGE (NW INTERACT SURNW)
}
DNW_2 {
@ It's not allowed to use DNW layer as resistor
  AND RESNW DNW
}
DNW_3ABC {
@ LV PMOS is not allowed in surrounding NW
@ MV PMOS is not allowed in surrounding MVNW
@ Add dummy layer "SURNW" on surrounding NW for DRC check
@ Min & Max "SURNW" enclosure surrounding NW space is 0.15

  LPGATE AND (NW INTERACT SURNW)
  MPGATE AND (MVNW INTERACT SURNW)
  (SIZE SURNW BY -0.15) XOR (SURNW AND NW)
}
DNW_4 {
@ Minimum extension of surounding LV or MV NW beyond DNW is 1.5um
  ENC DNW (LVNW INTERACT SURNW) < 1.5 ABUT<90 REGION
  ENC DNW (MVNW INTERACT SURNW) < 1.5 ABUT<90 REGION
}
DNW_5 {
@ Minimum overlap of surrounding LV or MV NW onto DNW is 2.0um
  ENC HOLE_INNW DNW < 2.0 ABUT<90 SINGULAR REGION
}
DNW_6 {
@ Minmum space from surrounding NW to HVNW is 8
  EXT NWHT (NW INTERACT SURNW) < 14 ABUT<90 SINGULAR REGION
}
DNW_7 {
@ Minimum width of a DNW region is 3.0
  INT DNW < 3.0 ABUT<90 SINGULAR REGION
}
DNW_8 {
@ Min.space between DNW to DNW is 8.0
  EXT DNW < 8.0 ABUT<90 SINGULAR REGION
}
DNW_9 {
@ Minimum space of DNW to NW which is outside DNW is 5
  X = NW NOT COIN EDGE DNW
  EXT X DNW < 5 ABUT<90 REGION MEASURE ALL

}
DNW_10 {
@ Minimum space from the DNW to a N+AA outside surrounding NW when DNW is implemented.
  X = NACT INSIDE DNW
  EXT (NW INTERACT SURNW) X < 0.5 ABUT<90 SINGULAR REGION
}
DNW_12 {
@ It's not allowed to use surrounding NW  as resistor
  (NW INTERACT SURNW) INTERACT RESNW 
}

//===========================
//  AA - for LV & MV device 
//===========================

GROUP GAA AA_?

AA_1a&1b {
@ Minimum LV AA width to define the width of NMOS/PMOS is 0.15um
@ Minimum MV AA width to define the width of NMOS/PMOS is 0.27um
  INT (LV_AA INTERACT GT) < 0.15 ABUT<90 SINGULAR REGION
  INT (MV_AA INTERACT GT) < 0.27 ABUT<90 SINGULAR REGION
}
AA_2 {
@ Minimum AA width for interconnect is 0.15um
  INT LM_AA < 0.15 ABUT<90 SINGULAR REGION
}
AA_3 {
@ Minimum space between AAs that are in the same well is 0.21um
  EXT LM_AA < 0.21 ABUT<90 SINGULAR REGION
}
AA_4 {
@ Minimum N+AA enclosured by NW is 0.23um
  ENC LNTAP NW < 0.23 ABUT<90 SINGULAR REGION
  ENC MNTAP NW < 0.23 ABUT<90 SINGULAR REGION
}
AA_5 {
@ Minimum space between NW and LV N+AA is 0.3um
@ Minimum space between NW and MV N+AA is 0.6um
  EXT NW LMOSN < 0.3 ABUT<90 SINGULAR REGION
  EXT NW MMOSN < 0.6 ABUT<90 SINGULAR REGION
}
AA_6 {
@ Minimum LV P+AA enclosured by NW is 0.3um
@ Minimum MV P+AA enclosured by NW is 0.6um
  ENC LMOSP NW < 0.3 ABUT<90 SINGULAR REGION
  ENC MMOSP NW < 0.6 ABUT<90 SINGULAR REGION
}
AA_7 {
@ Space between NW to P+AA inside PW is 0.23um
  EXT LPTAP NW < 0.23 ABUT<90 SINGULAR REGION
  EXT MPTAP NW < 0.23 ABUT<90 SINGULAR REGION
}
AA_8 {
@ Minimum area of a stand-along AA region is 0.10um
  AREA AA < 0.10
}

//==================================
//  AA DR for HV Symmetric device 
//==================================

GROUP GHVAA HVAA_?

HOLE_HVAA = HOLES HV_AA INNER 
HVAA_HOLE = HV_AA TOUCH HOLE_HVAA
SYMNSTI = STIDMY INSIDE PWHT
SYMPSTI = STIDMY INSIDE NWHT

//HVAA.1A ,1B including NDRF.1 and PDRF.1

HVAA_2a {
@ Min & Max STI width for HVNMOS field plate length is 1.20
  INT SYMNSTI <1.20 ABUT<90 SINGULAR REGION
  SIZE SYMNSTI BY -0.6
}
HVAA_2b {
@ Min @ Max STI width for HVPMOS field plate length is 0.50
  INT SYMPSTI <0.50 ABUT<90 SINGULAR REGION
  SIZE SYMPSTI BY -0.25
}
HVAA_3 {
@ Min AA width for HV transistor source & draion region length is 0.50um
  INT ((HMOSP NOT GT) AND PDRF) < 0.5 ABUT<90 SINGULAR REGION
  INT ((HMOSN NOT GT) AND NDRF) < 0.5 ABUT<90 SINGULAR REGION
}
HVAA_4 { 
@ Min AA width for HV transistor channel width is 4
  X1 = (HMOSP NOT GT) COIN EDGE STIDMY
  LENGTH X1 <4.0
  X2 = (HMOSN NOT GT) COIN EDGE STIDMY
  LENGTH X2 <4.0
}
HVAA_5 {
@ Minimum N+ AA to N+AA space ;Minimum P+ AA to P+ AA space is 3.50
  X= EXT HVN_AA < 3.50 ABUT<90 SINGULAR REGION
  Y= EXT HVP_AA < 3.50 ABUT<90 SINGULAR REGION
  X NOT INTERACT STIDMY
  Y NOT INTERACT STIDMY
}
HVAA_6 {
@ Minimum N+ AA to P+ AA plug space ;Minimum P+ AA to N+ AA plug space is 1.50
  EXT HVN_AA HPTAP < 1.5 ABUT<90 SINGULAR REGION
  EXT HVP_AA HNTAP < 1.5 ABUT<90 SINGULAR REGION
}

HVAA_7 {
@Add Dummy layer "STIDMY" on the HV transistor Field plate area for DRC check
@Min & Max STIDMY enclosure STI plate space is 0.13 
  STIDMY AND AA
  INT STIDMYi HMOSN < 0.13 ABUT<90 SINGULAR REGION
  INT STIDMYi HMOSP < 0.13 ABUT<90 SINGULAR REGION
}

HVAA_8 {
@ Minimum channel AA to plug AA space is 1.10
  EXT HNGATE HPTAP < 1.1 ABUT<90 SINGULAR REGION
  EXT HPGATE HNTAP < 1.1 ABUT<90 SINGULAR REGION
}
HVAA_9a {
@Recommend to put a guard-ring AA enclosure HV NMOS and HVPMOS for better isolation.
@ Minimum Guard ring AA width is 0.50um
  INT HVAA_HOLE < 0.50 ABUT<90 SINGULAR REGION
}
HVAA_9b {
@ Minimum N+/P+ AA to guard ring AA space is 1.50um
  X = (HNTAP OR HPTAP) TOUCH HOLE_HVAA
  EXT X (HVN_AA OR HVP_AA) < 1.50 ABUT<90 SINGULAR REGION
} 
HVAA_10a {
@ Minimum and Maxium AA extension source and drain AA for HVNMOS is 1.35
  X = HNGATE TOUCH EDGE STIDMY
  Y = EXPAND EDGE X BY 1.7
  A = Y NOT ((HVN_AA OR STIDMY) OR HNGATE)
  B = NOT RECTANGLE A == 1.7 BY == 1.35
  C = RECTANGLE A == 2.9 BY == 1.35
  B NOT C
}
HVAA_10b {
@ Minimum and Maxium AA extension source and drain AA for HVPMOS is 1.35
  X = HPGATE TOUCH EDGE STIDMY
  Y = EXPAND EDGE X BY 1
  A = Y NOT ((HVP_AA OR STIDMY) OR HPGATE)
  B = NOT RECTANGLE A == 1 BY == 1.35
  C = RECTANGLE A == 1.5 BY == 1.35
  B NOT C
}




//===================================
//  TG - HV thick xoide open area DRC
//===================================

GROUP GTG TG_?

TG_1 {
@ Min TG width is 0.90
  INT TG < 0.9 ABUT<90 SINGULAR REGION
}

TG_2 {
@ Minimum TG to TG space .Merge if lessthan 0.90 um
  EXT TG < 0.9 ABUT<90 SINGULAR REGION
}
TG_3a {
@ Minimum and Maximum TG enclosure HVNMOS channel AA is 0.6um
  X = CONVEX EDGE HVCANAA == 2
  Y = (EXPAND EDGE X OUT BY 0.6) INTERACT TG
  Z = Y TOUCH INSIDE EDGE TG
  Y NOT WITH EDGE Z
}
TG_3b {
@ Minimum and Maximum TG enclosure HVPMOS channel AA is 0.2um
  X = CONVEX EDGE HVCAPAA == 2
  Y = (EXPAND EDGE X OUT BY 0.2) INTERACT TG
  Z = Y TOUCH INSIDE EDGE TG
  Y NOT WITH EDGE Z
}
TG_4a {
@ Minimum and Maximum Field plate enclosure HVNMOS TG space is 0.6
  X = (TG AND SN) INSIDE EDGE STIDMY
  (EXPAND EDGE X OUT BY 0.6) NOT TOUCH HNSD
}
TG_4b {
@ Minimum and Maximum Field plate enclosure HVPMOS TG space is 0.3
  X = (TG AND SP) INSIDE EDGE STIDMY
  (EXPAND EDGE X OUT BY 0.3) NOT TOUCH HPSD
}
TG_5a {
@ For Symmetric HV transistor, only channel region need covered by TG
@ HV source & drain AA area must not be covered by TG
  (HMOSN CUT NDRF) NOT INTERACT TG
  HVN_AA INTERACT TG
  (HMOSP CUT PDRF) NOT INTERACT TG
  HVP_AA INTERACT TG
}
TG_5b {
@ TG cover non-channel AA region is not allowed
  TG AND (AA NOT INTERACT GT)
}
TG_5c {
@ TG cover HV AA pickup and LV & MV area is not allowed
  TG AND ((HVP_AA OR HVN_AA) OR LMV)
}
     
//===========================================
//  NDRF - define HV NMOS drift imolant area  
//===========================================

GROUP GNDRF NDRF_?

NDRF_SD    = NDRF ENCLOSE STIDMY
SYMNDRF_SD = NDRF ENCLOSE SYMNSTI
HVNCH   = (AA INSIDE TG) INTERACT NDRF

NDRF_1 {
@ Minimum Ndirft to Ndirft space in the Symmetric HV transistor channel region is 2.10
@ Define the Symmetric HV transistor channel length
  X = EXT NDRF_SD < 2.1 ABUT<90 SINGULAR REGION
  HVNCH AND X
}    
NDRF_2 {
@ Min & Max Ndrift extension channel AA in the channel region for Symmetric HV transistor is 0.40
  HVNGATE = (HVNCH AND GT) AND NDRF
  INT HVNGATE < 0.4 ABUT<90 SINGULAR REGION
  SIZE HVNGATE BY -0.2
}
NDRF_3 {
@ Min & Max Ndrift enclosure HV AA in the field region is 0.50
  Y = NDRF NOT (SIZE TG BY 0.1)
  HVN_AA XOR (SIZE Y BY -0.5)
}
NDRF_4 {
@ Min Ndrift to Ndrift space in the non_channel region is 2.50
  X = EXT NDRF  < 2.5 ABUT<90 SINGULAR REGION
  X NOT INTERACT HVNCH
}
NDRF_5 {
@ Min Ndrit width  is 2.20
  INT NDRF < 2.2 ABUT<90 SINGULAR REGION
}
NDRF_6 {
@ Min HVPW enclosure Ndrift space  is 5.00
  ENC NDRF PWHT < 5.00 ABUT<90 SINGULAR REGION
}
NDRF_7 {
@ Ndrift cover HV AA pickup area is not allowed 
  NDRF AND HPTAP
  NDRF AND HNTAP
}
NDRF_8 {
@ Ndrift cover LV & MV area is not allowed
  (NDRF AND AA) NOT PWHT   
}
//Skip NDRF_10: Asymmetric HV NMOS is not allowed


//===========================================
//  PDRF - define HV PMOS drift imolant area
//===========================================

GROUP GPDRF PDRF_?

PDRF_SD    = PDRF ENCLOSE STIDMY
SYMPDRF_SD = PDRF ENCLOSE SYMPSTI
HVPCH   = (AA INSIDE TG) INTERACT PDRF

PDRF_1 {
@ Minimum Pdirft to Pdirft space in the symmetric HV transistor channel region is 2.10
  X = EXT PDRF_SD < 2.10 ABUT<90 SINGULAR REGION
  HVPCH AND X
}
PDRF_2 {
@ Min & Max Pdrift extension channel AA in the channel region for symmetric HV transistor is 0.40
  HVPGATE = (HVPCH AND GT) AND PDRF
  INT HVPGATE < 0.4 ABUT<90 SINGULAR REGION
  SIZE HVPGATE BY -0.2
}
PDRF_3 {
@ Min & Max Pdrift enclosure HV AA in the field region is 0.60
  Y = PDRF NOT (SIZE TG BY -0.3)
  HVP_AA XOR (SIZE Y BY - 0.6)
}
PDRF_4 {
@ Min Pdrift to Pdrift space in the non_channel region is 2.50
  X = EXT PDRF < 2.5 ABUT<90 SINGULAR REGION
  X NOT INTERACT HVPCH
}
PDRF_5 {
@ Min Pdrit width  is 1.9
  INT PDRF < 1.9 ABUT<90 SINGULAR REGION
}
PDRF_6 {
@ Min DDNW enclosure Pdrift space  is 5.00
  ENC PDRF DDNW < 5.00 ABUT<90 SINGULAR REGION
}
PDRF_7 {
@ Pdrift cover HV AA pickup area is not allowed
  PDRF AND HNTAP
  PDRF AND HPTAP
}
PDRF_8 {
@ Pdrift cover LV & MV area is not allowed
  (PDRF AND AA) NOT NWHT
}
//Skip PDRF_10: Asymmetric HV PMOS is not allowed


//==========================
//  DG - Dual Gate DRC
//==========================

GROUP GDG DG_?

DG_1 { 
@ Minimum DG width is 0.77um
  INT DG < 0.77 ABUT<90 SINGULAR REGION
}
DG_2 {
@ Minimum space between two DGs is 0.7um
  EXT DG < 0.77 ABUT<90 SINGULAR REGION
}
DG_3 {
@ Minimum AA enclosured by DG is 0.6um
  ENC AA DG < 0.6 ABUT<90 SINGULAR REGION
}
DG_4 {
@ Minimum 5.5V(6V) transistor gate poly enclosured by DG is 0.33um
  ENC MGATE DG < 0.33 ABUT<90 SINGULAR REGION
}
DG_5 {
@ Minimum space between DG and AA is 0.25um
  EXT DG PACT < 0.25 ABUT<90 SINGULAR REGION
  EXT DG NACT < 0.25 ABUT<90 SINGULAR REGION
}
DG_6 {
@ Minimum space between DG and 1.5V transistor poly gate is 0.33um
  EXT DG LGATE < 0.33 ABUT<90 SINGULAR REGION
}

//========================
//  GT - Poly 1 Gate DRC
//========================

GROUP GGT GT_?

GT_1a {
@ Minimum GT width for 1.5V NMOS/PMOS transistor is 0.13um
  X = GT INSIDE EDGE (LMOSN OR LMOSP)
  INT X < 0.13 ABUT<90 REGION
}
GT_1b {
@ Minimum GT width fo 5.5V NMOS/PMOS transistor is 0.6um
  X = GT INSIDE EDGE (MMOSN_55 OR MMOSP_55)
  INT X < 0.6 ABUT<90 REGION
}
GT_1c {
@ Minimum GT width fo 6V NMOS/PMOS transistor is 0.65um
  X = GT INSIDE EDGE (MMOSN_60 OR MMOSP_60)
  INT X < 0.65 ABUT<90 REGION
}
GT_2 {
@ Minimum width of a GT region for interconnects is 0.13um
  INT (INTERGT NOT DDNW) < 0.13 ABUT<90 SINGULAR REGION
}
GT_3 {
@ Minimum space between GTs is 0.18um
  EXT GT < 0.18 ABUT<90 SINGULAR REGION
}
GT_4 {
@ Minimum space between GT and AA on field oxide is 0.07um
  EXT AA GT < 0.07 ABUT<90 REGION
}
GT_5 {
@ Minimum extension of AA outside of GT is 0.23um
  ENC GT AA < 0.23 ABUT<90 SINGULAR REGION
}
GT_6 {
@ Minimum extension of GT outside of AA is 0.17um
  ENC AA GT < 0.17 ABUT<90 SINGULAR REGION
}
GT_7 {
@ Minimum GT pattern density is 15%
  DENSITY GTi < 0.15 PRINT density_report_GT.log
}
GT_8 {
@ No bent GT on AA are allowed. All GT patterns on AA have to be orthogonal to AA edge
  X = GT INSIDE EDGE AA
  INT X < 0.01 ABUT == 135 REGION
  EXT X < 0.01 ABUT == 135 REGION
  INT X < 0.01 ABUT == 90 REGION
  EXT X < 0.01 ABUT == 90 REGION
  INT X < 0.01 ABUT == 45 REGION
  EXT X < 0.01 ABUT == 45 REGION
}
GT_9a {
@ NLL extension outside of poly resistor is 0.18
  X = GTR AND SN
  ENC X NLL < 0.18 ABUT<90 SINGULAR REGION
}
GT_9b {
@ NLH extension outside of poly resistor is 0.18
  X = GTR AND SN
  ENC X NLH < 0.18 ABUT<90 SINGULAR REGION
}
GT_9c {
@ SN extension outside of poly resistor is 0.18
  ENC GTR SN < 0.18 ABUT<90 SINGULAR REGION
}
GT_9d {
@ PLL extension outside of poly resistor is 0.18
  X = GTR AND SP
  ENC X PLL < 0.18 ABUT<90 SINGULAR REGION
}
GT_9e {
@ PLH extension outside of poly resistor is 0.18
  X = GTR AND SP
  ENC X PLH < 0.18 ABUT<90 SINGULAR REGION
}
GT_9f {
@ SP extension outside of poly resistor is 0.18
  ENC GTR SP < 0.18 ABUT<90 SINGULAR REGION
}
GT_10a {
@ Space between a NLL and a P-type poly resistor is 0.18
  X = GTR AND SP
  EXT NLL X < 0.18 ABUT<90 SINGULAR REGION
}
GT_10b {
@ Space between a NLH and a P-type poly resistor is 0.18
  X = GTR AND SP
  EXT NLH X < 0.18 ABUT<90 SINGULAR REGION
}
GT_10c {
@ Space between a SN and a P-type poly resistor is 0.18
  EXT SN GTR < 0.18 ABUT<90 SINGULAR REGION
}
GT_10d {
@ Space between a PLL and a N-type poly resistor is 0.18
  X = GTR AND SN
  EXT PLL X < 0.18 ABUT<90 SINGULAR REGION
}
GT_10e {
@ Space between a PLH and a N-type poly resistor is 0.18
  X = GTR AND SN
  EXT PLH X < 0.18 ABUT<90 SINGULAR REGION
}
GT_10f {
@ Space between a SP and a N-type poly resistor is 0.18
  EXT SP GTR < 0.18 ABUT<90 SINGULAR REGION
}
GT_11 {
@ GT is enclosed by SN and/or SP
  X = GT INTERACT HRP
  Z = (GT NOT HRPDMY) NOT X
  (Z NOT SP) NOT SN
}
GT_12 {
@ SN and SP overlap on GT is not allowed
  (GT AND SN) AND SP
}
/*
GT_Note2 {
@ For narrow GT < 0.24um, distance between CT should be > 1um to aviod voltage drop.
  X = INT GT < 0.24 ABUT<90 SINGULAR REGION
  Y = SIZE CT BY 0.5 INSIDE X
}
*/


//=====================================
//  HVGT - Poly design minima (for HV)
//=====================================

GROUP GHVGT HVGT_?

HVGT_1 {
@ Poly as interconnect is not allowed ,all interconnect must be metal
  HVGT NOT INTERACT ((GTR OR GT_HRP) OR (HMOSN OR HMOSP))
}
HVGT_2a {
@ Min & Max GT overlap Field plate space for HV NMOS is 0.60
  INT HVGT (STIDMY INSIDE PWHT) < 0.60 ABUT<90 SINGULAR REGION
  (SIZE HVGT BY -0.6) AND (STIDMY INSIDE PWHT)
}
HVGT_2b {
@ Min & Max GT overlap Field plate space for HV PMOS is 0.20
  INT HVGT (STIDMY OUTSIDE PWHT) < 0.20 ABUT<90 SINGULAR REGION
  (SIZE HVGT BY -0.2) AND (STIDMY OUTSIDE PWHT)
}
HVGT_3 {
@ Min GT extension AA to form poly end cap is 0.60
  ENC (HNGATE NOT COINCIDENT EDGE HVGT) HVGT < 0.6 ABUT< 90 REGION
  X = (SIZE HPGATE BY 0.2) NOT COINCIDENT EDGE HVGT
  ENC X HVGT < 0.4 ABUT< 90 
}
HVGT_4 {
@ Minimum space between GT to pick up AA is 0.50
  EXT HVGT (HPTAP OR HNTAP) < 0.5 ABUT<90 SINGULAR REGION
}
HVGT_5 {
@ Minimum space between two GT regions is 1.50
  EXT HVGT < 1.5 ABUT<90 SINGULAR REGION
}



//====================================
//  NLL - LV NLDD Implantation DRC
//====================================

GROUP GNLL NLL_?

NLL_1 {
@ Minimum NLL width is 0.3um
  INT NLL < 0.3 ABUT<90 SINGULAR REGION
}
NLL_2 {
@ Minimum space between NLLs is 0.3um
  EXT NLL < 0.3 ABUT<90 SINGULAR REGION
}
NLL_3 {
@ Minimum space between NLL and P+ AA inside N well is 0.16um
  X = (PACT NOT LPTAP) NOT TOUCH EDGE NLL
  EXT NLL X < 0.16 ABUT<90 REGION
}
NLL_4 {
@ Minimum space between NLL and P+ AA inside P well is 0.03um
  X = LPTAP NOT TOUCH EDGE NACT
  EXT NLL X < 0.03 ABUT<90 REGION
}
NLL_5 {
@ Minimum space between NLL and AA with NLH,PLL or PLH implantation is 0.16um
  EXT NLL (AA AND NLH) < 0.16 ABUT<90 SINGULAR REGION
  EXT NLL (AA AND PLL) < 0.16 ABUT<90 SINGULAR REGION
  EXT NLL (AA AND PLH) < 0.16 ABUT<90 SINGULAR REGION
}
NLL_6 {
@ Minimum NLL extension outside of NMOS poly gate is 0.4um
  ENC NGATE NLL < 0.4 ABUT<90 SINGULAR REGION
}
NLL_7 {
@ Minimum NLL extension outside of N+AA is 0.16um
@ if distance to the related poly is larger than 0.25um
  X = LMOSN NOT COIN EDGE PTAP
  ENC X NLL < 0.16 ABUT<90 OPPOSITE REGION
}
NLL_8 {
@ Minimum enclosure of an NLL region to an N-channel poly gate is 0.30um
@ if distance to related poly is less than 0.25um.
  X = GT INSIDE EDGE NACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = NGATE COIN EDGE NACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = A OR B
  (C INTERACT NLL) NOT NLL
}
NLL_9 {
@ Minimum NLL and AA overlap is 0.16um
  INT NLL AA < 0.16 ABUT<90 SINGULAR REGION
}
NLL_10 {
@ Minimum area of an NLL region is 0.23um
  AREA NLL < 0.23
}


//======================================
//  PLL - LV PLDD Implantation DRC
//======================================

GROUP GPLL PLL_?

PLL_1 {
@ Minimum NLL width is 0.3um
  INT PLL < 0.3 ABUT<90 SINGULAR REGION
}
PLL_2 {
@ Minimum space between PLLs is 0.3um
  EXT PLL < 0.3 ABUT<90 SINGULAR REGION
}
PLL_3 {
@ Minimum space between PLL and N+ AA inside P well is 0.16um
  X = (NACT NOT LNTAP) NOT TOUCH EDGE PLL
  EXT PLL X < 0.16 ABUT<90 REGION
}
PLL_4 {
@ Minimum space between PLL and N+ AA inside N well is 0.03um
  X = LNTAP NOT TOUCH EDGE PACT
  EXT PLL X < 0.03 ABUT<90 REGION
}
PLL_5 {
@ Minimum space between PLL and AA with PLH,NLL or NLH implantation is 0.16um
  EXT PLL (AA AND PLH) < 0.16 ABUT<90 SINGULAR REGION
  EXT PLL (AA AND NLL) < 0.16 ABUT<90 SINGULAR REGION
  EXT PLL (AA AND NLH) < 0.16 ABUT<90 SINGULAR REGION
}
PLL_6 {
@ Minimum PLL extension outside of PMOS poly gate is 0.4um
  ENC PGATE PLL < 0.4 ABUT<90 SINGULAR REGION
}
PLL_7 {
@ Minimum PLL extension outside of P+AA is 0.16um
@ if distance to the related poly is larger than 0.25um
  X = LMOSP NOT COIN EDGE NTAP
  ENC X PLL < 0.16 ABUT<90 OPPOSITE REGION
}
PLL_8 {
@ Minimum enclosure of an PLL region to an P-channel poly gate is 0.30um
@ if distance to related poly is less than 0.25um.
  X = GT INSIDE EDGE PACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = PGATE COIN EDGE PACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = A OR B
  (C INTERACT PLL) NOT PLL
}
PLL_9 {
@ Minimum PLL and AA overlap is 0.16um
  INT PLL AA < 0.16 ABUT<90 SINGULAR REGION
}
PLL_10 {
@ Minimum area of an PLL region is 0.23um
  AREA PLL < 0.23
}


//=====================================
//  NLH - MV NLDD Implantation DRC
//=====================================

GROUP GNLH NLH_?

NLH_1 {
@ Minimum NLH width is 0.3um
  INT NLH < 0.3 ABUT<90 SINGULAR REGION
}
NLH_2 {
@ Minimum space between NLHs is 0.3um
  EXT NLH < 0.3 ABUT<90 SINGULAR REGION
}
NLH_3 {
@ Minimum space between NLH and P+ AA inside N well is 0.16um
  X = (PACT NOT MPTAP) NOT TOUCH EDGE NLH
  EXT NLH X < 0.16 ABUT<90 REGION
}
NLH_4 {
@ Minimum space between NLH and P+ AA inside P well is 0.03um
  X = MPTAP NOT TOUCH EDGE NACT
  EXT NLH X < 0.03 ABUT<90 REGION
}
NLH_5 {
@ Minimum space between NLL and AA with NLL,PLL or PLH implantation is 0.16um
  EXT NLH (AA AND NLL) < 0.16 ABUT<90 SINGULAR REGION
  EXT NLH (AA AND PLL) < 0.16 ABUT<90 SINGULAR REGION
  EXT NLH (AA AND PLH) < 0.16 ABUT<90 SINGULAR REGION
}
NLH_6 {
@ Minimum NLH extension outside of NMOS poly gate is 0.4um
  ENC NGATE NLH < 0.4 ABUT<90 SINGULAR REGION
}
NLH_7 {
@ Minimum NLL extension outside of N+AA is 0.16um
@ if distance to the related poly is larger than 0.25um
  X = MMOSN NOT COIN EDGE PTAP
  ENC X NLH < 0.16 ABUT<90 OPPOSITE REGION
}
NLH_8 {
@ Minimum enclosure of an NLH region to an N-channel poly gate is 0.30um
@ if distance to related poly is less than 0.25um.
  X = GT INSIDE EDGE NACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = NGATE COIN EDGE NACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = A OR B
  (C INTERACT NLH) NOT NLH
}
NLH_9 {
@ Minimum NLH and AA overlap is 0.16um
  INT NLH AA < 0.16 ABUT<90 SINGULAR REGION
}
NLH_10 {
@ Minimum area of an NLH region is 0.23um
  AREA NLH < 0.23
}

  


//====================================
//  PLH - MV PLDD Implantation DRC
//====================================

GROUP GPLH PLH_?

PLH_1 {
@ Minimum NLH width is 0.3um
  INT PLH < 0.3 ABUT<90 SINGULAR REGION
}
PLH_2 {
@ Minimum space between PLHs is 0.3um
  EXT PLH < 0.3 ABUT<90 SINGULAR REGION
}
PLH_3 {
@ Minimum space between PLH and N+ AA inside P well is 0.16um
  X = (NACT NOT MNTAP) NOT TOUCH EDGE PLL
  EXT PLH X < 0.16 ABUT<90 REGION
}
PLH_4 {
@ Minimum space between PLH and N+ AA inside N well is 0.03um
  X = MNTAP NOT TOUCH EDGE PACT
  EXT PLH X < 0.03 ABUT<90 REGION
}
PLH_5 {
@ Minimum space between PLL and AA with PLH,NLL or NLH implantation is 0.16um
  EXT PLH (AA AND PLL) < 0.16 ABUT<90 SINGULAR REGION
  EXT PLH (AA AND NLL) < 0.16 ABUT<90 SINGULAR REGION
  EXT PLH (AA AND NLH) < 0.16 ABUT<90 SINGULAR REGION
}
PLH_6 {
@ Minimum PLH extension outside of PMOS poly gate is 0.4um
  ENC PGATE PLH < 0.4 ABUT<90 SINGULAR REGION
}
PLH_7 {
@ Minimum PLH extension outside of P+AA is 0.16um
@ if distance to the related poly is larger than 0.25um
  X = MMOSP NOT COIN EDGE NTAP
  ENC X PLH < 0.16 ABUT<90 OPPOSITE REGION
}
PLH_8 {
@ Minimum enclosure of an PLL region to an P-channel poly gate is 0.30um
@ if distance to related poly is less than 0.25um.
  X = GT INSIDE EDGE PACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = PGATE COIN EDGE PACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = A OR B
  (C INTERACT PLH) NOT PLH
}
PLH_9 {
@ Minimum PLH and AA overlap is 0.16um
  INT PLH AA < 0.16 ABUT<90 SINGULAR REGION
}
PLH_10 {
@ Minimum area of an PLH region is 0.23um
  AREA PLH < 0.23
}



//=============================
//  SN - N+ Implantation DRC
//=============================

GROUP GSN SN_?

SN_1 {
@ Minimum SN width is 0.3um
  INT SN < 0.3 ABUT<90 SINGULAR REGION
}
SN_2 {
@ Minimum space between SNs is 0.3um
  EXT SN < 0.3 ABUT<90 SINGULAR REGION
}
SN_3 {
@ Minimum space between SN and P+ AA inside N well is 0.16um
  X = (PACT NOT PTAP) NOT TOUCH EDGE SN
  EXT SN X < 0.16 ABUT<90 REGION
}
SN_4 {
@ Minimum space between SN and P+ pick-up AA inside P well is 0.03um
  X = PTAP NOT TOUCH EDGE NACT
  EXT SN X < 0.03 ABUT<90 REGION
}
SN_5 {
@ Minimum SN extension outside of NMOS poly gate is 0.4um
  ENC NGATE SN < 0.4 ABUT<90 SINGULAR REGION
}
SN_6 {
@ Minimum space between SN and PMOS poly gate is 0.4um
  EXT PGATE SN < 0.4 ABUT<90 SINGULAR REGION
}
SN_7 {
@ Minimum SN extension outside of N+AA is 0.16um
@ if distance to the related poly is larger than 0.25um
  X = MOSN NOT COIN EDGE PTAP
  ENC X SN < 0.16 ABUT<90 OPPOSITE REGION
}
SN_8 {
@ Minimum enclosure of an SN region to an N-channel poly gate is 0.30um
@ if distance to related poly is less than 0.25um.
  X = GT INSIDE EDGE NACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = NGATE COIN EDGE NACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = A OR B
  (C INTERACT SN) NOT SN
}
SN_9 {
@ N+AA enclosured by SN in NW is 0.16um
  ENC NTAP SN < 0.16 ABUT<90 SINGULAR REGION
}
SN_10 {
@ Minimum SN and AA overlap is 0.16um
  INT SN AA < 0.16 ABUT<90 SINGULAR REGION
}
SN_11 {
@ Minimum area of an SN region is 0.23um
  AREA SN < 0.23
}


//==============================
//  SP - P+ Implantation DRC
//==============================

GROUP GSP SP_?

SP_1 {
@ Minimum SP width is 0.3um
  INT SP < 0.3 ABUT<90 SINGULAR REGION
}
SP_2 {
@ Minimum space between SPs is 0.3um
  EXT SP < 0.3 ABUT<90 SINGULAR REGION
}
SP_3 {
@ Minimum space between SP and N+ AA inside P well is 0.16um
  X = (NACT NOT NTAP) NOT TOUCH EDGE SP
  EXT SP X < 0.16 ABUT<90 REGION
}
SP_4 {
@ Minimum space between SP and N+ pick-up AA inside N well is 0.03um
  X = NTAP NOT TOUCH EDGE PACT
  EXT SP X < 0.03 ABUT<90 REGION
}
SP_5 {
@ Minimum space between SP and NMOS poly gate is 0.4um
  EXT NGATE SP < 0.4 ABUT<90 SINGULAR REGION
}
SP_6 {
@ Minimum SP extension outside of PMOS poly gate is 0.4um
  ENC PGATE SP < 0.4 ABUT<90 SINGULAR REGION
}
SP_7 {
@ Minimum SP extension outside of P+AA is 0.16um
@ if distance to the related poly is larger than 0.25um
  X = MOSP NOT COIN EDGE NTAP
  ENC X SP < 0.16 ABUT<90 OPPOSITE REGION
}
SP_8 {
@ Minimum enclosure of an SP region to an P-channel poly gate is 0.30um
@ if distance to related poly is less than 0.25um.
  X = GT INSIDE EDGE PACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = PGATE COIN EDGE PACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = A OR B
  (C INTERACT SP) NOT SP
}
SP_9 {
@ N+AA enclosured by SP in PW is 0.16um
  ENC PTAP SP < 0.16 ABUT<90 SINGULAR REGION
}
SP_10 {
@ Minimum SP and AA overlap is 0.16um
  INT SP AA < 0.16 ABUT<90 SINGULAR REGION
}
SP_11 {
@ Minimum area of an SP region is 0.23um
  AREA SP < 0.23
}
SP_12 {
@ No SN and SP overlap is allowed
  SN AND SP
}



//==============================
//  SAB - Salicide Block DRC
//==============================

GROUP GSAB SAB_?

SAB_1 {
@ Minimum SAB width is 0.42um
  INT SAB < 0.42 ABUT<90 SINGULAR REGION
}
SAB_2 {
@ Minimum SAB space is 0.42um
  EXT SAB < 0.42 ABUT<90 SINGULAR REGION
}
SAB_3 {
@ Minimum extension of related AA outside of SAB is 0.2um
  ENC SAB AA < 0.20 ABUT<90 SINGULAR REGION
}
SAB_4 {
@ Minimum space between SAB and AA is 0.20um
  EXT SAB AA < 0.20 ABUT<90 SINGULAR REGION
}
SAB_5 {
@ Minimum space between SAB and poly on AA is 0.36um
  EXT SAB GATE < 0.36 ABUT<90 SINGULAR REGION
}
SAB_6 {
@ Minimum extension of SAB over related poly on field oxide is 0.20um
  X = GT INTERACT RESP1
  ENC X SAB < 0.20 ABUT<90 REGION
}
SAB_7 {
@ Minimum space between SAB and CT is 0.20um
  EXT SAB CT < 0.20 ABUT<90 SINGULAR REGION
}
SAB_8 {
@ Minimum extension of SAB outside of AA 0.20um
  ENC SAB AA < 0.20 ABUT<90 SINGULAR REGION
}
SAB_9 {
@ Minimum SAB overlap with SN or SP is 0.28
  INT (SAB NOT INTERACT HRP) SN < 0.28 ABUT<90 SINGULAR REGION
  INT (SAB NOT INTERACT HRP) SP < 0.28 ABUT<90 SINGULAR REGION
}
SAB_10 {
@ Minimum area of SAB is 0.50um
  AREA SAB < 0.50
}
SAB_11 {
@ Minimum space of SAB and poly on field oxide is 0.28um
  EXT SAB INTERGT < 0.28 ABUT<90 SINGULAR REGION
}


//====================
//  CT - Contact DRC
//====================

GROUP GCT CT_?

CT_1 {
@ Mininmum/Maximum contact size is 0.16um
  X = NOT RECTANGLE CT == 0.16 BY == 0.16
  X NOT TOUCH EOI
}
CT_2 {
@ Minimum space between two contacts is 0.18um
  EXT CT < 0.18 ABUT<90 SINGULAR REGION
}
CT_3 {
@ Minimum space between AA and CT on poly is 0.12um
  EXT INTERCT AA < 0.12 ABUT<90 SINGULAR REGION
}
CT_4a {
@ Minimum space between poly and CT on AA for 1.5V is 0.11um
  EXT GATE (ACT_CT NOT DG) < 0.11 ABUT<90 SINGULAR REGION
}
CT_4b {
@ Minimum space between poly and CT on AA for 5.5(6)V is 0.14um
  EXT GATE ((ACT_CT AND DG) NOT TG) < 0.14 ABUT<90 SINGULAR REGION
}
CT_5 {
@ Minimum CT enclosured by AA is 0.06um
  ENC CT AA < 0.06 ABUT<90 SINGULAR REGION
}
CT_6 {
@ Minimum CT enclosured by poly is 0.06um
  ENC CT GT < 0.06 ABUT<90 SINGULAR REGION
}
CT_7_8 {
@ Minimum CT enclosured by M1 is 0.00um
@ Minimum M1 line end ourside of CT is 0.05um.
    X = RECTANGLE ENC (CT NOT TOUCH M1) M1 ABUT<90 OUTSIDE ALSO
  GOOD 0 0.05 OPPOSITE 0 0.05 OPPOSITE

  A = ENC [X] M1 < 0.05 OPPOSITE
  INT A < 0.01 ABUT==90 INTERSECTING ONLY

  B = CT COIN EDGE M1
  INT B < 0.01 ABUT==90

  Y = ENC CT M1 < 0.05 OPPOSITE REGION
  C1 = CT COIN EDGE M1
  C2 = CT COIN EDGE Y
  INT C1 C2 < 0.01 ABUT==90

  CT NOT M1
}
CT_9 {
@ CT is not allowed to land on GATE.
  AND GATE CT
}
CT_10 {
@ CT to land on salicide surface.
  CT NOT (AA OR GT)
}
CT_11 {
@ CT on HV region must be > = 2 contacts
  (M1 AND HV_AA) INTERACT CT  == 1
  (M1 AND HVGT)  INTERACT CT  == 1
}
CT_12 {
@ Minimum space between high voltage poly ct to edge of channel AA is 0.45
  EXT (CT AND HVGT) HGATE <0.45 ABUT<90 SINGULAR REGION
}
CT_13a {
@ Minimum space of HV AA enclosure HV AA CT is 0.14
  SD_HVAA = HVN_AA OR HVP_AA
  ENC (CT AND SD_HVAA) SD_HVAA < 0.14 ABUT<90 SINGULAR REGION 
}
CT_13b {
@ Minimum space of HV plug AA enclosure CT is 0.14
  ENC (CT AND HPNTAP) HPNTAP < 0.14 ABUT<90 SINGULAR REGION
} 
CT_14 {
@ Minimum space of HV GT enclosure HV GT CT is 0.14
  ENC CT HVGT < 0.14 ABUT<90 SINGULAR REGION  
}
 
 


//====================
//  M1 - Metal 1 DRC
//====================

GROUP GM1 M1_?

M1_1 {
@ Minimum width of M1 region is 0.16um
  X = INT M1 < 0.16 ABUT<90 SINGULAR REGION
  X NOT INTERACT EOI
}
M1_2 {
@ Minimum space between two M1 regions is 0.17um
  EXT M1 < 0.17 ABUT<90 SINGULAR REGION
}
M1_3a {
@ Suggest minimum space between M1s is 0.2, if one or both M1 width greater than 0.4um
  X = M1 WITH WIDTH > 0.4
  EXT X M1 < 0.2 ABUT<90 SINGULAR REGION
}
M1_3b {
@ Suggest minimum space between M1s is 0.4, if one or both M1 width greater than 2um
  X = M1 WITH WIDTH > 2
  EXT X M1 < 0.4 ABUT<90 SINGULAR REGION
}
M1_3c {
@ Suggest minimum space between M1s is 0.5, if one or both M1 width greater than 10um
  X = M1 WITH WIDTH > 10
  EXT X M1 < 0.5 ABUT<90 SINGULAR REGION
}
M1_4 {
@ Minimum M1 area is 0.08um
  X = AREA M1 < 0.08
  X NOT INTERACT EOI
}
M1_5 {
@ Minimum dielectric area enclosured by M1 is 0.17
  X = HOLES M1 INNER EMPTY
  Y = AREA X < 0.17
  Y NOT INTERACT EOI
}
M1_6 {
@ Minimum density of M1 area is 30%
  DENSITY M1 < 0.3 PRINT density_report_M1.log
}
M1_7 {
@ Minimum space between metal line and 45 degree bent metal line that are larger than 0.5um is 0.2um
  X = ANGLE M1 == 45
  Y = LENGTH X >= 0.5
  EXT Y M1 < 0.2 ABUT<90 OPPOSITE REGION
}
M1_8 {
@ Maximum metal line width allowed is 14.00um.
@ Metal slot rule will apply for a metal with line width greater than this value.
  M1 WITH WIDTH > 14
}
M1_9 {
@ M1 as interconnect is allowed just for internal HV transistor.
@ External interconnect between two HV transistor must be Mn (n>=2).
  HV_device = HV_AA OR HVGT
  M1 INTERACT HV_device > 1
}
M1_10 {
@ HV M1 pass through LV & MV area is not allowed
@ Interconnect passed through HV and LV/MV area must be Mn
  HVM1 AND LMV
}

//====================
//  M2 - Metal 2 DRC
//====================

GROUP GM2 M2_?

M2_1 {
@ Minimum width of an M2 region is 0.20um
  X = INT M2 < 0.20 ABUT<90 SINGULAR REGION
  X NOT INTERACT EOI
}
M2_2 {
@ Minimum space between two M2 regions is 0.20um
  EXT M2 < 0.20 ABUT<90 SINGULAR REGION
}
M2_3a {
@ Suggest minimum space between M2s is 0.25, if one or both M2 width greater than 0.4um
  X = M2 WITH WIDTH > 0.4
  EXT X M2 < 0.25 ABUT<90 SINGULAR REGION
}
M2_3b {
@ Suggest minimum space between M2s is 0.4, if one or both M2 width greater than 2um
  X = M2 WITH WIDTH > 2
  EXT X M2 < 0.4 ABUT<90 SINGULAR REGION
}
M2_3c {
@ Suggest minimum space between M2s is 0.5, if one or both M2 width greater than 10um
  X = M2 WITH WIDTH > 10
  EXT X M2 < 0.5 ABUT<90 SINGULAR REGION
}
M2_4 {
@ Minimum M2 area is 0.12um
  X = AREA M2 < 0.12
  X NOT INTERACT EOI
}
M2_5 {
@ Minimum dielectric area enclosured by M2 is 0.26
  X = HOLES M2 INNER EMPTY
  Y = AREA X < 0.26
  Y NOT INTERACT EOI
}
M2_6 {
@ Dummy pattern is required in case Mn density is less than 30%
  DENSITY M2 < 0.3 PRINT density_report_M2.log
}
M2_7 {
@ Minimum space between metal line and 45 degree bent metal line that are larger than 0.5um is 0.22um
  X = ANGLE M2 == 45
  Y = LENGTH X >= 0.5
  EXT Y M2 < 0.22 ABUT<90 OPPOSITE REGION
}
M2_8 {
@ Maximum metal line width allowed is 15.00um.
@ Metal slot rule will apply for a metal with line width greater than this value.
  M2 WITH WIDTH > 15
}
M2_9 {
@ Minimum space between two HV M2 interconnect is 0.45
  EXT HVM2 < 0.45 ABUT<90 SINGULAR REGION
}


//====================
//  M3 - Metal 3 DRC
//====================

GROUP GM3 M3_?

M3_1 {
@ Minimum width of an M3 region is 0.20um
  X = INT M3 < 0.20 ABUT<90 SINGULAR REGION
  X NOT INTERACT EOI
}
M3_2 {
@ Minimum space between two M3 regions is 0.20um
  EXT M3 < 0.20 ABUT<90 SINGULAR REGION
}
M3_3a {
@ Suggest minimum space between M3s is 0.25, if one or both M3 width greater than 0.4um
  X = M3 WITH WIDTH > 0.4
  EXT X M3 < 0.25 ABUT<90 SINGULAR REGION
}
M3_3b {
@ Suggest minimum space between M3s is 0.4, if one or both M3 width greater than 2um
  X = M3 WITH WIDTH > 2
  EXT X M3 < 0.4 ABUT<90 SINGULAR REGION
}
M3_3c {
@ Suggest minimum space between M3s is 0.5, if one or both M3 width greater than 10um
  X = M3 WITH WIDTH > 10
  EXT X M3 < 0.5 ABUT<90 SINGULAR REGION
}
M3_4 {
@ Minimum M3 area is 0.12um
  X = AREA M3 < 0.12
  X NOT INTERACT EOI
}
M3_5 {
@ Minimum dielectric area enclosured by M3 is 0.26
  X = HOLES M3 INNER EMPTY
  Y = AREA X < 0.26
  Y NOT INTERACT EOI
}
M3_6 {
@ Dummy pattern is required in case Mn density is less than 30%
  DENSITY M3 < 0.3 PRINT density_report_M3.log
}
M3_7 {
@ Minimum space between metal line and 45 degree bent metal line that are larger than 0.5um is 0.22um
  X = ANGLE M3 == 45
  Y = LENGTH X >= 0.5
  EXT Y M3 < 0.22 ABUT<90 OPPOSITE REGION
}
M3_8 {
@ Maximum metal line width allowed is 15.00um.
@ Metal slot rule will apply for a metal with line width greater than this value.
  M3 WITH WIDTH > 15
}
M3_9 {
@ Minimum space between two HV M3 interconnect is 0.45
  EXT HVM3 < 0.45 ABUT<90 SINGULAR REGION
}


//====================
//  M4 - Metal 4 DRC
//====================

GROUP GM4 M4_?

M4_1 {
@ Minimum width of an M4 region is 0.20um
  X = INT M4 < 0.20 ABUT<90 SINGULAR REGION
  X NOT INTERACT EOI
}
M4_2 {
@ Minimum space between two M4 regions is 0.20um
  EXT M4 < 0.20 ABUT<90 SINGULAR REGION
}
M4_3a {
@ Suggest minimum space between M4s is 0.25, if one or both M4 width greater than 0.4um
  X = M4 WITH WIDTH > 0.4
  EXT X M4 < 0.25 ABUT<90 SINGULAR REGION
}
M4_3b {
@ Suggest minimum space between M4s is 0.4, if one or both M4 width greater than 2um
  X = M4 WITH WIDTH > 2
  EXT X M4 < 0.4 ABUT<90 SINGULAR REGION
}
M4_3c {
@ Suggest minimum space between M4s is 0.5, if one or both M4 width greater than 10um
  X = M4 WITH WIDTH > 10
  EXT X M4 < 0.5 ABUT<90 SINGULAR REGION
}
M4_4 {
@ Minimum M4 area is 0.12um
  X = AREA M4 < 0.12
  X NOT INTERACT EOI
}
M4_5 {
@ Minimum dielectric area enclosured by M4 is 0.26
  X = HOLES M4 INNER EMPTY
  Y = AREA X < 0.26
  Y NOT INTERACT EOI
}
M4_6 {
@ Dummy pattern is required in case Mn density is less than 30%
  DENSITY M4 < 0.3 PRINT density_report_M4.log
}
M4_7 {
@ Minimum space between metal line and 45 degree bent metal line that are larger than 0.5um is 0.22um
  X = ANGLE M4 == 45
  Y = LENGTH X >= 0.5
  EXT Y M4 < 0.22 ABUT<90 OPPOSITE REGION
}
M4_8 {
@ Maximum metal line width allowed is 15.00um.
@ Metal slot rule will apply for a metal with line width greater than this value.
  M4 WITH WIDTH > 15
}
M4_9 {
@ Minimum space between two HV M4 interconnect is 0.45
  EXT HVM4 < 0.45 ABUT<90 SINGULAR REGION
}


//====================
//  TM - TM2 DRC
//====================

GROUP GTM TM_?

TM_1 {
@ Minimum width of a top metal is 0.42um
  X = INT TM2 < 0.42 ABUT<90 SINGULAR REGION
  X NOT INTERACT EOI
}
TM_2 {
@ Minimum space between two top metal regions is 0.42um
  EXT TM2 < 0.42 ABUT<90 SINGULAR REGION
}
TM_4 {
@ Minimum space between top metal lines with one or both metal line width
@ and length are greater than 2um is 0.5um
  X = TM2 WITH WIDTH > 2
  EXT X TM2 < 0.5 ABUT<90 SINGULAR REGION
}
TM_5 {
@ Minimum area of an TM region is 0.42um
  X = AREA TM2 < 0.42
  X NOT INTERACT EOI
}
TM_6 {
@ Dielectric area enclosured by TM is 1
  X = HOLES TM2 INNER EMPTY
  Y = AREA X < 1
  Y NOT INTERACT EOI
}
TM_7 {
@ Minimum top metal pattern density is 30%
  DENSITY TM2 < 0.30 PRINT density_report_TM2.log
}
TM_8 {
@ Maximum TM2 width allowed is 30um
  TM2 WITH WIDTH > 30
}



//===================
//  V1 - Via 1 DRC
//===================

GROUP GV1 V1_?

V1_1 {
@ Minimum/maximum size of a V1 is 0.19um 
  X = NOT RECTANGLE V1 == 0.19 BY == 0.19
  X NOT TOUCH EOI
}
V1_2 {
@ Minimum space between V1s is 0.21um
  EXT V1 < 0.21 ABUT<90 SINGULAR REGION
}
V1_3 {
@ Space between V1s within array great or equal than 4*4 is 0.25
  X = SIZE V1 BY 0.155 OVERUNDER
  Y = SIZE X BY 0.65 UNDEROVER
  Z = V1 INSIDE Y
  EXT Z < 0.25 ABUT<90 SINGULAR REGION
}
V1_4_5_8 {
@ V1 enclosure by M1 is 0.005
@ Extension of M1 line end outside of V1 is 0.05
@ For V1 enclosed at the 90 degree corner by M1, M1 extension at least along one direction outside V1 is 0.05
  X = RECTANGLE ENC (V1 NOT INTERACT EXCLU) (M1 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.05 OPPOSITE 0.005 OPPOSITE 0.05 OPPOSITE

  ENC X M1 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M1 < 0.05 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V1 NOT M1
}
V1_6_7_9 {
@ V1 enclosure by M2 is 0.005
@ Extension of M2 line end outside of V1 is 0.03
@ For V1 enclosed at the 90 degree corner by M2, M2 extension at least along one direction outside V1 is 0.05
  X = RECTANGLE ENC (V1 NOT INTERACT EXCLU) (M2 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.03 OPPOSITE 0.005 OPPOSITE 0.03 OPPOSITE

  ENC X M2 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M2 < 0.03 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V1 NOT M2
}
//Skip V1_10: V1 must be enclosured by both M1 and M2. (Covered by V1_4_5_8 and V1_6_7_9.)

V1_11 {
@ Via1 number on HV region must be >=2 Via1
  (HVM1 AND HVM2) INTERACT HVV1 ==1
}
V1_Note {
@ Minimum No. of V1s when either or both M1 and M2 width > 0.9um is 2
  X = (M1 AND M2) WITH WIDTH > 0.9
  X INTERACT V1 <2
}


//===================
//  V2 - Via 2 DRC
//===================

GROUP GV2 V2_?

V2_1 {
@ Fixed V2 size is 0.19
  X=  NOT RECTANGLE V2 == 0.19 BY == 0.19
  X NOT TOUCH EXCLU
}
V2_2a {
@ Space between V2s is 0.21
  EXT V2 < 0.21 ABUT<90 SINGULAR REGION
}
V2_2b {
@ Space between V2s within array greater or equal to 4*4 is 0.25
  X = SIZE V2 BY 0.155 OVERUNDER
  Y = SIZE X BY 0.65 UNDEROVER
  Z = V2 INSIDE Y
  EXT Z < 0.25 ABUT<90 SINGULAR REGION
}
V2_3_4_7 {
@ V2 enclosure by M2 is 0.005
@ Extension of M2 line end outside of V2 is 0.05
@ For V2 enclosed at the 90 degree corner by M2, M2 extension at least along one direction outside V2 is 0.05
  X = RECTANGLE ENC (V2 NOT INTERACT EXCLU) (M2 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.05 OPPOSITE 0.005 OPPOSITE 0.05 OPPOSITE

  ENC X M2 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M2 < 0.05 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V2 NOT M2
}
V2_5_6_8 {
@ V2 enclosure by M3 is 0.005
@ Extension of M3 line end outside of V2 is 0.03
@ For V2 enclosed at the 90 degree corner by M3, M3 extension at least along one direction outside V2 is 0.03
  X = RECTANGLE ENC (V2 NOT INTERACT EXCLU) (M3 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.03 OPPOSITE 0.005 OPPOSITE 0.03 OPPOSITE

  ENC X M3 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M3 < 0.03 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V2 NOT M3
}
V2_10 {
@ Minimum No. of Vns when either or both M2 and M3 width > 0.9um is 2
  X = (M2 AND M3) WITH WIDTH > 0.9
  X INTERACT V2 <2
}
//Skip V2_11: V2 must be enclosured by both M2 and M3. (Covered by V2_3_4_7 and V2_5_6_8.)

V2_12 {
@ Via2 number on HV region must be >=2 Via2
  (HVM2 AND HVM3) INTERACT HVV2 ==1
}

//===================
//  V3 - Via 3 DRC
//===================

GROUP GV3 V3_?

V3_1 {
@ Fixed V3 size is 0.19
  X=  NOT RECTANGLE V3 == 0.19 BY == 0.19
  X NOT TOUCH EXCLU
}
V3_2a {
@ Space between V3s is 0.21
  EXT V3 < 0.21 ABUT<90 SINGULAR REGION
}
V3_2b {
@ Space between V3s within array greater or equal to 4*4 is 0.25
  X = SIZE V3 BY 0.155 OVERUNDER
  Y = SIZE X BY 0.65 UNDEROVER
  Z = V3 INSIDE Y
  EXT Z < 0.25 ABUT<90 SINGULAR REGION
}
V3_3_4_7 {
@ V3 enclosure by M3 is 0.005
@ Extension of M3 line end outside of V3 is 0.05
@ For V3 enclosed at the 90 degree corner by M3, M3 extension at least along one direction outside V3 is 0.05
  X = RECTANGLE ENC (V3 NOT INTERACT EXCLU) (M3 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.05 OPPOSITE 0.005 OPPOSITE 0.05 OPPOSITE

  ENC X M3 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M3 < 0.05 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V3 NOT M3
}
V3_5_6_8 {
@ V3 enclosure by M4 is 0.005
@ Extension of M4 line end outside of V3 is 0.03
@ For V3 enclosed at the 90 degree corner by M4, M4 extension at least along one direction outside V3 is 0.03
  X = RECTANGLE ENC (V3 NOT INTERACT EXCLU) (M4 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.03 OPPOSITE 0.005 OPPOSITE 0.03 OPPOSITE

  ENC X M4 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M4 < 0.03 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V3 NOT M4
}
V3_10 {
@ Minimum No. of Vns when either or both M3 and M4 width > 0.9um is 2
  X = (M3 AND M4) WITH WIDTH > 0.9
  X INTERACT V3 <2
}
//Skip V3_11: V3 must be enclosured by both M3 and M4. (Covered by V3_3_4_7 and V3_5_6_8.)

V3_12 {
@ HV via3 must be couple Via3
  (HVM4 AND HVM3) INTERACT HVV3 ==1
}

//========================
//  TV - Top Via DRC
//========================

GROUP GTV TV_?

TV_1 {
@ Fixed TV2 size is 0.36
  X=  NOT RECTANGLE TV2 == 0.36 BY == 0.36
  X NOT TOUCH EXCLU
}
TV_2 {
@ Space between TV2s is 0.34
  EXT TV2 < 0.34 ABUT<90 SINGULAR REGION
}
TV_4_5 {
@ TV2 enclosure by metal layer directly underneath TV2 is 0.01
@ Extension of Mn line end outside of TV2. Mn is a metal layer directly underneath TV2 is 0.05
  X = RECTANGLE ENC (TV2 NOT INTERACT EXCLU) (M4 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.01 OPPOSITE 0.05 OPPOSITE 0.01 OPPOSITE 0.05 OPPOSITE

  ENC X M4 < 0.01 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M4 < 0.05 OPPOSITE
  INT Y < 0.01 ABUT==90 INTERSECTING ONLY
  TV2 NOT M4
}
TV_6_7 {
@ TV2 enclosure by TM2 is 0.03
@ Extension of TM2 line end outside of TV2 is 0.07
  X = RECTANGLE ENC (TV2 NOT INTERACT EXCLU) (TM2 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.03 OPPOSITE 0.07 OPPOSITE 0.03 OPPOSITE 0.07 OPPOSITE

  ENC X TM2 < 0.03 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] TM2 < 0.07 OPPOSITE
  INT Y < 0.03 ABUT==90 INTERSECTING ONLY
  TV2 NOT TM2
}
//Skip TV_10: TV must be enclosured by M4 and TM.

TV_11 {
@ HV Top Via must be couple TV2
  (HVTM2 AND HVM4) INTERACT HVTV2 ==1
}


//=========================
//  DDNW - Deep N-Well DRC
//=========================

GROUP GDDNW DDNW_?

DDNW_1 {
@ Min DDNW width is 12.00um
  INT DDNW < 12 ABUT<90 SINGULAR REGION 
}
DDNW_2a{
@ Minimum space between 2 DDNW regions with the same potential is 8.00   
@ Merge if space is less than 8.0um
  EXT DDNW < 8.00 ABUT<90 SINGULAR REGION CONNECTED
}
DDNW_2b{
@ Minimum space between 2 DDNW regions with different potential is 8.00           
  EXT DDNW < 8.0 ABUT<90 SINGULAR REGION NOT CONNECTED
}
DDNW_3 {
@ Minimum DDNW enclosure of PWHT space is 7.0um
  ENC PWHT DDNW < 7.0 ABUT<90
}
DDNW_4 {
@ Minimum space of DDNW to NW is 4um
  EXT DDNW NW < 8 ABUT<90 SINGULAR REGION
}
DDNW_5 {
@ Minimum space of DDNW enclosure N+AA plug in DDNW 3.50um
  ENC (NTAP AND DDNW) DDNW <  3.50 ABUT<90 SINGULAR REGION
}
DDNW_6 {
@ Minimum space of DDNW to N+AA outside DDNW is 8.0um
  EXT LMMOSN DDNW < 8.0 ABUT<90 SINGULAR REGION
}
DDNW_7 {
@ Minimum space of DDNW enclosure P+AA in DDNW 5.50um
  ENC HMOSP DDNW < 5.50 ABUT<90 SINGULAR REGION
}
DDNW_8 {
@ Minimum space of DDNW to P+AA plug outside DDNW is 5.00
  EXT (PTAP NOT DDNW) DDNW < 5.00 ABUT<90 SINGULAR REGION
}
DDNW_9 {
@ Recommend add P+ pickup guard ring between adjacent DDNW and DDNW to NW/MVNW
  PWAA = (AA AND PW) AND SP
  HOLE_PWAA = HOLES PWAA 
  DDNW NOT INSIDE HOLE_PWAA
  HOLE_PWAA INTERACT DDNW >= 2
}
DDNW_10A{
@ Must add guard ring surround DDNW and PWHT 
@ If there is NO active HVPMOS inside:Add one N+ pick up guard rings inside DDNW
  DDNW_NHV = DDNW NOT (DDNW INTERACT HMOSP)
  HOLE_AA  = HOLES NTAP
  DD_HOLE  = DDNW_NHV INTERACT HOLE_AA
  DDNW_NHV NOT DD_HOLE
}
DDNW_10B1{
@ If there is active HVPMOS inside:Add N+ pick up guard rings at DDNW/PW boundary.
  DDNW_HV = DDNW INTERACT HMOSP
  HOLE_AA  = HOLES NTAP
  HMOSP_HOLE  = HOLE_AA ENCLOSE HMOSP
  DDNW_HV NOT INTERACT HMOSP_HOLE
}
DDNW_10B2{
@ If there is active HVPMOS inside: Add N+ pick up guard ring at DDNW/PWHT boundary.
  DDNW_HV = DDNW INTERACT HMOSP
  NT_HV  = NTAP AND DDNW
  N_PWHO = HOLES NT_HV 
  NW_NT  = N_PWHO ENCLOSE PWHT
  DDNW_PWHT = DDNW_HV INTERACT PWHT
  DDNW_PWHT NOT INTERACT NW_NT
}
DDNW_10C{
@ Add one P+pick up guard ring inside PWHT at DDNW/PWHT boundary.
  HOLE_PK = HOLES PTAP INNER EMPTY
  PWHT_PH = PWHT ENCLOSE HOLE_PK
  PWHT NOT PWHT_PH
}

//==================================
//  HRP - High Resistance Poly DRC
//==================================

GROUP GHRP HRP_?

//HRP.1 Suggest resistor aquare number >5 for precision Rs

HRP_2 {
@ Minimum extension from HRP region beyond a PO resistor region is 0.20
  ENC GT_HRP HRP < 0.20 ABUT SINGULAR REGION
}
HRP_3_4 {
@ Minimum clearance from HRP region to Polygate of NMOS/PMOS is 0.20
  GT_MOS = GT INTERACT GATE
  EXT HRP GT_MOS < 0.20 ABUT SINGULAR REGION
}
HRP_5 {
@ Minimum and maximum P+ implant for pickup overlap with SAB is 0.3
  SP_HRP = SP INTERACT HRP
  SAB_HRP = SAB INTERACT HRP
  INT SP_HRP SAB_HRP < 0.3 ABUT<90 SINGULAR REGION
  SIZE_SP = SIZE SP_HRP BY -0.3
  AND SIZE_SP SAB_HRP
}
HRP_6 {
@ Minimum extension from SAB region beyond a PO resistor region is 0.22
  ENC GT_HRP SAB < 0.22 ABUT<90 SINGULAR REGION
}
HRP_7 {
@ Minimum width of Poly region for high resistance poly resistor is 2.0
  INT HR_GT < 2.0 ABUT<90 SINGULAR REGION
}
HRP_8 {
@ Dummy layer "HRPDMY" is needed for DRC for high resistance Poly region
  SAB_HRP = SAB INTERACT HRP
  SAB_HRP NOT INTERACT HRPDMY
}
HRP_9 {
@ S/D or LDD is not allowed in HRP region.
  NDRF AND HRP
  PDRF AND HRP
  NLL AND HRP
  NLH AND HRP
  PLL AND HRP
  PLH AND HRP
}



//=================================== 
//  MIM - Metal-Insulator-Metal DRC
//===================================

CAP_MIM  = MIM INTERACT TV2
DMY_MIM  = MIM NOT CAP_MIM

M4_CAPMIM = (M4 INTERACT CAP_MIM) AND MIMDMY
M4_DMYMIM = (M4 NOT INTERACT CAP_MIM) AND MIMDMY

TV_MIM = TV2 AND CAP_MIM
TV_CAPM4 = (TV2 AND M4_CAPMIM) NOT TV_MIM

WID_DMY = INT DMY_MIM == 2 REGION
LET_DMY = LENGTH WID_DMY == 5

CVn = (V1 OR V2) OR V3
CMn = (M1 OR M2) OR M3


GROUP GMIM MIM_?

MIM_1a {
@ MIM.1a
@ Min MIM width as capacitor top plate is 3.00
  INT CAP_MIM < 3.0 ABUT<90 SINGULAR REGION
}
MIM_1b {
@ MIM.1b
@ If capacitor lager than 25*25, please use combination of smaller capacitor
  X = LENGTH CAP_MIM > 25
  CAP_MIM WITH EDGE X
}
MIM_2 {
@ MIM.2
@ Space between MIMs as top plate is 1.20
  EXT CAP_MIM < 1.20 ABUT<90 SINGULAR REGION
}
MIM_3 {
@ MIM.3
@ MIM extension outside of Via which connect to this MIM is 0.24
  ENC TV_MIM CAP_MIM < 0.24 ABUT<90 SINGULAR REGION
  (TV2 INTERACT MIM) NOT MIM
}
MIM_4 {
@ MIM.4
@ Bottom plate(Mn) extension outside of MIM region is 0.40
  ENC CAP_MIM M4_CAPMIM < 0.40 ABUT<90 OUTSIDE ALSO SINGULAR REGION
}
MIM_5 {
@ MIM.5
@ Bottom metal (Mn) width in capacitor is 30
@ If capacitor larger than 30*30,please use combination of smaller capacitor
@ For this bottom Metal structure,the slot rules don't need follow.
  SIZE M4_CAPMIM BY 15 UNDEROVER
}
MIM_6a {
@ MIM.6a
@ Density of MIM pattern is 3%
@ Dummy MiM pattern suggested 2 X 5um with line space 1.5um is required if density less than 3%
  DENSITY MIMi < 0.03
}
MIM_6b {
@ MIM.6b (GuideLine, could be controled by User)
@ Dummy MIM pattern suggested 2*5 um*um
  DMY_MIM NOT WITH EDGE LET_DMY
}
MIM_6c {
@ MIM.6c (GuideLine, could be controled by User)
@ Dummy MIM pattern suggested 2*5 um*um with space 1.5 um
  Z = DMY_MIM WITH EDGE LET_DMY
  EXT Z < 1.5 ABUT<90 SINGULAR REGION
}
MIM_7 {
@ MIM.7
@ Capacitor bottom metal extension outside of Via connect to Mn is 0.12
  ENC TV_CAPM4 M4_CAPMIM < 0.12 ABUT<90 SINGULAR REGION
}
MIM_8 {
@ MIM.8
@ Clearance from top Via to MiM region is 0.40
  EXT CAP_MIM TV_CAPM4 < 0.40 ABUT<90 SINGULAR REGION
}
MIM_9 {
@ MIM.9
@ Space between two Vias on MiM is 1.00
  X= EXT TV_MIM < 1.0 ABUT<90 SINGULAR REGION
  X INSIDE CAP_MIM
}
MIM_10 {
@ MIM.10
@ Space between Vias on bottom metal(Mn) is 0.36
  X= EXT TV_CAPM4 < 0.36 ABUT<90 SINGULAR REGION
  X INSIDE M4
}
MIM_11 {
@ MIM.11
@ Dummy MiM width is 0.40
  INT DMY_MIM < 0.4 ABUT<90 SINGULAR REGION
}
MIM_12 {
@ MIM.12
@ Space between dummy MIM and MIM region is 0.80
  EXT CAP_MIM DMY_MIM < 0.8 ABUT<90 SINGULAR REGION
}
MIM_13 {
@ MIM.13
@ Clearance from mim capacitor bottom Mn to dummy Mn is 0.80
  EXT M4_CAPMIM M4_DMYMIM < 0.8 ABUT<90 SINGULAR REGION
}
MIM_14 {
@ MIM.14
@ Clearance from MiM cap bottom Metal(Mn) with Via to below all Metal layers is 2.0
  EXT M4_CAPMIM CMn < 2.00 ABUT<90 SINGULAR REGION
  M4_CAPMIM AND CMn
}
MIM_15 {
@ Via and metal layers under MIM region are not allowed.
@ One DRC dummy layer "MIMDMY" is needed to separate MIM region from metal interconnection
  AND CVn CAP_MIM
  AND CMn CAP_MIM
}
MIM_16 {
@ MIM.16
@ MiM region cross bottom plate Metal(Mn) region is not allowed
  MIM NOT INSIDE M4
}
MIM_17 {
@ MIM.17
@ Both active and passive devices under MIM region are not allowed
  X = (AA OR GT) OR RESNW
  AND X CAP_MIM
}
MIM_18 {
@ MIM.18
@ Minimum extension of dummy layer MIMDMY beyond Mn region is 1.80
  ENC M4 MIMDMY < 2.0 ABUT<90 SINGULAR REGION
}


//==========================================
//  PI - Guidline for polymide layout minima
//==========================================

GROUP GPI PI_?

PI_1 {
@ Space between two polymide open area is 15
  EXT PI < 15 ABUT<90 SINGULAR REGION
}




