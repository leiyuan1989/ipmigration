

//  This Rule File will check 1P8M, you can choose TM2(122)/TV2(123) or M8(68)/V7(76) as top metal2/via2 
//                                                 TM1(120)/TV1(121) or M7(67)/V6(75) as top metal1/via1


//=================================================================================
//|                                                                               |
//|  0.13um 1P8M Calibre DRC rule file for                                        |
//|                                                                               |
//|       SMIC:           0.13um LOGIC 1P8M Salicide 1.2V/2.5V/3.3V Design Rule   |
//|       Doc. No.:       TD-LO13-DR-2001         Rev.: 17P                       |
//|                                                                               |
//|       SMIC:   0.13um Mixed Signal & RF 1P8M Salicide 1.2/2.5/3.3v Design Rule |
//|       Doc. No.:       TD-MM13-DR-2001         Rev.: 7R                        |
//|                                                                               |
//|                                                                               |
//|       Calibre tool version:   2009.4_16.13                                    |
//|                                                                               |
//|===============================================================================|
//|                                                                               |
//|    DISCLAIMER                                                                 |
//|                                                                               |
//|   SMIC hereby provides the quality information to you but makes no claims,    |
//| promises or guarantees about the accuracy, completeness, or adequacy of the   |
//| information herein. The information contained herein is provided on an "AS IS"|
//| basis without any warranty, and SMIC assumes no obligation to provide support |
//| of any kind or otherwise maintain the information.                            |
//|                                                                               |
//|   SMIC disclaims any representation that the information does not infringe any|
//| intellectual property rights or proprietary rights of any third parties.SMIC  |
//| makes no other warranty, whether express, implied or statutory as to any      |
//| matter whatsoever, including but not limited to the accuracy or sufficiency of|
//| any information or the merchantability and fitness for a particular purpose.  |
//| Neither SMIC nor any of its representatives shall be liable for any cause of  |
//| action incurred to connect to this service.                                   |
//|                                                                               |
//|===============================================================================|
//|                                                                               |
//|    STATEMENT OF USE AND CONFIDENTIALITY                                       |
//|                                                                               |
//|   The following/attached material contains confidential and proprietary       |
//| information of SMIC. This material is based upon information which SMIC       |
//| considers reliable, but SMIC neither represents nor warrants that such        |
//| information is accurate or complete, and it must not be relied upon as such.  |
//| This information was prepared for informational purposes and is for the use by|
//| SMIC's customer only. SMIC reserves the right to make changes in the          |
//| information at any time without notice.                                       |
//|   No part of this information may be reproduced, transmitted, transcribed,    |
//| stored in a retrieval system, or translated into any human or computer        |
//| language, in any form or by any means, electronic, mechanical, magnetic,      |
//| optical, chemical, manual, or otherwise, without the prior written consent of |
//| SMIC. Any unauthorized use or disclosure of this material is strictly         |
//| prohibited and may be unlawful. By accepting this material, the receiving     |
//| party shall be deemed to have acknowledged, accepted, and agreed to be bound  |
//| by the foregoing limitations and restrictions. Thank you.                     |
//|                                                                               |
//|===============================================================================|
//|                                                                               |
//|   This file includes SVRF/TVF Technology under license by Mentor Graphics     |
//| Corporation.  "SVRF/TVF Technology" shall mean Mentor Graphics' Standard      |
//| Verification Rule Format ("SVRF") and Tcl Verification Format ("TVF")         |
//| proprietary syntaxes for expressing process rules.  You shall not use SVRF/TVF|
//| Technology unless you are a Mentor Graphics customer with a license to use the|
//| applicable Mentor Graphics software. The exact terms of your obligations and  |
//| rights are governed by your respective license.  You shall not use SVRF/TVF   |
//| Technology except: (a) for your internal business purposes and (b) for use    |
//| with Mentor Graphics' Calibre tools. All SVRF/TVF Technology constitutes or   |
//| contains trade secrets and confidential information of Mentor Graphics or its |
//| licensors. You shall not make SVRF/TVF Technology available in any form to any|
//| person other than your employees and on-site contractors, excluding Mentor    |
//| Graphics competitors, whose job performance requires access and who are under |
//| obligations of confidentiality.                                               |
//|                                                                               |
//|===============================================================================|
//|                                                                               |
//| 1. Skip the following rules:                                                  |
//|       1)      Mark Rules                                                      |
//|       2)      Seal Ring and Guard Ring rules                                  |
//|       3)      Note Rules                                                      |
//|       4)      CDR Rules                                                       | 
//|       5)      NC.3; PC.3; MVN.3; MVP.3                                        |
//|       6)      SL.5; FUSE.11; FUSE.19; PI.2                                    |
//| 2. Please pay attention to the "User Choice" block to make some useful choices|
//|										  |
//|===============================================================================|
//|                                                                               |
//| Revision History :                                                            |
//|                                                                               |
//|   Rev         Date           Who                      What                    |
//| -------  -------------  --------------  --------------------------------------|
//| V2.2_0T       07-Nov-2011   Eric Wang      Remove DG_7,Update PA_3 Update NW_6|
//|                                     Add datatype for  DNW,HRPDMY,MIMDMY,ALPA ;|
//|                                  Update CT_5,CT_5a for device AA and pickup AA|
//|                           Update CT_5,CT_5a,PA_3 and Update Vn_10(n=1,2,3,4,5)|
//|                              make switch for Vn_10(n=1,2,3,4,5)in user choice |
//|                                                           Update GBD rules    |
//|                                        Update AA_7,AA_9,PSUB_2a,PSUB_2b,PSUB_7|
//|                                               GT_Note3,NLL_3,NLL_4,PLL_3,PLL_4|
//|                                                NLH_3,NLH_4,PLH_3,PLH_4,SN_3   |
//|                                                SN_4,SP_3,SP_4,SP_10,CT_5a     |
//|                                                Vn_10(n=2,3,4,5),PI_2,RDL_PA2_8|
//|                                                RDL_PI_3,RDL_PI_8,PA_3,        |
//|                                          Modify RESOLUTION 5->1               |
//|   2.7     27-July-2011   Zhitao Chen     Recover DG_5 back as version 2.5     |
//|                                           to ignore BJT and diode.            |
//|   2.6     26-July-2011   Zhitao Chen     Fix false alarms of PSUB_4 TM2_5     |
//|                                           in inductor and AA_10 in RESNW.     |
//|                                          Add BJT and diode as devices in DG_5.|
//|                                          Change GT_11 into only check channel.|
//|                                          Comment out GT_12, covered by SP_12. |
//|                                          Optimize DNW_4 to find violation     |
//|                                           not depending on NW hole in DNW.    |
//|   2.5     20-July-2011   Zhitao Chen     Add option for 0.11um process DG_4.  |
//|   2.4     20-July-2011   Zhitao Chen     Remove code for DNW in AA_10.        |
//|   2.3     19-July-2011   Zhitao Chen     SN_1/2 SP_1/2 update to avoid false  |
//|                                           alarm at corner to corner patterns. |
//|                                          Vn_2b updated to 4x4.                |
//|   2.2     12-July-2011   Zhitao Chen     DG_4 0.4 ==> 0.27                    |
//|                                          DG_5 add RESNW and RESAA into code.  |
//|                                          V1_2b Vn_2b TV_2b updated.           |
//|                                          NW_4 divided into NW_4 NW_4a NW_4b.  |
//|                                          DNW_3 optimized to cover cases       |
//|                                           that NW is not full ring around DNW.|
//|                                          Optimize M*_3c with ABUT<89.9 instead|
//|                                           of <90 to avoid false alarm of some |
//|                                           non-orthogonal patterns.            |
//|   2.1     26-May-2011    Zhitao Chen     Update MVN_1/2 MVP_1/2 to follow doc.|
//|                                          Add AA_10 DG_7 GT_Note3.             |
//|                                                                               |
//|   2.0     22-March-2011  Zhitao Chen     Change HRP_1~9 into HRP_3~11         |
//|                                          Add HRP_1 HRP_2 (min W & space 0.30) |
//|                                          Del NW_6 NW_7ab, change NW_8 to NW_5 |
//|                                          NW_4 diff net space 1.0 -> 1.2       |
//|                                          PSUB_8 added, PSUB AND DNW           |
//|                                          RDL_PA2_14 RDL_PI_14 added. den 0~50%|
//|                                          SN_9 updated, add NWH for post LOTA  |
//|                                          Vn.2b changed into 3x3 0.29um.       |
//|                                          Add TV.2b 3x3 0.54um.                |
//|                                          GBD (border rules) added             |
//|                                           GBD option is added for user.       |
//|                                           It should be checked only for chip. |
//|                                           Calibre ver >= 2008.3 needed for it.|
//|                                          And simply change comment of rules:  |
//|                                           DG_4, DG_6, NW_3.                   |
//|*******************************************************************************|
//|  Old File Name : SmicDRL16PM5R_cal013_mixlog_sali_p1mt8_2tm_122533.drc        |
//|   1.0     09-Aug-2010    Seek Rui        Correct PA_3: TM2_SL->TM2 OR TM2SLOT1|
//|                                          Logic DR update to 16P:              |
//|                                            Modify DG.4: 0.33->0.4,            |
//|                                                         DRC will check DG     |
//|                                                         enclose channel on    |
//|                                                         both X/Y direction.   |
//|*******************************************************************************|
//|  Old File Name : SmicDRL15RM4R_cal013_mixlog_sali_p1mt8_2tm_122533.drc        |
//|   1.5     29-Dec-2009    Seek Rui        Modify AA.7: PACT -> PTAP.           |
//|                                          Modify PA.3: PA must covered by TM.  |
//|                                          Modify NW.4, NW.7ab: space between   |
//|                                                 NWRs should follow different  |
//|                                                 potential rule.               |
//|                                          Delete TV2_6_7: covered by TM2_3.    |
//|   1.4     12-Jun-2009    Seek Rui        Add layer AADUM(10:1)                |
//|                                          Modify BAD_IMP rule check:           |
//|                                                 NOT INCLUDE AADUM.            |
//|   1.3.a   01-Jun-2009    Seek Rui        Modify NC.2/PC.2                     |
//|                                                 TM2.10a/TM2.10b/TM2.10c/TM2.10d |
//|                                                 "INTERACT" --> "AND"          |
//|   1.2     07-May-2009    Seek Rui        Modify NC.1/NC.2/PC.1/PC.2           |
//|   1.1     14-Apr-2009    Seek Rui        Update DRC to 5R follow mixed RF DR. |
//|                                          Creat 2TM DRC for it.                |
//|                                          Add PA rules.                        |
//|                                          Modify TM2.10c, correct the define of|
//|                                                 ind_rf inductor.              |
//|*******************************************************************************|
//|  Old File Name : SmicDRL14RM4R_cal013_mixlog_sali_p1mtt8_1225.drc             |
//|   1.2     12-Dec-2008    Carolyn Y       Redefine GTO(add GTDUM)              | 
//|                                          Add layer LOGO(26), MD(130)          |
//|                                              RDL_PA2 rules, RDL_PI rules      |
//|                                          Modify ALPA.1, PA.1                  |
//|                                                 Convention_LAT.3a(NTAP->NTAPi)|
//|                                                 Convention_LAT.3b(PTAP->PTAPi)|
//|                                          Optimize NLL_8/PLL_8/NLH_8/PLH_8     |
//|   1.1    25-Nov-2008     Jenny Jiang     Merge 0.13Logic/Mixed 1.2/2.5&1.2/3.3V|
//|                                          Add option to select 1.2/2.5 or 1.2/3.3V|
//|                                          Update to 4R,Optimize SLOT rule check|
//|                                          Optimize V6_3_4_7;CAP_MIM;MIM.3&MIM.10|
//|                                          Add BMIM & SMIM;3T-inductor rule check|
//|                                          Update to TM.10a;TM10b;TM10c;TM10d rule check|
//|*******************************************************************************|
//|  Old File Name : SmicDRL14RM3T_cal013_mixlog_sali_p1mtt8_1225.drc             |
//|   1.4     25-Aug-2008    Carolyn Y      No update,just follow the version of p1mt6|
//|   1.3     18-Jan-2008    Drincy Wei     Optimize NC.4,PC.4 (add "OPPOSITE")   |
//|						     DG.1(TOUCH -> INTERACT)	  |
//|                                                  NW.4(NW-> NW NOT INTERACT DG)|
//|   1.2     31-Dec-2007    Karen Kang     add Mn.3a,Mn.3b for choice rules	  |
//|					    add PA.1,modify the GTi->GT0 in connect part|
//|   1.1     21-Nov-2007    Karen Kang     update to 14R,no rule update          |
//|*******************************************************************************|
//|  Old File Name : SmicDRL13RM2T_cal013_mixlog_sali_p1mtt8_1255.drc             |
//|   1.1     11-Jul-2007    Karen Kang     update to LO (13R),MM(3T)             |
//|					    modify GT.8,GT.9,GT.11,MIM.13,SP.7,SP.9|	
//|					    add convention latch up rules         |	
//|                                         RESOLUTION:1-->5,Mn.8                 |
//|*******************************************************************************|
//|  Old File Name : SmicDRL12RM2T_cal013_mixlog_sali_p1mtt8_1255.drc             |
//|   1.5     22-Jun-2007    Karen Kang     redefine PW_INNW                      | 
//|                                         Modify NC.1 NC.2,PC.1,PC.2            |
//|   1.4     27-Mar-2007    Shirley Chen   update NC.1,NC.2;PC.1,PC.2            |
//|					    change file name                      |
//|   1.3     25-Jan-2007    Karen Kang     Optimize NC.4,PC.4 definition         |
//|   1.2     04-Jan-2007    Karen Kang     Optimize SLOT definition              |
//|   1.1     06-Dec-2006    Karen Kang     Update to 12R , based on DR           |
//|                                         Add "ALPA" layer definition           |
//|                                         Optimize Slot definition;HRP.1,HRP.2, |
//|                                         FUSE.1,FUSE,6;TM.4                    |
//|*******************************************************************************|
//|  Old File Name : SmicDR11R_cal013_mixlog_p1mtt8_2.5V.drc                      |
//|   1.1     12-May-2006    Shirley Chen   Update to 11R , based on DR           |
//|					    Add Top metal density check (TM.7)    |
//|*******************************************************************************|
//|  Old File Name : SmicDR10R_cal013_mixlog_p1mtt8_2.5V.drc                      |
//|                                                                               |
//|   1.4     25-Apr-2006    Shirley Chen   Modify SAB.9: 0.30-->0.28             |
//|   1.3     13-Mar-2006    Drincy Wei     Optimize rule NC_1,NC_2,PC_1,PC_2     |
//|   1.2     16-Nov-2005    Jenny Jiang    Add M8/V7 as top metal/via for        |
//|                                         compatible and modify SN.9,GT.11,TM.4 |
//|                                                                               |
//|   1.1     07-Oct-2005    Drifty Yang    Update to 10R                         |
//|                                         Add Fuse rules                        |
//|                                                                               |
//|*******************************************************************************|
//|   Old File Name : SmicDR8R_cal013_log_p1mt8_sali.drc                          |
//|                                                                               |
//|   Rev         Date           Who                      What                    |
//| -------  -------------  --------------  --------------------------------------|
//|   1.4     31-Mar-2005     Jenny Jiang   Modify M1_1/M1_2  to 0.16/0.17.       |
//|                                         Modify AA_11 from 0.32 to 0.30        |
//|   1.3     24-Feb-2005     Jenny Jiang   Merge NC and VTNH to NC;PC and VTPH to| 
//|                                         PC                                    |
//|   1.2     26-Jan-2005     Rimbo Yan     change layer number PC-->16 NC-->21   |
//|   1.1     13-Jan-2005     Rimbo Yan     Update to 8P;modified DNW.2,DNW.5,    |
//|                                         N/PC.3 .4 .7 .8;add N/PC.9 .10;MVP/N  |
//|                                                                               |
//|*******************************************************************************|
//|   Old File Name : SmicDR7R_cal013_log_p1mt8_sali.drc                          |
//|                                                                               |
//|   1.1     20-Dec-2004     Rimbo Yan     Update to 7P;rename VTNH,VTPH to NC,PC|
//|                                                                               |
//|*******************************************************************************|
//|   Old File Name : SmicDR6R_cal013_log_p1mt8_sali.drc                          |
//|                                                                               |
//|   1.2     30-Nov-2004     Rimbo Yan     Add NC check;clean false errors caused|
//|                                         by EXCLU layer                        |    
//|   1.1     18-Oct-2004     Rimbo Yan     Add VTNH,PC rule for high vt N/PMOS   |
//|                                                                               |
//|*******************************************************************************|
//| Old File Name : SmicDR5T_cal013_log_p1mt8_sali.drc                            |
//|                                                                               |
//|   1.2     08-Sep-2004     Rimbo Yan     modified AA_11                        |
//|   1.1     26-Aug-2004     Rimbo Yan     update to 5T; modified NW.7,AA.11,DG4,|
//|                                         DG.7,DG.8                             |
//|                                                                               |
//|*******************************************************************************|
//| Old File Name : SmicDR4R_cal013_log_p1mt8_sali.drc                            |
//|                                                                               |
//|   1.2     13-Jul-2004     Rimbo Yan     modified  DG.7 ,GT.10                 |
//|   1.1     01-Jul-2004     Rimbo Yan     update to 4R; modified M1.6, M1.8,Mn.4|
//|                                         Mn.7,Mn.8,Mn.9,MT.5,V1.4,SL_3_4_5a    |
//|                                                                               |
//|*******************************************************************************|
//| Old File Name : SmicDR3T_cal013_log_p1mt8_sali.drc                            |
//|                                                                               |
//|   1.4     22-Jun-2004     Rimbo Yan      Merge Layer 12 ,13 , AA ;            |
//|                                          modified AA.4 ;SAB.11;MT3            |
//|   1.3     10-Jun-2004     Rimbo Yan      add P/NTAP connect to P/NSD ;        |
//|                                           modified M1_2;NW.5                  |
//|   1.2     15-Feb-2004     Rimbo Yan      modify the SL_3_4_5,SN/P.14,DG.3,SN.6|
//|                                          SN.10 SP.7 M1.14                     |
//|   1.1     04-Feb-2004     Rimbo Yan      update to 3T                         |
//|                                                                               |
//|*******************************************************************************|
//| Old File Name : SmicDR2T_cal013_log_p1mt8_sali.drc                            |
//|
//|   1.10    15-DEC-2003     Micky Yu      Improve 45 wide metal def.; SN.10/12  |
//|                                         DG.8                                  |
//|   1.9     17-OCT-2003     Micky Yu      CT.3; Vn.3; NW.5/8; PD.GT; GT.11; DG.3|
//|                                         Mn.3                                  |
//|   1.8     30-SEP-2003     Micky Yu      MT.3; Add virtual connect colon       |
//|                                         NW_DNW Def; AA.11                     |
//|   1.7     15-SEP-2003     Micky Yu      Move SL_0 to Mn Part,check no-SL metal|
//|   1.6     05-SEP-2003     Micky Yu       Make a choice of Un-check SLOT rules |
//|   1.5     25-AUG-2003     Micky Yu       Improve file structure; Connectivity |
//|                                          GT.4/5; FLT_NW; Vn.10; DG.6          |
//|   1.4     11-AUG-2003     Micky Yu       Mn.3                                 |
//|   1.3     10-JUL-2003     Micky Yu       AA.6; CT.3                           |
//|   1.2     04-JUL-2003     Micky Yu       Mn.3                                 |
//|   1.1     27-JUN-2003     Micky Yu       Rename based on D/R revise to 2T     |
//|                                                                               |
//|*******************************************************************************|
//| Old File Name : SmicDR1T_cal013_log_p1mt8_sali.drc                            |
//|                                                                               |
//|   Rev         Date           Who                      What                    |
//| -------  -------------  --------------  --------------------------------------|
//|   1.4     19-JUN-2003     Micky Yu      Density; add LVS power name; NR.6     |
//|                                         NN.NOTE.4;DG.3/7                      |
//|   1.3     13-JUN-2003     Micky Yu      SN/SP/NLL~PLH.3/4, SN/SP.8,AA.11      |
//|                                         M1.11,M2~7.7; Add Text Layer          |
//|   1.2     09-JUN-2003     Micky Yu      GT.8, AA.10                           |
//|   1.1     09-JUN-2003     Micky Yu      Original version                      |
//|                                                                               |
//|===============================================================================|


//=>  The runset default is to check 1.2/2.5V.
//=>  If you check 1.2/3.3V, then add "//" in front of "DRC UNSELECT CHECK GT_1c2 GT_1c3 CT_4b TM_10d".
//=>  and remove the "//" in front of "DRC UNSELECT CHECK GT_1c1".

//=>   1.2V core   :  no DG
//=>   2.5V/3.3V   :  DG only

//#########################################################
//#  <Choice 1>   For design follow 1.2/2.5V process      #
//#########################################################


//  DRC UNSELECT CHECK GT_1c1 //;  DRC default to check 1.2/2.5V process, and shield this option.
                            //;  If you want to check 1.2/3.3V process, remove "//" from it and shield Choice 2.



//#########################################################
//#  <Choice 2>   For design follow 1.2/3.3V process      #
//#########################################################


  DRC UNSELECT CHECK GT_1c2 GT_1c3 CT_4b //;  DRC default to check 1.2/2.5V process, and open this option.
                                         //;  If you want to check 1.2/3.3V process, add "//" before it and shield Choice 1.


//##########################################
//#  <Choice 3>   For RDL rules choice     #
//##########################################

//There are two series RDL rules, PA2 RDL and PI RDL.
//Default to check PA2 RDL rules
//If design follow PA2 RDL rules, keep default setup, if design follow PI RDL rules
// move the "//" before "DRC UNSELECT CHECK GPA2_RDL", and add "//" before
// "DRC UNSELECT CHECK GPI_RDL"

// DRC UNSELECT CHECK GPA2_RDL
   DRC UNSELECT CHECK GPI_RDL


//#########################################
//#  <Choice 4>   DG_4 for 0.13 or 0.11   #
//#########################################

#DEFINE DG_4_process 13           //; If you run it for 0.11um process, please change it into 11.
                                  //; But it 13 by default, which means for 0.13um process.



//*****************************************************************************************
//   User Choice !  ( Recommended to make some useful choices to speed DRC running time! )
//*****************************************************************************************

//DRC UNSELECT CHECK GMS                //; Mix-Rules checking choice !
                                        //; Open the mark is NOT to check Mix-rules
                                        //; For Logic-User, can open the mark
                                        //; For Mix-User, should mark this command

  DRC UNSELECT CHECK GSL          //;*Please check the M1.8/Mn.8 result before slot check.
                                 //; SLOT checking choice!
                                 //; Open the mark will NOT check "SLOT"

  DRC UNSELECT CHECK SL_1        //; Min slot density of wide metal
                                 //; Open the mark will NOT check "SL_1"

  DRC UNSELECT CHECK GPI         //; PI Guideline checking choice!
                                 //; Open the mark will NOT check "PI"

  DRC UNSELECT CHECK GFUSE       //; FUSE Guideline checking choice!
                                 //; Open the mark will NOT check "FUSE"

  DRC UNSELECT CHECK PA_1        //; PA_1 Guideline checking choice!
                                 //; Open the mark will NOT check "PA_1"

  DRC UNSELECT CHECK M1_3a M1_3b M2_3a M2_3b M3_3a M3_3b M4_3a M4_3b M5_3a M5_3b M6_3a M6_3b
			         //; These rules are suggestion rules
  				 //; Open the mark will NOT check them

  DRC UNSELECT CHECK TM2_7b TM2_7c        //; Guidelines checking choice !
                                        //; Open the mark is NOT to check guidelines
                                        //; TM.7b, TM.7c are guidelines, user can decide follow or not

  DRC UNSELECT CHECK GBD                //; This group should be checked only for chip level GDS.
                                        //; So please comment out this option for chip level GDS.

  DRC UNSELECT CHECK V1_10 V2_10 V3_10 V4_10 V5_10       //; Vn_10(n=1,2,3,4,5) Guideline checking choice,default no to check!
                                                        //; please comment out this option if you want to check Vn_10(n=1,2,3,4,5)

//*************************************
//     DIRECTIVE SECTION
//*************************************

LAYOUT PRIMARY "topcell"
LAYOUT PATH "*.gds"
LAYOUT SYSTEM GDSII 

DRC RESULTS DATABASE "drc_CAL.OUT" ASCII 
DRC SUMMARY REPORT "drc_CAL.SUM"

PRECISION 	1000
RESOLUTION 	1	     

FLAG ACUTE YES
FLAG NONSIMPLE YES
FLAG SKEW YES
//FLAG OFFGRID YES

skew_edge_check {
           @Skew edge check
           DRAWN SKEW }
// does not check layers that do not appear in other rule
// checks used in the run

TEXT DEPTH PRIMARY

LAYOUT TOP LAYER TM2ai TV2ai TM1ai TV1ai M6i V5i M5i V4i M4i V3i M3i V2i M2i V1i M1i
LAYOUT TOP LAYER M8i V7i M7i V6i M6i V5i M5i V4i M4i V3i M3i V2i M2i V1i M1i
LVS POWER NAME "?VDD?" "?VCC?" 




//************************************               
//     INPUT LAYER STATEMENTS
//************************************               

LAYER	KVi	 9  	//	Alignment mark clear out
LAYER   MAP     10 DATATYPE 0 510
LAYER	AAi	510 12 13  //	Active Area / SDG
LAYER   MAP     10 DATATYPE 1 500
LAYER   AADUM   500     //      Dummy AA layer
LAYER	NWi	14  	//	N-Well / N-Tub
LAYER   MAP     19 DATATYPE 0 190
LAYER   DNWi    190     //      Deep Nwell
LAYER 	DGi	29  	//	Dual Gate
LAYER   MAP     30 DATATYPE 1 410
LAYER   GTDUMi  410     //     GT dummy layer
LAYER   MAP     30 DATATYPE 0 420
LAYER   GTi     420     //      Poly Gate, Poly-1, ONO Gate
LAYER   P2i     31      //      Capacitor bottom plate
LAYER   NLLi	35  	//	NMOS LDD Implant for Low VDD
LAYER   NLHi	36  	//	NMOS LDD Implant for High VDD
LAYER   PLHi    37  	//	PMOS LDD Implant for High VDD
LAYER   PLLi    38  	//	PMOS LDD Implant for Low VDD
LAYER   HRPi    39      //      High Resistance Poly Imp
LAYER   SNi 	40  	//	N+ S/D Implant
LAYER   ESD1i   41     	//	ESD1 Implant 
LAYER   SPi 	43  	//	P+ S/D Implant
LAYER   MVPi    44      //      Low Vt Pmos 
LAYER   MVNi    45      //      Low Vt Nmos 
LAYER   VTPHi   46      //       PCH HV VT adjust Implant
LAYER   VTNHi   47      //       NCH HV VT adjust Implant
LAYER   PC1     16      //      High Vt Pmos 
LAYER   NC1     21      //      High Vt Nmos 
LAYER   SABi	48  	//	Resist Protect Oxide / Salicide block
LAYER   CTi  	50  	//	Contact Hole
LAYER   MIMi    58      //      Top Plate of MiM capacitor
LAYER   M1i  	61  	//	Metal-1
LAYER   M2i 	62  	//	Metal-2
LAYER   M3i 	63  	//	Metal-3
LAYER   M4i 	64  	//	Metal-4
LAYER   M5i 	65  	//	Metal-5
LAYER   M6i 	66  	//	Metal-6
LAYER   M7i 	67  	//	Metal-7
LAYER   M8i 	68  	//	Metal-8
LAYER   TM1ai 	120  	//	Top Metal 1
LAYER   TM2ai 	122  	//	Top Metal 2
LAYER   V1i  	70  	//	Via-1 Hole
LAYER   V2i 	71  	//	Via-2 Hole
LAYER   V3i 	72  	//	Via-3 Hole
LAYER   V4i 	73  	//	Via-4 Hole
LAYER   V5i 	74  	//	Via-5 Hole
LAYER   V6i 	75  	//	Via-6 Hole
LAYER   V7i 	76  	//	Via-7 Hole
LAYER   TV1ai 	121  	//	Top Via 1
LAYER   TV2ai 	123  	//	Top Via 2

LAYER   LOGO    26      //      L mark area
LAYER   PAi 	80  	//	Passivation / Pad
LAYER   FUSEi  	81  	//	Fuse Window
LAYER   PIi     82      //      Polyimid passivation layer
LAYER   MAP     83 DATATYPE 0 830
LAYER   ALPAi   830     //      AL Bonding Pad
LAYER   MD      130     //      Metal about topmetal for redistribution;Passivation 2

LAYER   PSUBi  	85  	//	Native Mos area
LAYER   DUMBM   90      //      Block layer for Dummy operation on all Metal
LAYER   RESNW   95  	//	Dummy Pattern for NWELL Resistor
LAYER   MAP     96      DATATYPE 0 310
LAYER   RESP1   310 	//	Dummy Pattern for Poly-1 Resistor
LAYER   MAP     96      DATATYPE 1 300
LAYER   RESP3T  300     //      Dummy Pattern for 3T Resistor
LAYER   RESAA   97  	//	Dummy Pattern for AA Resistor
LAYER	OPCBA	100   	//	Blocking Layer for OPC operation on AA
LAYER	OPCBP	101  	//	Blocking Layer for OPC operation on GT
LAYER	OPCBM	102  	//	Blocking Layer for OPC operation on M1
LAYER	PWH 	105  	//	P Well for HV device
LAYER	NWH 	106  	//	N Well for HV device
LAYER   BORDER  127     //      Border layer for chip level design
LAYER   EXCLU   132     //      Un-DRC region
LAYER   MAP     134     DATATYPE 0 1340
LAYER   DMPNP   1340    //      BJT ID layer
LAYER   CAPBP   137     //      Capacitor Dummy
LAYER   MAP     138     DATATYPE 0 1380
LAYER   DSTR    1380
LAYER   MAP     210     DATATYPE 0 2100
LAYER   HRPDMY  2100    //      HRP dummy
LAYER   MAP     211     DATATYPE 0 2110
LAYER   MIMDMY  2110    //      MIM Dummy Layer            
LAYER   INDMY   212     //      IN Dummy Layer
LAYER   empty   999     //      Only for slot density check


TM1i     = M7i OR TM1ai
TM2i     = M8i OR TM2ai
TV1i     = V6i OR TV1ai
TV2i     = V7i OR TV2ai
KV	= KVi NOT EXCLU
AAO	= (AAi OR AADUM) NOT EXCLU                      
NW	= NWi NOT EXCLU
DNW     = DNWi NOT EXCLU
DG      = DGi NOT EXCLU
GTO	= (GTi OR GTDUMi) NOT EXCLU
NLL 	= NLLi NOT EXCLU
NLH 	= NLHi NOT EXCLU
PLH     = PLHi NOT EXCLU
PLL     = PLLi NOT EXCLU
HRP     = HRPi NOT EXCLU
MIM     = MIMi NOT EXCLU
SN  	= SNi NOT EXCLU
ESD     = ESD1i NOT EXCLU
SP  	= SPi NOT EXCLU
PCi     = PC1 OR VTPHi
NCi     = NC1 OR VTNHi 
PC      = PCi NOT EXCLU
MVP     = MVPi NOT EXCLU
NC      = NCi NOT EXCLU
MVN     = MVNi NOT EXCLU
SAB 	= SABi NOT EXCLU
CTO  	= CTi NOT EXCLU
M1O  	= M1i NOT EXCLU
M2  	= M2i NOT EXCLU
M3  	= M3i NOT EXCLU
M4  	= M4i NOT EXCLU
M5  	= M5i NOT EXCLU
M6  	= M6i NOT EXCLU
M7  	= M7i NOT EXCLU
M8      = M8i NOT EXCLU
TM1     = TM1i NOT EXCLU
TM2  	= TM2i NOT EXCLU
V1O  	= V1i NOT EXCLU
V2  	= V2i NOT EXCLU
V3  	= V3i NOT EXCLU
V4  	= V4i NOT EXCLU
V5  	= V5i NOT EXCLU
V6  	= V6i NOT EXCLU
V7      = V7i NOT EXCLU
TV1     = TV1i NOT EXCLU
TV2  	= TV2i NOT EXCLU
PA  	= PAi NOT EXCLU
P2      = P2i NOT EXCLU
FUSE    = FUSEi NOT EXCLU
PI      = PIi NOT EXCLU
ALPA    = ALPAi NOT EXCLU
PSUB  	= PSUBi NOT EXCLU
GTDUM	= GTDUMi NOT EXCLU
GT0	= GTi NOT (RESP1 OR HRPDMY)

TEXT	LAYER	61 141
TEXT	LAYER	62 142
TEXT	LAYER	63 143
TEXT	LAYER	64 144
TEXT	LAYER	65 145
TEXT	LAYER	66 146
TEXT	LAYER	120 147
TEXT	LAYER	122 148

ATTACH	141	M1i
ATTACH	142	M2i
ATTACH	143	M3i
ATTACH	144	M4i
ATTACH	145	M5i
ATTACH	146	M6i
ATTACH	147	TM1i
ATTACH	148	TM2i
ATTACH	61	M1i
ATTACH	62	M2i
ATTACH	63	M3i
ATTACH	64	M4i
ATTACH	65	M5i
ATTACH	66	M6i
ATTACH	120	TM1i
ATTACH	122	TM2i


CONNECT ALPAi TM2i BY PAi
CONNECT TM2i TM1i BY TV2i 
CONNECT TM1i M6i BY TV1i
CONNECT M6i M5i BY V5i
CONNECT M5i M4i BY V4i
CONNECT M4i M3i BY V3i
CONNECT M3i M2i BY V2i
CONNECT M2i M1i BY V1i
CONNECT M1i GT0 NTAPi PTAPi PSDi NSDi BY CTi
CONNECT NTAPi PSDi
CONNECT PTAPi NSDi
SCONNECT NTAPi NWi
LABEL ORDER TM2i TM1i M6i M5i M4i M3i M2i M1i NTAPi PTAPi NSDi PSDi GT0 NWi 

VIRTUAL CONNECT NAME "?VDD?" "?VCC?" "?VSS?" "?GND?"
VIRTUAL CONNECT COLON YES 



//******************************************       
//    LAYER DERIVATIONS AND OPERATIONS
//******************************************       


//==================
// For Connectivity
//==================

PACTi   = SPi AND AAi
NACTi   = SNi AND AAi

PTAPi   = PACTi NOT NWi
NTAPi   = NACTi AND NWi

PGATEi  = PACTi AND GTi
NGATEi  = NACTi AND GTi

PSDi    = (PACTi NOT PGATEi) TOUCH PGATEi
NSDi    = (NACTi NOT NGATEi) TOUCH NGATEi

AA 	= NOT AAO OPCBA
GT      = NOT GTO OPCBP
CT 	= NOT CTO OPCBM
M1 	= NOT M1O OPCBM
V1 	= NOT V1O OPCBM

//==================
//  Resistor Def
//==================

NWR	= NW INTERACT RESNW
AAR	= AA INTERACT RESAA
GTR	= GT INTERACT RESP1
RESP3T_HRP = RESP3T INTERACT HRP
GTR_3T  = GTR INTERACT RESP3T_HRP
GTR_N3T = GTR NOT GTR_3T

NR	= NWR INSIDE AA
SNNR	= SN INTERACT NWR


//==================
//   Well Def
//==================

DRC:1	= EXTENT
BULK	= SIZE DRC:1 BY 1.0

PW 	= (BULK NOT NW) NOT PSUB

PW_INNW = HOLES NW    // DNW defination
NW_DNW 	= (NW TOUCH PW_INNW) INTERACT DNW 

HVNW	= NW INTERACT (DG OR NWH)
LVNW	= NW NOT HVNW

//==================
// Transistor Def
//==================

PACT	= SP AND AA
NACT	= SN AND AA

PTAP	= PACT NOT NW
NTAP	= NACT AND NW

PGATE	= PACT AND GT
NGATE	= NACT AND GT
GATE	= AA AND GT
PSD	= (PACT NOT PGATE) TOUCH PGATE
NSD	= (NACT NOT NGATE) TOUCH NGATE

NMOS    = NGATE OR NSD

HVAA    = AA AND DG
LVAA    = AA NOT HVAA

PSUB_AA   = AA AND PSUB
PSUB_GT   = GT INTERACT PSUB

NLLAA   = AA AND NLL
NLHAA   = AA AND NLH
PLLAA   = AA AND PLL
PLHAA   = AA AND PLH

INTERGT = GT NOT GATE

//=====================
// CT / Wide Metal Def
//=====================

ACT_CT   = CT AND AA

Res1 = RESP1 OR RESAA
POLY = GT OUTSIDE Res1
GATEI = POLY AND AA
NMOSI = NACT ENCLOSE GATEI
PMOSI = PACT ENCLOSE GATEI

//=====================
//  HRP / MIM  Def
//=====================

GT_HRP   = GT INTERACT HRP
HR_GT    = GT_HRP AND HRPDMY
SAB_GT   = GT_HRP AND SAB

Vn = ((((V1 OR V2) OR V3) OR V4) OR V5) OR TV1
Mn = ((((M1 OR M2) OR M3) OR M4) OR M5) OR M6
Vn_1 = (((V1 OR V2) OR V3) OR V4) OR V5
Mn_1 = (((M1 OR M2) OR M3) OR M4) OR M5
/*
CAP_MIM  = MIM INTERACT (TV OR V7)
DMY_MIM  = MIM NOT CAP_MIM

M7_CAPMIM = (SIZE M7 BY 1.5 UNDEROVER) INTERACT CAP_MIM
M7_DMYMIM = M7 INTERACT DMY_MIM

TV_MIM = TV AND CAP_MIM
TV_CAPM7 = (TV AND M7_CAPMIM) NOT TV_MIM

WID_DMY = INT DMY_MIM == 2 REGION
LET_DMY = LENGTH WID_DMY == 5
*/

TM2A = TM2 INTERACT TV2
DMY_TM2  = TM2 NOT TM2A
WID_TM2Y = INT DMY_TM2 == 2 REGION
LET_TM2Y = LENGTH WID_TM2Y ==5

//********************************************
//         CHECK GROUP DEFINITIONS
//********************************************


//=================
//   Logic Part
//=================

GROUP GLG
  GDNW
  GAA
  GNW
  GNC
  GPC
  GDG
  GGT
  GMVN
  GMVP
  GPSUB
  GNLL
  GPLL
  GNLH
  GPLH
  GSN
  GSP
  GSAB
  GCT
  GM1
  GM2
  GM3
  GM4
  GM5
  GM6
  GTM1
  GTM2
  GV1
  GV2
  GV3
  GV4
  GV5
  GPI
  GALPA
  GTV1
  GTV2
  GFUSE
  GPA
  GPA2_RDL
  GPI_RDL
  GConvention

//===================
//  Mix-Signal Part
//===================

GROUP GMS
  GHRP
  GBMIM
  GP2

//==================================
//  DNW -  Deep N-Well  DRC
//==================================

GROUP GDNW DNW_?

DNW_1 {
@ DNW.1
@ DNW width is 3.00
  INT DNW < 3 ABUT<90 SINGULAR REGION
}
DNW_2 {
@ DNW.2
@ Space between DNWs is 6.00
  EXT DNW < 6.0 ABUT<90 SINGULAR REGION
}
DNW_3 {
@ DNW.3
@ DNW enclosure by NW is 2.0um
 ENC DNW NW < 2.0 ABUT<90 SINGULAR REGION
 DNW COINCIDENT EDGE NW
 DNW OUTSIDE EDGE NW
}
DNW_4 {
@ DNW.4
@ Overlap of NW and DNW is 2.00um.
 X = SIZE DNW BY -2
 (DNW NOT X) NOT NW
}
DNW_5 {
@ DNW.5
@ Space between DNW and NW is 4.10
  X = NW NOT COIN EDGE DNW
  EXT X DNW < 4.10 ABUT<90 REGION
}

//===========================
//  AA - Active Area DRC
//===========================

GROUP GAA AA_?

AA_1_2 {
@ AA width MOS transistors is 0.15
@ AA width for interconnect is 0.15
  INT AA < 0.15 ABUT<90 SINGULAR REGION
}
AA_3 {
@ Space between AAs that are on the same well is 0.21
  EXT AA < 0.21 ABUT<90 SINGULAR REGION
}
AA_4 {
@ N+ AA enclosure by NW is 0.23
  ENC NTAP (NW NOT NWR) < 0.23 ABUT<90 SINGULAR REGION
}
AA_5 {
@ Space between NW and N+ AA is 0.30
  EXT NW NACT < 0.30 ABUT<90 SINGULAR REGION
}
AA_6 {
@ P+ AA enclosure by NW is 0.30
  ENC PACT NW < 0.30 ABUT<90 SINGULAR REGION
}
AA_7 {
@ Space between NW to P+AA inside PW is 0.23
  EXT (PACT AND PW) NW < 0.23 ABUT<90 SINGULAR REGION
}
AA_8 {
@ AA area is 0.1 (in um2)
  AREA AA < 0.1
}
AA_9 {
@ Space between N+AA and NW which enclosure a DNW is 0.4
  EXT NACT (NW INTERACT DNW) < 0.4 ABUT<90 SINGULAR REGION
}
AA_10 {
@ AA or AADUM pattern is not allowed to straddle on a boundary of NW.
 (NW NOT INTERACT RESNW) INSIDE EDGE AA
}

//==================================
//  NW -  N-Well  DRC
//==================================

GROUP GNW NW_?

NW_1 {
@ NW_1
@ NW width is 0.6 
  INT NW < 0.60 ABUT<90 SINGULAR REGION
}
NW_2 {
@ NW_2
@ NW width for NW resistor is 1.60
  INT NWR < 1.6 ABUT<90 SINGULAR REGION
}
NW_3 {
@ NW_3
@ Space between 1.2V NWs at same net is 0.60um
  EXT NW < 0.60 ABUT<90 SINGULAR REGION CONNECTED
}
NW_4 {
@ NW_4
@ Minimum space between 1.2V NWs with different net is 1.0um
  EXT (NW NOT INTERACT DG) < 1.0 ABUT<90 SINGULAR REGION NOT CONNECTED
  EXT ((NW INTERACT RESNW) NOT INTERACT DG) < 1.0 ABUT<90 SINGULAR REGION
}
NW_4a {
@ NW_4a
@ Minimum space between 1.2V NW and 2.5V/3.3V NW on different net is 1.2um.
  EXT (NW NOT INTERACT DG) (NW INTERACT DG) < 1.2 ABUT<90 SINGULAR REGION NOT CONNECTED
}
NW_4b {
@ NW_4b
@ Minimum space between 2.5V/3.3V NWs on different net is 1.2um.
  EXT (NW INTERACT DG) < 1.2 ABUT<90 SINGULAR REGION NOT CONNECTED
}
NW_5 {
@ NW_5
@ NW area is 0.92
  AREA NW < 0.92
}
NW_6 {
@ It is not allowed if N+AA/P+AA straddle on a boundary of the NW 
  (NW NOT INTERACT RESNW) INSIDE EDGE NACT
  (NW NOT INTERACT RESNW) INSIDE EDGE PACT
}

//=====================================================
// PSUB - Design minima for layer to define Native NMOS 
//=====================================================

GROUP GPSUB PSUB_?

PSUB_1 {
@ PSUB_1
@ PSUB width is 0.6
  INT PSUB < 0.60 ABUT<90 SINGULAR REGION
}
PSUB_2a {
@ PSUB.2a
@ 1.2V NMOS channel length is 0.30
  X = PSUB_GT INSIDE EDGE (PSUB_AA AND SN)
  INT X < 0.3 ABUT<90 REGION
}
PSUB_2b {
@ PSUB.2b
@ 2.5/3.3V NMOS channel length is 1.00
  X = PSUB_GT INSIDE EDGE ((PSUB_AA AND SN) INTERACT DG)
  INT X < 1.0 ABUT<90 REGION
}
PSUB_3 {
@ PSUB.3
@ Space between PSUBs is 0.60
  EXT PSUB < 0.60 ABUT<90 SINGULAR REGION
}
PSUB_4 {
@ PSUB.4
@ Fixed AA enclosure by PSUB is 0.22, except NMOS capacitor
  X = SIZE (PSUB_AA NOT INTERACT CAPBP) BY 0.22
  Y = PSUB INTERACT PSUB_AA
  Z = NMOS INTERACT CAPBP
  (XOR X Y) NOT INTERACT Z
  (PSUB NOT INTERACT AA) NOT INTERACT INDMY
}
PSUB_5 {
@ PSUB.5
@ Space between PSUB and AA is 0.37
  EXT PSUB AA < 0.37 ABUT<90 SINGULAR REGION
}
PSUB_6 {
@ PSUB.6
@ Space between PSUB and NW is 1.2
  EXT PSUB NW < 1.2 ABUT<90 SINGULAR REGION
}
PSUB_7 {
@ PSUB.7
@ Extension of Native NMOS poly gate outside of AA is 0.31
  X = (PSUB_AA AND SN) INSIDE EDGE PSUB_GT
  ENC X PSUB_GT < 0.31 ABUT<90 REGION
}
PSUB_8 {
@ PSUB.8
@ PSUB inside or overlap or cross over a Deep N-Well is not allowed
 PSUB AND DNW
}

//===========================================
//  NC: high Vt NMOS design minima (optional)
//===========================================

GROUP GNC NC_?

NC_1 {
@ NC_1
@ NC width is 0.60
  X = INT NC < 0.31 ABUT<90  REGION
  X NOT INTERACT EXCLU
  A = INT NC < 0.60 ABUT<90 OPPOSITE REGION
  X1 = GT INSIDE EDGE NACT
  Y1 = EXPAND EDGE X1 OUTSIDE BY 0.27 EXTEND BY 0.18
  X2 = NACT INSIDE EDGE GT
  Y2 = EXPAND EDGE X2 OUTSIDE BY 0.18
  Z = (GATE OR Y1) OR Y2
  (A INTERACT Z) NOT INTERACT EXCLU
}
NC_2 {
@ NC_2
@ Space between NCs is 0.6
  X = EXT NC < 0.31 ABUT<90  REGION
  X NOT INTERACT EXCLU
  A = EXT NC < 0.60 ABUT<90 OPPOSITE REGION
  X1 = GT INSIDE EDGE NACT
  Y1 = EXPAND EDGE X1 OUTSIDE BY 0.27 EXTEND BY 0.18
  X2 = NACT INSIDE EDGE GT
  Y2 = EXPAND EDGE X2 OUTSIDE BY 0.18
  Z = (GATE OR Y1) OR Y2
  (A INTERACT Z) NOT INTERACT EXCLU
}
// NC_3  @ Distance between NC and NW 0

NC_4 {
@ NC_4
@ NC extension outside of N+AA along poly length is 0.18
X = NACT TOUCH EDGE GATE
ENC X NC < 0.18 ABUT<90 OPPOSITE
}
NC_5 {
@ NC_5
@ Space between NC and N+AA for nominal NMOS along poly length is 0.18
X = NACT TOUCH EDGE (AA INSIDE EDGE GT)
EXT X NC < 0.18 ABUT<90  REGION OPPOSITE
}
NC_6 {
@ NC_6
@ NC extension outside of poly gate is 0.27
X = GT INSIDE EDGE (NACT AND NC)
Y = EXPAND EDGE X OUTSIDE BY 0.27
Y NOT NC
}
NC_7 {
@ NC_7
@ Space between NC and poly gate for nominal device is 0.27
X = GT INSIDE EDGE (NACT NOT NC)
EXT NC X < 0.27 ABUT<90 REGION OPPOSITE
}
NC_8 {
@ NC_8
@ No NC and PSUB overlap is allowed
NC AND PSUB
}

//===========================================
//  PC: high Vt PMOS design minima (optional)
//===========================================

GROUP GPC PC_?

PC_1 {
@ PC_1
@ PC width is 0.6
  X = INT PC < 0.31 ABUT<90  REGION
  X NOT INTERACT EXCLU
  A = INT PC < 0.60 ABUT<90 OPPOSITE REGION
  X1 = GT INSIDE EDGE PACT
  Y1 = EXPAND EDGE X1 OUTSIDE BY 0.27 EXTEND BY 0.18
  X2 = PACT INSIDE EDGE GT
  Y2 = EXPAND EDGE X2 OUTSIDE BY 0.18
  Z = (GATE OR Y1) OR Y2
  (A INTERACT Z) NOT INTERACT EXCLU
}
PC_2 {
@ PC_2
@ Space between PCs is 0.6
  X = EXT PC < 0.31 ABUT<90  REGION
  X NOT INTERACT EXCLU
  A = EXT PC < 0.60 ABUT<90 OPPOSITE REGION
  X1 = GT INSIDE EDGE PACT
  Y1 = EXPAND EDGE X1 OUTSIDE BY 0.27 EXTEND BY 0.18
  X2 = PACT INSIDE EDGE GT
  Y2 = EXPAND EDGE X2 OUTSIDE BY 0.18
  Z = (GATE OR Y1) OR Y2
  (A INTERACT Z) NOT INTERACT EXCLU
}
// PC_3  @ Distance between PC and NW 0

PC_4 {
@ PC_4
@ PC extension outside of P+AA along poly length is 0.18
X = PACT TOUCH EDGE GATE
ENC X PC < 0.18 ABUT<90 OPPOSITE
}
PC_5 {
@ PC_5
@ Space between PC and P+AA for nominal PMOS along poly length is 0.18
X = PACT TOUCH EDGE (AA INSIDE EDGE GT)
EXT X PC < 0.18 ABUT<90  REGION OPPOSITE
}
PC_6 {
@ PC_6
@ PC extension outside of poly gate is 0.27
X = GT INSIDE EDGE (PACT AND PC)
Y = EXPAND EDGE X OUTSIDE BY 0.27
Y NOT PC
}
PC_7 {
@ PC_7
@ Space between PC and poly gate for nominal device is 0.27
X = GT INSIDE EDGE (PACT NOT PC)
EXT PC X < 0.27 ABUT<90 REGION OPPOSITE
}
PC_8 {
@ PC_8
@ No PC and PSUB overlap is allowed
PC AND PSUB
}

//===========================================
//  MVN: low Vt NMOS design minima (optional)
//===========================================

GROUP GMVN MVN_?

MVN_1 {
@ MVN_1
@ MVN width is 0.6
 INT MVN < 0.6 ABUT<90 SINGULAR REGION
}
MVN_2 {
@ MVN_2
@ Space between MVNs is 0.6
 EXT MVN < 0.6 ABUT<90 SINGULAR REGION
}
// MVN_3  @ Distance between MVN and NW region  0
MVN_4 {
@ MVN_4
@ MVN extension outside of N+AA along poly length is 0.18
X = ENC NACT MVN < 0.18  ABUT<90 SINGULAR REGION
X INTERACT GT
}
MVN_5 {
@ MVN_5
@ Space between MVN and N+AA for nominal NMOS along poly length is 0.18
X = NACT TOUCH EDGE (AA INSIDE EDGE GT)
EXT X MVN < 0.18 ABUT<90  REGION OPPOSITE
}
MVN_6 {
@ MVN_6
@ MVN extension outside of poly gate is 0.27
X = GT INSIDE EDGE (NACT AND MVN)
Y = EXPAND EDGE X OUTSIDE BY 0.27
Y NOT MVN
}
MVN_7 {
@ MVN_7
@ Space between MVN and poly gate for nominal device is 0.27
X = GT INSIDE EDGE (NACT NOT MVN)
EXT MVN X < 0.27 ABUT<90 REGION OPPOSITE
}
MVN_8 {
@ MVN_8
@ No MVN and PSUB overlap is allowed
MVN AND PSUB
}

//============================================
//  MVP:  low Vt PMOS design minima (optional)
//============================================

GROUP GMVP MVP_?

MVP_1 {
@ MVP_1
@ MVP width is 0.6
 INT MVP < 0.6 ABUT<90 SINGULAR REGION
}
MVP_2 {
@ MVP_2
@ Space between MVPs is 0.6
 EXT MVP < 0.6 ABUT<90 SINGULAR REGION
}
// MVP_3  @ NW extension outside of MVP 0
MVP_4 {
@ MVP_4
@ MVP extension outside of P+AA along poly length is 0.18
X = ENC PACT MVP< 0.18  ABUT<90 SINGULAR REGION
X INTERACT GT
}
MVP_5 {
@ MVP_5
@ Space between MVP and P+AA for nominal PMOS along poly length is 0.18
X = PACT TOUCH EDGE (AA INSIDE EDGE GT)
EXT X MVP < 0.18 ABUT<90  REGION OPPOSITE
}
MVP_6 {
@ MVP_6
@ MVP extension outside of poly gate is 0.27
X = GT INSIDE EDGE (PACT AND MVP)
Y = EXPAND EDGE X OUTSIDE BY 0.27
Y NOT MVP
}
MVP_7 {
@ MVP_7
@ Space between MVP and poly gate for nominal device is 0.27
X = GT INSIDE EDGE (PACT NOT MVP)
EXT MVP X < 0.27 ABUT<90 REGION OPPOSITE
}
MVP_8 {
@ MVP_8
@ No MVP and PSUB overlap is allowed
MVP AND PSUB
}

//============================
//   DG - Dual Gate DRC
//============================

GROUP GDG DG_?

DG_1 {
@ DG_1
@ DG width is 0.70
  X = INT DG < 0.70 ABUT<90 SINGULAR REGION
  X NOT INTERACT EXCLU
}
DG_2 {
@ DG_2
@ Space between two DGs is 0.70
  EXT DG < 0.70 ABUT<90 SINGULAR REGION
}
DG_3 {
@ DG_3
@ AA enclosure by DG is 0.25
X = AA INTERACT GT
  ENC X DG < 0.25 ABUT<90 SINGULAR REGION
}
#IFDEF  DG_4_process 13
DG_4 {
@ DG_4 for 0.13um
@ 2.5/3.3V transistor channel (overlap of poly and AA) enclosure by DG is 0.27
  ENC GATE DG < 0.27 ABUT<90 SINGULAR REGION
}
#ENDIF
#IFDEF  DG_4_process 11
DG_4 {
@ DG_4 for 0.11um
@ 2.5/3.3V transistor channel (overlap of poly and AA) enclosure by DG is 0.30
  ENC GATE DG < 0.30 ABUT<90 SINGULAR REGION
}
#ENDIF
DG_5 {
@ DG_5
@ Space between DG and AA is 0.25
  X = AA INTERACT (GT OR (RESNW OR RESAA))
  EXT DG X < 0.25 ABUT<90 SINGULAR REGION
}
DG_6 {
@ DG_6
@ Space between DG and 1.2V transistor channel (overlap of poly and AA) is 0.33
  X = GT AND LVAA
  EXT DG X < 0.33 ABUT<90 SINGULAR REGION INSIDE ALSO
}
/*
DG_7 {
@ DG cannot overlap or touch with 1.2v MOS AA (AA with poly).
 AA12 = AA NOT DG
 MOS12 = AA12 INTERACT GT
 DG COINCIDENT EDGE MOS12
}
*/
//==========================
// GT - Poly design minima
//==========================

GROUP GGT GT_?

GT_1a {
@ GT width for 1.2V NMOS/PMOS transistor is 0.13
  X = GT INSIDE EDGE LVAA
  INT X < 0.13 ABUT<90 REGION
}
GT_1c1 {
@ GT width for 2.5V MOS transistor is 0.28
  X = GT INSIDE EDGE (AA AND DG)
  INT X < 0.28 ABUT<90
}
GT_1c2 {
@ GT width for 3.3V NMOS transistor is 0.35
  X = GT INSIDE EDGE (NACT AND DG)
  INT X < 0.35 ABUT<90 REGION
}
GT_1c3 {
@ GT width for 3.3V PMOS transistor is 0.30
  X = GT INSIDE EDGE (PACT AND DG)
  INT X < 0.30 ABUT<90
}
GT_2 {
@ Interconnect width is 0.13um
  INT INTERGT < 0.13 ABUT<90 SINGULAR REGION
}
GT_3 {
@ Space between GTs is 0.18 
  EXT GT < 0.18 ABUT<90 REGION
}
GT_4 {
@ Space between AA and GT on field oxide is 0.07
  X = AA INSIDE EDGE GT
  Y = AA NOT COIN EDGE X
  EXT Y INTERGT < 0.07 ABUT<90 REGION
}
GT_5 {
@ Extension of AA outside of GT is 0.23
  ENC GT AA < 0.23 ABUT<90 REGION OPPOSITE
}
GT_6 {
@ Extension of GT outside of AA is 0.17
  X = AA INSIDE EDGE GT
  ENC X GT < 0.17 ABUT<90 REGION
}
GT_7 {
@ GT pattern density is 15%
  DENSITY (GTi OR GTDUMi) < 0.15 PRINT density_report_GT.log
}
GT_8 {
@ No bent GT on AA are allowed. All GT patterns on AA have to be orthogonal to AA edge
  X = GT INSIDE EDGE AA
  INT X < 0.01 ABUT == 135 REGION
  EXT X < 0.01 ABUT == 135 REGION
  INT X < 0.01 ABUT == 90 REGION
  EXT X < 0.01 ABUT == 90 REGION
  INT X < 0.01 ABUT == 45 REGION
  EXT X < 0.01 ABUT == 45 REGION
}
GT_9a {
@ NLL extension outside of poly resistor is 0.18
  X = GTR_N3T AND SN
  ENC X NLL < 0.18 ABUT<90 SINGULAR REGION
}
GT_9b {
@ NLH extension outside of poly resistor is 0.18
  X = GTR_N3T AND SN
  ENC X NLH < 0.18 ABUT<90 SINGULAR REGION
}
GT_9c {
@ SN extension outside of poly resistor is 0.18
  ENC GTR_N3T SN < 0.18 ABUT<90 SINGULAR REGION
}
GT_9d {
@ PLL extension outside of poly resistor is 0.18
  X = GTR_N3T AND SP
  ENC X PLL < 0.18 ABUT<90 SINGULAR REGION
}
GT_9e {
@ PLH extension outside of poly resistor is 0.18
  X = GTR_N3T AND SP
  ENC X PLH < 0.18 ABUT<90 SINGULAR REGION
}
GT_9f {
@ SP extension outside of poly resistor is 0.18
  ENC GTR_N3T SP < 0.18 ABUT<90 SINGULAR REGION
}

GT_10a {
@ Space between a NLL and a P- type poly resistor is 0.18
  X = GTR AND SP
  EXT NLL X < 0.18 ABUT<90 SINGULAR REGION
}
GT_10b {
@ Space between a NLH and a P- type poly resistor is 0.18
  X = GTR AND SP
  EXT NLH X < 0.18 ABUT<90 SINGULAR REGION
}
GT_10c {
@ Space between a SN and a P- type poly resistor is 0.18
  EXT SN GTR < 0.18 ABUT<90 SINGULAR REGION
}
GT_10d {
@ Space between a PLL and a N- type poly resistor is 0.18
  X = GTR AND SN
  EXT PLL X < 0.18 ABUT<90 SINGULAR REGION
}
GT_10e {
@ Space between a PLH and a N- type poly resistor is 0.18
  X = GTR AND SN
  EXT PLH X < 0.18 ABUT<90 SINGULAR REGION
}
GT_10f {
@ Space between a SP and a N- type poly resistor is 0.18
  EXT SP GTR < 0.18 ABUT<90 SINGULAR REGION
}

GT_11 {
@ GT used as MOS gate poly must be enclosed by SN or SP
  X = GT INTERACT HRP
  Z = (GATE NOT GTDUMi) NOT X
  (Z NOT SP) NOT SN
}
/* GT_12 is covered by SP_12.
GT_12 {
@ SN and SP overlap on gate is not allowed
  (GATE AND SN) AND SP
}
*/
GT_Note3 {
@ No poly dummy is allowed to straddle on a boundadry of NW and DNW.
 GTDUM INSIDE EDGE NW
 GTDUM INSIDE EDGE DNW
}

//=======================================
//  NLL - 1.2V NLDD implantation DRC
//=======================================

GROUP GNLL NLL_?

NLL_1 {
@ NLL width is 0.3
  INT NLL < 0.3 ABUT<90 SINGULAR REGION
}
NLL_2 {
@ Space between NLLs is 0.3
  EXT NLL < 0.3 ABUT<90 SINGULAR REGION
}
NLL_3 {
@ Space between NLL and P+ AA inside NW is 0.16
  X = (PACT NOT PTAP) NOT TOUCH EDGE NLL
  EXT (NLL AND NW) X < 0.16 ABUT<90 REGION
}
NLL_4 {
@ Space between NLL and P+ AA inside PW is 0.03
  X = (PACT AND PW) NOT TOUCH EDGE NACT
  EXT (NLL AND PW) X < 0.03 ABUT<90 REGION
}
NLL_4b {
@ Min. space between a NLL and a butted P+ AA is 0
  AND NLL PACT
}
NLL_5 {
@ Space between NLL and AA with NLH,PLL,or PLH implantation is 0.16
  EXT NLL (NLHAA NOT COIN EDGE NLLAA) < 0.16 ABUT<90 REGION
  EXT NLL (PLLAA NOT COIN EDGE NLLAA) < 0.16 ABUT<90 REGION
  EXT NLL (PLHAA NOT COIN EDGE NLLAA) < 0.16 ABUT<90 REGION
}
NLL_6 {
@ NLL extension outside of NMOS poly gate is 0.4
  X = GT INSIDE EDGE NACT
  ENC X NLL < 0.4 ABUT<90 OPPOSITE REGION
}
NLL_7 {
@ NLL extension outside of N+ AA is 0.16, if distance to related poly is >0.25
  X = NACT NOT COIN EDGE PACT
  ENC X NLL < 0.16 ABUT<90 REGION
}
NLL_8 {
@ NLL extension outside of N+ AA is 0.30, if distance to related poly is <0.25
  X = GT INSIDE EDGE NACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = NGATE COIN EDGE NACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = (A OR B) INTERACT NLL
  C NOT NLL
  C AND SP
}
NLL_9 {
@ NLL and AA overlap is 0.16
  INT NLL AA < 0.16 ABUT<90 SINGULAR REGION
}
NLL_10 {
@ NLL area is 0.23
  AREA NLL < 0.23
}


//=======================================
//  PLL - 1.2V PLDD implantation DRC
//=======================================

GROUP GPLL PLL_?

PLL_1 {
@ PLL width is 0.3
  INT PLL < 0.3 ABUT<90 SINGULAR REGION
}
PLL_2 {
@ Space between PLLs is 0.3
  EXT PLL < 0.3 ABUT<90 SINGULAR REGION
}
PLL_3 {
@ Space between PLL and N+ AA inside PW is 0.16
  X = (NACT AND PW) NOT TOUCH EDGE PLL
  EXT (PLL AND PW) X < 0.16 ABUT<90 REGION
}
PLL_4 {
@ Space between PLL and N+ AA inside NW is 0.03
  X = NTAP NOT TOUCH EDGE PACT
  EXT (PLL AND NW) X < 0.03 ABUT<90 REGION
}
PLL_4b {
@ Min. space between a PLL and a butted N+ AA is 0
  AND PLL NACT
}
PLL_5 {
@ Space between PLL and AA with NLH,NLL,or PLH implantation is 0.16
  EXT PLL (NLHAA NOT COIN EDGE PLLAA) < 0.16 ABUT<90 REGION
  EXT PLL (NLLAA NOT COIN EDGE PLLAA) < 0.16 ABUT<90 REGION
  EXT PLL (PLHAA NOT COIN EDGE PLLAA) < 0.16 ABUT<90 REGION
}
PLL_6 {
@ PLL extension outside of PMOS poly gate is 0.4
  X = GT INSIDE EDGE PACT
  ENC X PLL < 0.4 ABUT<90 OPPOSITE REGION
}
PLL_7 {
@ PLL extension outside of P+ AA is 0.16, if distance to related poly is >0.25
  X = PACT NOT COIN EDGE NACT
  ENC X PLL < 0.16 ABUT<90 REGION
}
PLL_8 {
@ PLL extension outside of P+ AA is 0.30, if distance to related poly is <0.25
  X = GT INSIDE EDGE PACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = PGATE COIN EDGE PACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = (A OR B) INTERACT PLL
  C NOT PLL
  C AND SN
}
PLL_9 {
@ PLL and AA overlap is 0.16
  INT PLL AA < 0.16 ABUT<90 SINGULAR REGION
}
PLL_10 {
@ PLL area is 0.23
  AREA PLL < 0.23
}


//=======================================
//  NLH - 2.5/3.3V NLDD implantation DRC
//=======================================

GROUP GNLH NLH_?

NLH_1 {
@ NLH width is 0.3
  INT NLH < 0.3 ABUT<90 SINGULAR REGION
}
NLH_2 {
@ Space between NLHs is 0.3
  EXT NLH < 0.3 ABUT<90 SINGULAR REGION
}
NLH_3 {
@ Space between NLH and P+ AA inside NW is 0.16
  X = (PACT NOT PTAP) NOT TOUCH EDGE NLH
  EXT (NLH AND NW) X < 0.16 ABUT<90 REGION
}
NLH_4 {
@ Space between NLH and P+ AA inside PW is 0.03
  X = (PACT AND PW) NOT TOUCH EDGE NACT
  EXT (NLH AND PW) X < 0.03 ABUT<90 REGION
}
NLH_4b {
@ Min. space between a NLH and a butted P+ AA is 0
  AND NLH PACT
}
NLH_5 {
@ Space between NLH and AA with NLL,PLL,or PLH implantation is 0.16
  EXT NLH (NLLAA NOT COIN EDGE NLHAA) < 0.16 ABUT<90 REGION
  EXT NLH (PLLAA NOT COIN EDGE NLHAA) < 0.16 ABUT<90 REGION
  EXT NLH (PLHAA NOT COIN EDGE NLHAA) < 0.16 ABUT<90 REGION
}
NLH_6 {
@ NLH extension outside of NMOS poly gate is 0.4
  X = GT INSIDE EDGE NACT
  ENC X NLH < 0.4 ABUT<90 OPPOSITE REGION
}
NLH_7 {
@ NLH extension outside of N+ AA is 0.16, if distance to related poly is >0.25
  X = NACT NOT COIN EDGE PACT
  ENC X NLH < 0.16 ABUT<90 REGION
}
NLH_8 {
@ NLH extension outside of N+ AA is 0.30, if distance to related poly is <0.25
  X = GT INSIDE EDGE NACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = NGATE COIN EDGE NACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = (A OR B) INTERACT NLH
  C NOT NLH
  C AND SP
}
NLH_9 {
@ NLH and AA overlap is 0.16
  INT NLH AA < 0.16 ABUT<90 SINGULAR REGION
}
NLH_10 {
@ NLH area is 0.23
  AREA NLH < 0.23
}

//=======================================
//  PLH - 2.5V PLDD implantation DRC
//=======================================

GROUP GPLH PLH_?

PLH_1 {
@ PLH width is 0.3
  INT PLH < 0.3 ABUT<90 SINGULAR REGION
}
PLH_2 {
@ Space between PLHs is 0.3
  EXT PLH < 0.3 ABUT<90 SINGULAR REGION
}
PLH_3 {
@ Space between PLH and N+ AA inside PW is 0.16
  X = (NACT AND PW) NOT TOUCH EDGE PLH
  EXT (PLH AND PW) X < 0.16 ABUT<90 REGION
}
PLH_4 {
@ Space between PLH and N+ AA inside NW is 0.03
  X = NTAP NOT TOUCH EDGE PACT
  EXT (PLH AND NW) X < 0.03 ABUT<90 REGION
}
PLH_4b {
@ Min. space between a PLH and a butted N+ AA is 0
  AND PLH NACT
}
PLH_5 {
@ Space between PLH and AA with PLL, NLL or NLH implantation is 0.16
  EXT PLH (NLHAA NOT COIN EDGE PLHAA) < 0.16 ABUT<90 REGION
  EXT PLH (NLLAA NOT COIN EDGE PLHAA) < 0.16 ABUT<90 REGION
  EXT PLH (PLLAA NOT COIN EDGE PLHAA) < 0.16 ABUT<90 REGION
}
PLH_6 {
@ PLH extension outside of PMOS poly gate is 0.4
  X = GT INSIDE EDGE PACT
  ENC X PLH < 0.4 ABUT<90 OPPOSITE REGION
}
PLH_7 {
@ PLH extension outside of P+ AA is 0.16, if distance to related poly is >0.25
  X = PACT NOT COIN EDGE NACT
  ENC X PLH < 0.16 ABUT<90 REGION
}
PLH_8 {
@ PLH extension outside of P+ AA is 0.30, if distance to related poly is <0.25
  X = GT INSIDE EDGE PACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = PGATE COIN EDGE PACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = (A OR B) INTERACT PLH
  C NOT PLH
  C AND SN
}
PLH_9 {
@ PLL and AA overlap is 0.16
  INT PLH AA < 0.16 ABUT<90 SINGULAR REGION
}
PLH_10 {
@ PLH area is 0.23
  AREA PLH < 0.23
}

//=================================
//  SN - N+ S/D Implantation DRC
//=================================

GROUP GSN SN_?

SN_1 {
@ SN width is 0.3
  INT SN < 0.3 ABUT<90 REGION
}
SN_2 {
@ Space between SNs is 0.3
  EXT SN < 0.3 OPPOSITE ABUT<90 REGION
}
SN_3 {
@ Space between SN and P+ AA inside NW is 0.16
  X = (PACT NOT PTAP) NOT TOUCH EDGE SN
  EXT (SN AND NW) X < 0.16 ABUT>0<90 REGION
}
SN_4 {
@ Space between SN and P+ AA inside PW is 0.03
  X = (PACT AND PW) NOT TOUCH EDGE NACT
  EXT (SN AND PW) X < 0.03 ABUT<90 REGION
}
SN_4b {
@ Min. space between a SN and a butted P+ AA is 0
  AND SN PACT
}
SN_5 {
@ SN extension outside of NMOS poly gate is 0.4
X = GT INSIDE EDGE NACT
ENC X SN < 0.4 ABUT<90 OPPOSITE  REGION
}
SN_6 {
@ Space between SN and poly gate for PMOS is 0.4
  X = GT INSIDE EDGE PACT
  EXT SN X < 0.4 ABUT<90 REGION OPPOSITE
}
SN_7 {
@ SN extension outside of N+ AA is 0.16, if distance to related poly is >0.25
  X = (((NACT NOT NTAP) NOT SNNR) NOT COIN EDGE PACT) NOT COIN EDGE SP
  ENC X SN < 0.16 ABUT<90 REGION
}
SN_8 {
@ SN extension outside of N+ AA is 0.30, if distance to related poly is <0.25
  X = GT INSIDE EDGE NACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = NGATE COIN EDGE NACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = A OR B
  (C INTERACT SN) NOT SN
  C AND SP
}
SN_9 {
@ N+AA enclosure by SN in NW is 0.03
  NTAP_SN_9 = NACT AND (NW OR NWH)
  X = ((NTAP_SN_9 NOT SNNR) NOT COIN EDGE PACT) NOT COIN EDGE SP
  ENC X SN < 0.03 ABUT<90 REGION
}
SN_10 {
@ SN and AA overlap is 0.16
  X = SN NOT COIN EDGE AA
  INT X AA < 0.16 ABUT<90  REGION
}
SN_11 {
@ SN area is 0.23
  AREA SN < 0.23
}


//=======================================
//  SP - P+ S/D implantation DRC
//=======================================

GROUP GSP SP_?

SP_1 {
@ SP width is 0.3
  INT SP < 0.3 ABUT<90 REGION
}
SP_2 {
@ Space between SNs is 0.3
  EXT SP < 0.3 OPPOSITE ABUT<90 REGION
}
SP_3 {
@ Space between SP and N+ AA inside PW is 0.16
  X = (NACT AND PW) NOT TOUCH EDGE SP
  EXT (SP AND PW) X < 0.16 ABUT<90 REGION
}
SP_4 {
@ Space between SP and N+ AA inside NW is 0.03
  X = NTAP NOT TOUCH EDGE PACT
  EXT (SP AND NW) X < 0.03 ABUT<90 REGION
}
SP_5 {
@ Space between SP and N-channel poly gate is 0.4
  X = GT INSIDE EDGE NACT
  EXT SP X < 0.4 ABUT<90 REGION OPPOSITE
}
SP_6 {
@ SP extension outside of PMOS poly gate is 0.4
X = GT INSIDE EDGE PACT
ENC X SP < 0.4 ABUT<90 OPPOSITE  REGION
}
SP_7 {
@ SP extension outside of P+ AA is 0.16, if distance to related poly is >0.25
//  X = ((PACT NOT PTAP) NOT COIN EDGE NACT) NOT COIN EDGE SN
//  ENC X SP < 0.16 ABUT<90 REGION
   X = NW NOT (NW INTERACT RESNW)
   Y = ((PACT AND X) NOT COIN EDGE NACT) NOT COIN EDGE SN
   ENC Y SP < 0.16 ABUT<90 REGION
}
SP_8 {
@ SP extension outside of P+ AA is 0.30, if distance to related poly is <0.25
  X = GT INSIDE EDGE PACT
  A = EXPAND EDGE X INSIDE BY 0.25 OUTSIDE BY 0.25 CORNER FILL
  Y = PGATE COIN EDGE PACT
  B = EXPAND EDGE Y OUTSIDE BY 0.30 EXTEND BY 0.25
  C = A OR B
  (C INTERACT SP) NOT SP
  C AND SN
}
SP_9 {
@ P+AA enclosure by SP in PW is 0.03
//  X = PTAP NOT COIN EDGE NACT
//  ENC X SP < 0.03 ABUT<90 REGION
  X = NW INTERACT RESNW 
  Y =((PACT NOT (PACT INTERACT X)) NOT NW) NOT COIN EDGE NACT
  ENC Y SP < 0.03 ABUT<90 REGION
}
SP_10 {
@ SP and AA overlap is 0.18
  X = SP NOT COIN EDGE AA
  INT X AA < 0.18 ABUT<90  REGION
}
SP_11 {
@ SP area is 0.23
  AREA SP < 0.23
}
SP_12 {
@ No SN and SP overlap is allowed
  AND SN SP
}
// SP can not be generated by the reverse tone of SN

//=================================
//  SAB - Salicide Block DRC
//=================================

GROUP GSAB SAB_?

SAB_1 {
@ SAB width is 0.42
  INT SAB < 0.42 ABUT<90 SINGULAR REGION
}
SAB_2 {
@ Space between SABs is 0.42
  EXT SAB < 0.42 ABUT<90 SINGULAR REGION
}
SAB_3 {
@ Extension of related AA outside of SAB is 0.2
  ENC SAB AA < 0.20 ABUT<90 SINGULAR REGION
}
SAB_4 {
@ Space between SAB and AA is 0.2
  EXT SAB AA < 0.2 ABUT<90 SINGULAR REGION
}
SAB_5 {
@ Space between SAB and GT on AA is 0.36
  EXT SAB GATE < 0.36 ABUT<90 SINGULAR REGION
}
SAB_6 {
@ Extension of SAB outside of poly on field oxide is 0.2
  X = SAB NOT INTERACT GATE
  ENC INTERGT X < 0.2 ABUT<90 REGION
}
SAB_7 {
@ Space between SAB and CT is 0.2
  EXT SAB CT < 0.2 ABUT<90 SINGULAR REGION
}
SAB_8 {
@ Extension of SAB outside of AA is 0.2
  ENC AA SAB < 0.2 ABUT<90 SINGULAR REGION
}
SAB_9 {
@ Overlap of SN/SP and SAB is 0.28
  INT SN SAB < 0.28 ABUT<90 SINGULAR REGION
  INT SP SAB < 0.28 ABUT<90 SINGULAR REGION
}
SAB_10 {
@ SAB area is 0.5
  AREA SAB < 0.50
}
SAB_11 {
@ Space between SAB and poly on field oxide is 0.28
  X = SAB NOT INTERACT GATE
  EXT X INTERGT < 0.28 ABUT<90 SINGULAR REGION
}

//========================
//  CT - Contact DRC
//========================

GROUP GCT CT_?

CT_1 {
@ Fixed contact size (edge of a square via) is 0.16
  NOT RECTANGLE CT == 0.16 BY == 0.16
}
CT_2 {
@ Space between contacts is 0.18
  EXT CT < 0.18 ABUT<90 SINGULAR REGION
}
CT_3 {
@ Space between AA and contact on poly is 0.12
  EXT CT AA < 0.12 ABUT<90 SINGULAR REGION
}
CT_4a {
@ Space between poly and contact on AA for 1.2 and 2.5V is 0.11
  EXT GATE ACT_CT < 0.11 ABUT<90 SINGULAR REGION
}
CT_4b {
@ Space between poly and contact on AA for 3.3V is 0.13
  EXT (GATE AND DG) ACT_CT < 0.13 ABUT<90 SINGULAR REGION
  EXT GATE (ACT_CT AND DG) < 0.13 ABUT<90 SINGULAR REGION
}
CT_5 {
@ CT enclosure by AA for CT landed on device AA is 0.06
  BJTID = (DNW INTERACT DMPNP) OR (SIZE (NW INTERACT (DMPNP NOT INTERACT DNW)) BY 1.5)
  DIOID = SIZE DSTR BY 0.25
  X = AA INTERACT ((GT OR (RESNW OR RESAA)) OR (BJTID OR DIOID))
  Y = CT INTERACT X               
  ENC Y X< 0.06 ABUT<90 SINGULAR REGION
}
CT_5a {
@ CT enclosure by AA for CT landed on pickup AA is 0.05
  BJTID = (DNW INTERACT DMPNP) OR (SIZE (NW INTERACT (DMPNP NOT INTERACT DNW)) BY 1.5)
  DIOID = SIZE DSTR BY 0.25
  X = AA INTERACT ((GT OR (RESNW OR RESAA)) OR (BJTID OR DIOID))
  Y = CT INTERACT (AA NOT X)          
  ENC Y (AA NOT X) < 0.05 ABUT<90 SINGULAR REGION
}
CT_5b {
@ CONT on STI is not allowed
  (((CT NOT OPCBA) NOT OPCBP) NOT AA) NOT GT
}
CT_6 {
@ CT enclosure by poly for CT landed on poly is 0.06
  X = CT INTERACT GT
  ENC X GT < 0.06 ABUT<90 SINGULAR OUTSIDE ALSO
}
CT_7_8 {
@ CT enclosure by M1 is 0.00
@ M1 line end extension outside of CT is 0.05
  X = RECTANGLE ENC (CT NOT TOUCH M1) M1 ABUT<90 OUTSIDE ALSO
  GOOD 0 0.05 OPPOSITE 0 0.05 OPPOSITE

  A = ENC [X] M1 < 0.05 OPPOSITE
  INT A < 0.01 ABUT==90 INTERSECTING ONLY      //; For CT not touching M1

  B = CT COIN EDGE M1
  INT B < 0.01 ABUT==90         //; For CT touch M1 with >= 2 butting edges

  Y = ENC CT M1 < 0.05 OPPOSITE REGION
  C1 = CT COIN EDGE M1
  C2 = CT COIN EDGE Y
  INT C1 C2 < 0.01 ABUT==90     //; For CT touch M1 with only 1 edge

  CT NOT M1
}
CT_9 {
@ CT is not allowed to land on gate
  AND GATE CT
}
CT_10 {
@ CT to land on salicided surface 
  AND SAB CT
}

/*=====================
//  Fat Metal find 
//=====================


SLWIDE_M1 = M1 INTERACT (SIZE M1 BY -7)   // wide metal
SLWDM1_45 = ANGLE SLWIDE_M1 >0<90
SLWDM1_90 = NOT ANGLE SLWIDE_M1 >0<90
SLWDM1_EXP_45 = EXPAND EDGE SLWDM1_45 INSIDE BY 7*1.414213562
SLWDM1_EXP_90 = EXPAND EDGE SLWDM1_90 INSIDE BY 7 EXTEND BY 7
SLTHIN_M1_TMP = SLWIDE_M1 NOT (SLWDM1_EXP_45 OR SLWDM1_EXP_90)
SLTHIN_M1 = SNAP SLTHIN_M1_TMP 1            // end of shrink
// begin grow
SLTNM1_45 = ANGLE SLTHIN_M1 >0<90
SLTNM1_90 = NOT ANGLE SLTHIN_M1 >0<90
SLTNM1_EXP_45_TMP = EXPAND EDGE SLTNM1_45 OUTSIDE BY 7*1.414213562
SLTNM1_EXP_45 = SNAP SLTNM1_EXP_45_TMP 1
SLTNM1_EXP_90 = EXPAND EDGE SLTNM1_90 OUTSIDE BY 7 EXTEND BY 7
SLTNM1_EXP = SLTNM1_EXP_45 OR SLTNM1_EXP_90
SLWDM1_PRE = SLTNM1_EXP OR SLTHIN_M1
SLWDM1_PRE_FILL = EXT SLWDM1_PRE < 7 ABUT>0<90 INTERSECTING ONLY REGION
M1OVER14 = SLWDM1_PRE OR SLWDM1_PRE_FILL    // end of grow

SLWIDE_M2 = M2 INTERACT (SIZE M2 BY -7.5)   // wide metal
SLWDM2_45 = ANGLE SLWIDE_M2 >0<90
SLWDM2_90 = NOT ANGLE SLWIDE_M2 >0<90
SLWDM2_EXP_45 = EXPAND EDGE SLWDM2_45 INSIDE BY 7.5*1.414213562
SLWDM2_EXP_90 = EXPAND EDGE SLWDM2_90 INSIDE BY 7.5 EXTEND BY 7.5
SLTHIN_M2_TMP = SLWIDE_M2 NOT (SLWDM2_EXP_45 OR SLWDM2_EXP_90)
SLTHIN_M2 = SNAP SLTHIN_M2_TMP 1            // end of shrink
// begin grow
SLTNM2_45 = ANGLE SLTHIN_M2 >0<90
SLTNM2_90 = NOT ANGLE SLTHIN_M2 >0<90
SLTNM2_EXP_45_TMP = EXPAND EDGE SLTNM2_45 OUTSIDE BY 7.5*1.414213562
SLTNM2_EXP_45 = SNAP SLTNM2_EXP_45_TMP 1
SLTNM2_EXP_90 = EXPAND EDGE SLTNM2_90 OUTSIDE BY 7.5 EXTEND BY 7.5
SLTNM2_EXP = SLTNM2_EXP_45 OR SLTNM2_EXP_90
SLWDM2_PRE = SLTNM2_EXP OR SLTHIN_M2
SLWDM2_PRE_FILL = EXT SLWDM2_PRE < 7.5 ABUT>0<90 INTERSECTING ONLY REGION
M2OVER15 = SLWDM2_PRE OR SLWDM2_PRE_FILL    // end of grow

SLWIDE_M3 = M3 INTERACT (SIZE M3 BY -7.5)   // wide metal
SLWDM3_45 = ANGLE SLWIDE_M3 >0<90
SLWDM3_90 = NOT ANGLE SLWIDE_M3 >0<90
SLWDM3_EXP_45 = EXPAND EDGE SLWDM3_45 INSIDE BY 7.5*1.414213562
SLWDM3_EXP_90 = EXPAND EDGE SLWDM3_90 INSIDE BY 7.5 EXTEND BY 7.5
SLTHIN_M3_TMP = SLWIDE_M3 NOT (SLWDM3_EXP_45 OR SLWDM3_EXP_90)
SLTHIN_M3 = SNAP SLTHIN_M3_TMP 1            // end of shrink
// begin grow
SLTNM3_45 = ANGLE SLTHIN_M3 >0<90
SLTNM3_90 = NOT ANGLE SLTHIN_M3 >0<90
SLTNM3_EXP_45_TMP = EXPAND EDGE SLTNM3_45 OUTSIDE BY 7.5*1.414213562
SLTNM3_EXP_45 = SNAP SLTNM3_EXP_45_TMP 1
SLTNM3_EXP_90 = EXPAND EDGE SLTNM3_90 OUTSIDE BY 7.5 EXTEND BY 7.5
SLTNM3_EXP = SLTNM3_EXP_45 OR SLTNM3_EXP_90
SLWDM3_PRE = SLTNM3_EXP OR SLTHIN_M3
SLWDM3_PRE_FILL = EXT SLWDM3_PRE < 7.5 ABUT>0<90 INTERSECTING ONLY REGION
M3OVER15 = SLWDM3_PRE OR SLWDM3_PRE_FILL    // end of grow

SLWIDE_M4 = M4 INTERACT (SIZE M4 BY -7.5)   // wide metal
SLWDM4_45 = ANGLE SLWIDE_M4 >0<90
SLWDM4_90 = NOT ANGLE SLWIDE_M4 >0<90
SLWDM4_EXP_45 = EXPAND EDGE SLWDM4_45 INSIDE BY 7.5*1.414213562
SLWDM4_EXP_90 = EXPAND EDGE SLWDM4_90 INSIDE BY 7.5 EXTEND BY 7.5
SLTHIN_M4_TMP = SLWIDE_M4 NOT (SLWDM4_EXP_45 OR SLWDM4_EXP_90)
SLTHIN_M4 = SNAP SLTHIN_M4_TMP 1            // end of shrink
// begin grow
SLTNM4_45 = ANGLE SLTHIN_M4 >0<90
SLTNM4_90 = NOT ANGLE SLTHIN_M4 >0<90
SLTNM4_EXP_45_TMP = EXPAND EDGE SLTNM4_45 OUTSIDE BY 7.5*1.414213562
SLTNM4_EXP_45 = SNAP SLTNM4_EXP_45_TMP 1
SLTNM4_EXP_90 = EXPAND EDGE SLTNM4_90 OUTSIDE BY 7.5 EXTEND BY 7.5
SLTNM4_EXP = SLTNM4_EXP_45 OR SLTNM4_EXP_90
SLWDM4_PRE = SLTNM4_EXP OR SLTHIN_M4
SLWDM4_PRE_FILL = EXT SLWDM4_PRE < 7.5 ABUT>0<90 INTERSECTING ONLY REGION
M4OVER15 = SLWDM4_PRE OR SLWDM4_PRE_FILL    // end of grow


SLWIDE_M5 = M5 INTERACT (SIZE M5 BY -7.5)   // wide metal
SLWDM5_45 = ANGLE SLWIDE_M5 >0<90
SLWDM5_90 = NOT ANGLE SLWIDE_M5 >0<90
SLWDM5_EXP_45 = EXPAND EDGE SLWDM5_45 INSIDE BY 7.5*1.414213562
SLWDM5_EXP_90 = EXPAND EDGE SLWDM5_90 INSIDE BY 7.5 EXTEND BY 7.5
SLTHIN_M5_TMP = SLWIDE_M5 NOT (SLWDM5_EXP_45 OR SLWDM5_EXP_90)
SLTHIN_M5 = SNAP SLTHIN_M5_TMP 1            // end of shrink
// begin grow
SLTNM5_45 = ANGLE SLTHIN_M5 >0<90
SLTNM5_90 = NOT ANGLE SLTHIN_M5 >0<90
SLTNM5_EXP_45_TMP = EXPAND EDGE SLTNM5_45 OUTSIDE BY 7.5*1.414213562
SLTNM5_EXP_45 = SNAP SLTNM5_EXP_45_TMP 1
SLTNM5_EXP_90 = EXPAND EDGE SLTNM5_90 OUTSIDE BY 7.5 EXTEND BY 7.5
SLTNM5_EXP = SLTNM5_EXP_45 OR SLTNM5_EXP_90
SLWDM5_PRE = SLTNM5_EXP OR SLTHIN_M5
SLWDM5_PRE_FILL = EXT SLWDM5_PRE < 7.5 ABUT>0<90 INTERSECTING ONLY REGION
M5OVER15 = SLWDM5_PRE OR SLWDM5_PRE_FILL    // end of grow


SLWIDE_M6 = M6 INTERACT (SIZE M6 BY -7.5)   // wide metal
SLWDM6_45 = ANGLE SLWIDE_M6 >0<90
SLWDM6_90 = NOT ANGLE SLWIDE_M6 >0<90
SLWDM6_EXP_45 = EXPAND EDGE SLWDM6_45 INSIDE BY 7.5*1.414213562
SLWDM6_EXP_90 = EXPAND EDGE SLWDM6_90 INSIDE BY 7.5 EXTEND BY 7.5
SLTHIN_M6_TMP = SLWIDE_M6 NOT (SLWDM6_EXP_45 OR SLWDM6_EXP_90)
SLTHIN_M6 = SNAP SLTHIN_M6_TMP 1            // end of shrink
// begin grow
SLTNM6_45 = ANGLE SLTHIN_M6 >0<90
SLTNM6_90 = NOT ANGLE SLTHIN_M6 >0<90
SLTNM6_EXP_45_TMP = EXPAND EDGE SLTNM6_45 OUTSIDE BY 7.5*1.414213562
SLTNM6_EXP_45 = SNAP SLTNM6_EXP_45_TMP 1
SLTNM6_EXP_90 = EXPAND EDGE SLTNM6_90 OUTSIDE BY 7.5 EXTEND BY 7.5
SLTNM6_EXP = SLTNM6_EXP_45 OR SLTNM6_EXP_90
SLWDM6_PRE = SLTNM6_EXP OR SLTHIN_M6
SLWDM6_PRE_FILL = EXT SLWDM6_PRE < 7.5 ABUT>0<90 INTERSECTING ONLY REGION
M6OVER15 = SLWDM6_PRE OR SLWDM6_PRE_FILL    // end of grow


SLWIDE_TM1 = TM1 INTERACT (SIZE TM1 BY -15)   // wide metal
SLWDTM1_45 = ANGLE SLWIDE_TM1 >0<90
SLWDTM1_90 = NOT ANGLE SLWIDE_TM1 >0<90
SLWDTM1_EXP_45 = EXPAND EDGE SLWDTM1_45 INSIDE BY 15*1.414213562
SLWDTM1_EXP_90 = EXPAND EDGE SLWDTM1_90 INSIDE BY 15 EXTEND BY 15
SLTHIN_TM1_TMP = SLWIDE_TM1 NOT (SLWDTM1_EXP_45 OR SLWDTM1_EXP_90)
SLTHIN_TM1 = SNAP SLTHIN_TM1_TMP 1            // end of shrink
// begin grow
SLTNTM1_45 = ANGLE SLTHIN_TM1 >0<90
SLTNTM1_90 = NOT ANGLE SLTHIN_TM1 >0<90
SLTNTM1_EXP_45_TMP = EXPAND EDGE SLTNTM1_45 OUTSIDE BY 15*1.414213562
SLTNTM1_EXP_45 = SNAP SLTNTM1_EXP_45_TMP 1
SLTNTM1_EXP_90 = EXPAND EDGE SLTNTM1_90 OUTSIDE BY 15 EXTEND BY 15
SLTNTM1_EXP = SLTNTM1_EXP_45 OR SLTNTM1_EXP_90
SLWDTM1_PRE = SLTNTM1_EXP OR SLTHIN_TM1
SLWDTM1_PRE_FILL = EXT SLWDTM1_PRE < 15 ABUT>0<90 INTERSECTING ONLY REGION
TM1OVER30 = SLWDTM1_PRE OR SLWDTM1_PRE_FILL    // end of grow

SLWIDE_TM2 = TM2 INTERACT (SIZE TM2 BY -15)   // wide metal
SLWDTM2_45 = ANGLE SLWIDE_TM2 >0<90
SLWDTM2_90 = NOT ANGLE SLWIDE_TM2 >0<90
SLWDTM2_EXP_45 = EXPAND EDGE SLWDTM2_45 INSIDE BY 15*1.414213562
SLWDTM2_EXP_90 = EXPAND EDGE SLWDTM2_90 INSIDE BY 15 EXTEND BY 15
SLTHIN_TM2_TMP = SLWIDE_TM2 NOT (SLWDTM2_EXP_45 OR SLWDTM2_EXP_90)
SLTHIN_TM2 = SNAP SLTHIN_TM2_TMP 1            // end of shrink
// begin grow
SLTNTM2_45 = ANGLE SLTHIN_TM2 >0<90
SLTNTM2_90 = NOT ANGLE SLTHIN_TM2 >0<90
SLTNTM2_EXP_45_TMP = EXPAND EDGE SLTNTM2_45 OUTSIDE BY 15*1.414213562
SLTNTM2_EXP_45 = SNAP SLTNTM2_EXP_45_TMP 1
SLTNTM2_EXP_90 = EXPAND EDGE SLTNTM2_90 OUTSIDE BY 15 EXTEND BY 15
SLTNTM2_EXP = SLTNTM2_EXP_45 OR SLTNTM2_EXP_90
SLWDTM2_PRE = SLTNTM2_EXP OR SLTHIN_TM2
SLWDTM2_PRE_FILL = EXT SLWDTM2_PRE < 15 ABUT>0<90 INTERSECTING ONLY REGION
TM2OVER30 = SLWDTM2_PRE OR SLWDTM2_PRE_FILL    // end of grow
*/

M1SLOT1 = HOLES M1 INNER EMPTY
M2SLOT1 = HOLES M2 INNER EMPTY
M3SLOT1 = HOLES M3 INNER EMPTY
M4SLOT1 = HOLES M4 INNER EMPTY
M5SLOT1 = HOLES M5 INNER EMPTY
M6SLOT1 = HOLES M6 INNER EMPTY
TM1SLOT1 = HOLES TM1 INNER EMPTY
TM2SLOT1 = HOLES TM2 INNER EMPTY

M1_SL  = (M1 INTERACT M1SLOT1) OR M1SLOT1
M2_SL  = (M2 INTERACT M2SLOT1) OR M2SLOT1
M3_SL  = (M3 INTERACT M3SLOT1) OR M3SLOT1
M4_SL  = (M4 INTERACT M4SLOT1) OR M4SLOT1
M5_SL  = (M5 INTERACT M5SLOT1) OR M5SLOT1
M6_SL  = (M6 INTERACT M6SLOT1) OR M6SLOT1
TM1_SL  = (TM1 INTERACT TM1SLOT1) OR TM1SLOT1
TM2_SL  = (TM2 INTERACT TM2SLOT1) OR TM2SLOT1


//=====================
//  M1 - Metal 1 DRC
//=====================


GROUP GM1 M1_?
/*
//--* Find Wide Metal *--//

WIDE_M1 = M1 INTERACT (SIZE M1 BY -5)   // wide metal

// begin shrink
WDM1_45 = ANGLE WIDE_M1 >0<90

WDM1_90 = NOT ANGLE WIDE_M1 >0<90

WDM1_EXP_45 = EXPAND EDGE WDM1_45 INSIDE BY 5*1.414213562

WDM1_EXP_90 = EXPAND EDGE WDM1_90 INSIDE BY 5 EXTEND BY 5

THIN_M1_TMP = WIDE_M1 NOT (WDM1_EXP_45 OR WDM1_EXP_90)

THIN_M1 = SNAP THIN_M1_TMP 1            // end of shrink

// begin grow
TNM1_45 = ANGLE THIN_M1 >0<90

TNM1_90 = NOT ANGLE THIN_M1 >0<90

TNM1_EXP_45_TMP = EXPAND EDGE TNM1_45 OUTSIDE BY 5*1.414213562

TNM1_EXP_45 = SNAP TNM1_EXP_45_TMP 1

TNM1_EXP_90 = EXPAND EDGE TNM1_90 OUTSIDE BY 5 EXTEND BY 5

TNM1_EXP = TNM1_EXP_45 OR TNM1_EXP_90
WDM1_PRE = TNM1_EXP OR THIN_M1

WDM1_PRE_FILL = EXT WDM1_PRE < 5 ABUT>0<90 INTERSECTING ONLY REGION

M1OVER10 = WDM1_PRE OR WDM1_PRE_FILL    // end of grow

//--* End of Finding Wide Metal *--//
*/

M1_1 {
@ M1 width is 0.16
  X =INT M1 < 0.16 ABUT<90 SINGULAR REGION
  X NOT INTERACT EXCLU
}
M1_2 {
@ Space between M1s is 0.17
  EXT M1 < 0.17 ABUT<90 SINGULAR REGION
}
M1_3a {
@ Space between M1s with one or both M1 width greater than 0.4um is 0.2
  X = M1 WITH WIDTH > 0.4
  EXT M1 X < 0.2 ABUT<90 REGION
}
M1_3b {
@ Space between M1s with one or both M1 width greater than 2um is 0.4
  X = M1 WITH WIDTH > 2
  EXT M1 X < 0.4 ABUT<90 REGION
}
M1_3c {
@ Space between M1s with one or both M1 width greater than 10um is 0.5
  X = M1 WITH WIDTH > 10
  EXT M1 X < 0.5 ABUT<89.9 REGION
}
M1_4 {
@ M1 area is 0.08
  X = AREA M1 < 0.08
  X NOT INTERACT EXCLU
}
M1_5 {
@ Dielectric area enclosed by M1(um2) is 0.17
  X = HOLES M1 INNER EMPTY
  AREA X < 0.17 
}
M1_6a {
@ Dummy pattern is required in case M1 density is less than 16%
  DENSITY M1i < 0.16 WINDOW 200 STEP 100 PRINT density_report_M1a.log  //; Density check in each 200x200 square
}
M1_6b {
@ Max. Metal 1 GDS pattern density is 82%
  DENSITY M1i > 0.82 WINDOW 200 STEP 100 PRINT density_report_M1b.log  //; Density check in each 200x200 square
}
M1_7 {
@ Space between metal line and 45 degree bent metal line that are longer than 0.5um is 0.2
  X = ANGLE M1 == 45
  Y = LENGTH X >= 0.5
  EXT M1 Y < 0.2 ABUT<90 REGION
}
M1_8 {
@ Max line width allowed. Metal slot rule will apply for a metal with line width greater than this value(14)
  X = M1 WITH WIDTH > 14
  Y = X INTERACT MIM
  (X NOT Y) NOT INTERACT PA
}

//========================
//  V1 - Via 1 DRC
//========================

GROUP GV1 V1_?

V1_1 {
@ Fixed V1 size is 0.19
  V1x = V1 NOT V1FUSE_RING
  X = NOT RECTANGLE V1x == 0.19 BY == 0.19
  X NOT TOUCH EXCLU
}
V1_2a {
@ Space between V1s is 0.21um
  EXT V1 < 0.21 ABUT<90 SINGULAR REGION
}
V1_2b {
@ Space between V1s within array greater or equal to 4*4 is 0.25
@ Two via regions whose space is <= 0.27um are considered to be in the same array
  X = SIZE V1 BY 0.135 OVERUNDER
  Y = SIZE X BY 0.65 UNDEROVER
  Z = V1 INSIDE Y
  EXT Z < 0.25 ABUT<90 SINGULAR REGION
}
V1_4_5_8 {
@ V1 enclosure by M1 is 0.005
@ Extension of M1 line end outside of V1 is 0.05
@ For V1 enclosed at the 90 degree corner by M1, M1 extension at least along one direction outside V1 is 0.05
  X = RECTANGLE ENC (V1 NOT INTERACT EXCLU) (M1 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.05 OPPOSITE 0.005 OPPOSITE 0.05 OPPOSITE

  ENC X M1 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M1 < 0.05 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V1 NOT M1
}
V1_6_7_9 {
@ V1 enclosure by M2 is 0.005
@ Extension of M2 line end outside of V1 is 0.03
@ For V1 enclosed at the 90 degree corner by M2, M2 extension at least along one direction outside V1 is 0.05
  X = RECTANGLE ENC (V1 NOT INTERACT EXCLU) (M2 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.03 OPPOSITE 0.005 OPPOSITE 0.03 OPPOSITE

  ENC X M2 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M2 < 0.03 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V1 NOT M2
}

V1_10 {
@ V1 must be enclosed by both M1 and M2 
@ When one or both M1 and M2 width > 0.9um and intersection area is large enough to enclose a rectangle of size >=0.2um*0.7um, there should be at least two V1s in this M1and M2 intersection area
   M1M2 = M1 AND M2
   M1M2_enc = ENCLOSE RECTANGLE M1M2 0.2 0.7
   X1_M1 = M1 WITH WIDTH > 0.9
   X2_M1 = (M1M2_enc INTERACT X1_M1) INTERACT V1
   X2_M1 INTERACT V1 <2
   X1_M2 = M2 WITH WIDTH > 0.9
   X2_M2 = (M1M2_enc INTERACT X1_M2) INTERACT V1
   X2_M2 INTERACT V1 <2
}

//========================
//  M2 - Metal 2 DRC 
//========================


GROUP GM2 M2_?

/*
//--* Find Wide Metal *--//

WIDE_M2        = M2 INTERACT (SIZE M2 BY -5)   // wide metal
// begin shrink
WDM2_45        = ANGLE WIDE_M2 >0<90
WDM2_90        = NOT ANGLE WIDE_M2 >0<90

WDM2_EXP_45    = EXPAND EDGE WDM2_45 INSIDE BY 5*1.414213562
WDM2_EXP_90    = EXPAND EDGE WDM2_90 INSIDE BY 5 EXTEND BY 5

THIN_M2_TMP    = WIDE_M2 NOT (WDM2_EXP_45 OR WDM2_EXP_90)
THIN_M2        = SNAP THIN_M2_TMP 1            // end of shrink

// begin grow
TNM2_45        = ANGLE THIN_M2 >0<90
TNM2_90        = NOT ANGLE THIN_M2 >0<90

TNM2_EXP_45_TMP = EXPAND EDGE TNM2_45 OUTSIDE BY 5*1.414213562
TNM2_EXP_45    = SNAP TNM2_EXP_45_TMP 1
TNM2_EXP_90    = EXPAND EDGE TNM2_90 OUTSIDE BY 5 EXTEND BY 5

TNM2_EXP       = TNM2_EXP_45 OR TNM2_EXP_90

WDM2_PRE       = TNM2_EXP OR THIN_M2
WDM2_PRE_FILL  = EXT WDM2_PRE < 5 ABUT>0<90 INTERSECTING ONLY REGION

M2OVER10       = WDM2_PRE OR WDM2_PRE_FILL    // end of grow


//--* End of Finding Wide Metal *--//
*/

M2_1 {
@ M2 width is 0.2
X =  INT M2 < 0.20 ABUT<90 SINGULAR REGION
X NOT INTERACT EXCLU
}
M2_2 {
@ Space between M2s is 0.2
  EXT M2 < 0.20 ABUT<90 SINGULAR REGION
}
M2_3a {
@ Space between M2s with one or both M2 width greater than 0.4um is 0.25
  X = M2 WITH WIDTH > 0.4
  EXT M2 X < 0.25 ABUT<90 REGION
}
M2_3b {
@ Space between M2s with one or both M2 width greater than 2um is 0.4
  X = M2 WITH WIDTH > 2
  EXT M2 X < 0.4 ABUT<90 REGION
}
M2_3c {
@ Space between M2s with one or both M2 width greater than 10um is 0.5
  X = M2 WITH WIDTH > 10
  EXT M2 X < 0.5 ABUT<89.9 REGION
}
M2_4 {
@ M2 area (in um2) is 0.12
  X = AREA M2 < 0.12
  X NOT INTERACT EXCLU
}
M2_5 {
@ Dielectric area enclosed by M2(um2) is 0.26
  X = HOLES M2 INNER EMPTY
  AREA X < 0.26 
}
M2_6a {
@ Dummy pattern is required in case M2 density is less than 16%
  DENSITY M2i < 0.16 WINDOW 200 STEP 100 PRINT density_report_M2a.log  //; Density check in each 200x200 square
}
M2_6b {
@ Max. Metal2  GDS pattern density is 82%
  DENSITY M2i > 0.82 WINDOW 200 STEP 100 PRINT density_report_M2b.log  //; Density check in each 200x200 square
}
M2_7 {
@ Space between metal line and 45 degree bent metal line that are longer than 0.5um is 0.22
  X = ANGLE M2 == 45
  Y = LENGTH X >= 0.5
  EXT M2 Y < 0.22 ABUT<90 REGION
}
M2_8 { 
@ Max. M2 width is 15. Metal wider than this size should apply slot .
  X = M2 WITH WIDTH > 15
  Y = X INTERACT MIM
  (X NOT Y) NOT INTERACT PA
} 

//========================
//  M3 - Metal 3 DRC 
//========================


GROUP GM3 M3_?
/*
//--* Find Wide Metal *--//

WIDE_M3        = M3 INTERACT (SIZE M3 BY -5)   // wide metal

// begin shrink
WDM3_45        = ANGLE WIDE_M3 >0<90
WDM3_90        = NOT ANGLE WIDE_M3 >0<90

WDM3_EXP_45    = EXPAND EDGE WDM3_45 INSIDE BY 5*1.414213562
WDM3_EXP_90    = EXPAND EDGE WDM3_90 INSIDE BY 5 EXTEND BY 5

THIN_M3_TMP    = WIDE_M3 NOT (WDM3_EXP_45 OR WDM3_EXP_90)
THIN_M3        = SNAP THIN_M3_TMP 1            // end of shrink

// begin grow
TNM3_45        = ANGLE THIN_M3 >0<90
TNM3_90        = NOT ANGLE THIN_M3 >0<90

TNM3_EXP_45_TMP = EXPAND EDGE TNM3_45 OUTSIDE BY 5*1.414213562
TNM3_EXP_45    = SNAP TNM3_EXP_45_TMP 1
TNM3_EXP_90    = EXPAND EDGE TNM3_90 OUTSIDE BY 5 EXTEND BY 5

TNM3_EXP       = TNM3_EXP_45 OR TNM3_EXP_90

WDM3_PRE       = TNM3_EXP OR THIN_M3
WDM3_PRE_FILL  = EXT WDM3_PRE < 5 ABUT>0<90 INTERSECTING ONLY REGION

M3OVER10       = WDM3_PRE OR WDM3_PRE_FILL    // end of grow


//--* End of Finding Wide Metal *--//
*/

M3_1 {
@ M3 width is 0.2
  X=  INT M3 < 0.20 ABUT<90 SINGULAR REGION
  X NOT INTERACT EXCLU
}
M3_2 {
@ Space between M3s is 0.2
  EXT M3 < 0.20 ABUT<90 SINGULAR REGION
}
M3_3a {
@ Space between M3s with one or both M3 width greater than 0.4um is 0.25
  X = M3 WITH WIDTH > 0.4
  EXT M3 X < 0.25 ABUT<90 REGION
}
M3_3b {
@ Space between M3s with one or both M3 width greater than 2um is 0.4
  X = M3 WITH WIDTH > 2
  EXT M3 X < 0.4 ABUT<90 REGION
}
M3_3c {
@ Space between M3s with one or both M3 width greater than 10um is 0.5
  X = M3 WITH WIDTH > 10
  EXT M3 X < 0.5 ABUT<89.9 REGION
}
M3_4 {
@ M3 area (in um2) is 0.12
  X=  AREA M3 < 0.12
  X NOT INTERACT EXCLU
}
M3_5 {
@ Dielectric area enclosed by M3(um2) is 0.26
  X = HOLES M3 INNER EMPTY
  AREA X < 0.26 
}
M3_6a {
@ Dummy pattern is required in case M3 density is less than 16%
  DENSITY M3i < 0.16 WINDOW 200 STEP 100 PRINT density_report_M3a.log  //; Density check in each 200x200 square
}
M3_6b {
@ Max. Metal3  GDS pattern density is 82%
  DENSITY M3i > 0.82 WINDOW 200 STEP 100 PRINT density_report_M3b.log  //; Density check in each 200x200 square
}
M3_7 {
@ Space between metal line and 45 degree bent metal line that longer than 0.5um is 0.22
  X = ANGLE M3 == 45
  Y = LENGTH X >= 0.5
  EXT M3 Y < 0.22 ABUT<90 REGION
}
M3_8 { 
@ Max. M3 width is 15. Metal wider than this size should apply slot .
  X = M3 WITH WIDTH > 15
  Y = X INTERACT MIM
  (X NOT Y) NOT INTERACT PA
} 

//========================
//  M4 - Metal 4 DRC 
//========================


GROUP GM4 M4_?

/*
//--* Find Wide Metal *--//

WIDE_M4        = M4 INTERACT (SIZE M4 BY -5)   // wide metal

// begin shrink
WDM4_45        = ANGLE WIDE_M4 >0<90
WDM4_90        = NOT ANGLE WIDE_M4 >0<90

WDM4_EXP_45    = EXPAND EDGE WDM4_45 INSIDE BY 5*1.414213562
WDM4_EXP_90    = EXPAND EDGE WDM4_90 INSIDE BY 5 EXTEND BY 5

THIN_M4_TMP    = WIDE_M4 NOT (WDM4_EXP_45 OR WDM4_EXP_90)
THIN_M4        = SNAP THIN_M4_TMP 1            // end of shrink

// begin grow
TNM4_45        = ANGLE THIN_M4 >0<90
TNM4_90        = NOT ANGLE THIN_M4 >0<90

TNM4_EXP_45_TMP = EXPAND EDGE TNM4_45 OUTSIDE BY 5*1.414213562
TNM4_EXP_45    = SNAP TNM4_EXP_45_TMP 1
TNM4_EXP_90    = EXPAND EDGE TNM4_90 OUTSIDE BY 5 EXTEND BY 5

TNM4_EXP       = TNM4_EXP_45 OR TNM4_EXP_90

WDM4_PRE       = TNM4_EXP OR THIN_M4
WDM4_PRE_FILL  = EXT WDM4_PRE < 5 ABUT>0<90 INTERSECTING ONLY REGION

M4OVER10       = WDM4_PRE OR WDM4_PRE_FILL    // end of grow


//--* End of Finding Wide Metal *--//
*/

M4_1 {
@ M4 width is 0.2
  X= INT M4 < 0.20 ABUT<90 SINGULAR REGION
  X NOT INTERACT EXCLU
}
M4_2 {
@ Space between M4s is 0.2
  EXT M4 < 0.20 ABUT<90 SINGULAR REGION
}
M4_3a {
@ Space between M4s with one or both M4 width greater than 0.4um is 0.25
  X = M4 WITH WIDTH > 0.4
  EXT M4 X < 0.25 ABUT<90 REGION
}
M4_3b {
@ Space between M4s with one or both M4 width greater than 2um is 0.4
  X = M4 WITH WIDTH > 2
  EXT M4 X < 0.4 ABUT<90 REGION
}
M4_3c {
@ Space between M4s with one or both M4 width greater than 10um is 0.5
  X = M4 WITH WIDTH > 10
  EXT M4 X < 0.5 ABUT<89.9 REGION
}
M4_4 {
@ M4 area is 0.12
  X=  AREA M4 < 0.12
  X NOT INTERACT EXCLU
}
M4_5 {
@ Dielectric area enclosed by M4(um2) is 0.26
  X = HOLES M4 INNER EMPTY
  AREA X < 0.26 
}
M4_6a {
@ Dummy pattern is required in case M4 density is less than 16%
  DENSITY M4i < 0.16 WINDOW 200 STEP 100 PRINT density_report_M4a.log  //; Density check in each 200x200 square
}
M4_6b {
@ Max. Metal4  GDS pattern density is 82%
  DENSITY M4i > 0.82 WINDOW 200 STEP 100 PRINT density_report_M4b.log  //; Density check in each 200x200 square
}
M4_7 {
@ Space between metal line and 45 degree bent metal line that longer than 0.5um is 0.22
  X = ANGLE M4 == 45
  Y = LENGTH X >= 0.5
  EXT M4 Y < 0.22 ABUT<90 REGION
}
M4_8 { 
@ Max. M4 width is 15. Metal wider than this size should apply slot .
  X = M4 WITH WIDTH > 15
  Y = X INTERACT MIM
  (X NOT Y) NOT INTERACT PA
} 

//========================
//  M5 - Metal 5 DRC 
//========================


GROUP GM5 M5_?
/*
//--* Find Wide Metal *--//

WIDE_M5        = M5 INTERACT (SIZE M5 BY -5)   // wide metal

// begin shrink
WDM5_45        = ANGLE WIDE_M5 >0<90
WDM5_90        = NOT ANGLE WIDE_M5 >0<90

WDM5_EXP_45    = EXPAND EDGE WDM5_45 INSIDE BY 5*1.414213562
WDM5_EXP_90    = EXPAND EDGE WDM5_90 INSIDE BY 5 EXTEND BY 5

THIN_M5_TMP    = WIDE_M5 NOT (WDM5_EXP_45 OR WDM5_EXP_90)
THIN_M5        = SNAP THIN_M5_TMP 1            // end of shrink

// begin grow
TNM5_45        = ANGLE THIN_M5 >0<90
TNM5_90        = NOT ANGLE THIN_M5 >0<90

TNM5_EXP_45_TMP = EXPAND EDGE TNM5_45 OUTSIDE BY 5*1.414213562
TNM5_EXP_45    = SNAP TNM5_EXP_45_TMP 1
TNM5_EXP_90    = EXPAND EDGE TNM5_90 OUTSIDE BY 5 EXTEND BY 5

TNM5_EXP       = TNM5_EXP_45 OR TNM5_EXP_90

WDM5_PRE       = TNM5_EXP OR THIN_M5
WDM5_PRE_FILL  = EXT WDM5_PRE < 5 ABUT>0<90 INTERSECTING ONLY REGION

M5OVER10       = WDM5_PRE OR WDM5_PRE_FILL    // end of grow


//--* End of Finding Wide Metal *--//
*/

M5_1 {
@ M5 width is 0.2
  X=  INT M5 < 0.20 ABUT<90 SINGULAR REGION
  X NOT INTERACT EXCLU
}
M5_2 {
@ Space between M5s is 0.2
  EXT M5 < 0.20 ABUT<90 SINGULAR REGION
}
M5_3a {
@ Space between M5s with one or both M5 width greater than 0.4um is 0.25
  X = M5 WITH WIDTH > 0.4
  EXT M5 X < 0.25 ABUT<90 REGION
}
M5_3b {
@ Space between M5s with one or both M5 width greater than 2um is 0.4
  X = M5 WITH WIDTH > 2
  EXT M5 X < 0.4 ABUT<90 REGION
}
M5_3c {
@ Space between M5s with one or both M5 width greater than 10um is 0.5
  X = M5 WITH WIDTH > 10
  EXT M5 X < 0.5 ABUT<89.9 REGION
}
M5_4 {
@ M5 area is 0.12
  X=  AREA M5 < 0.12
  X NOT INTERACT EXCLU
}
M5_5 {
@ Dielectric area enclosed by M5(um2) is 0.26
  X = HOLES M5 INNER EMPTY
  AREA X < 0.26 
}
M5_6a {
@ Dummy pattern is required in case M5 density is less than 16%
  DENSITY M5i < 0.16 WINDOW 200 STEP 100 PRINT density_report_M5a.log  //; Density check in each 200x200 square
}
M5_6b {
@ Max. Metal5  GDS pattern density is 82%
  DENSITY M5i > 0.82 WINDOW 200 STEP 100 PRINT density_report_M5b.log  //; Density check in each 200x200 square
}
M5_7 {
@ Space between metal line and 45 degree bent metal line that longer than 0.5um is 0.22
  X = ANGLE M5 == 45
  Y = LENGTH X >= 0.5
  EXT M5 Y < 0.22 ABUT<90 REGION
}
M5_8 { 
@ Max. M5 width is 15. Metal wider than this size should apply slot .
  X = M5 WITH WIDTH > 15
  Y = X INTERACT MIM
  (X NOT Y) NOT INTERACT PA
} 

//========================
//  M6 - Metal 6 DRC 
//========================


GROUP GM6 M6_?
/*
//--* Find Wide Metal *--//

WIDE_M6        = M6 INTERACT (SIZE M6 BY -5)   // wide metal

// begin shrink
WDM6_45        = ANGLE WIDE_M6 >0<90
WDM6_90        = NOT ANGLE WIDE_M6 >0<90

WDM6_EXP_45    = EXPAND EDGE WDM6_45 INSIDE BY 5*1.414213562
WDM6_EXP_90    = EXPAND EDGE WDM6_90 INSIDE BY 5 EXTEND BY 5

THIN_M6_TMP    = WIDE_M6 NOT (WDM6_EXP_45 OR WDM6_EXP_90)
THIN_M6        = SNAP THIN_M6_TMP 1            // end of shrink

// begin grow
TNM6_45        = ANGLE THIN_M6 >0<90
TNM6_90        = NOT ANGLE THIN_M6 >0<90

TNM6_EXP_45_TMP = EXPAND EDGE TNM6_45 OUTSIDE BY 5*1.414213562
TNM6_EXP_45    = SNAP TNM6_EXP_45_TMP 1
TNM6_EXP_90    = EXPAND EDGE TNM6_90 OUTSIDE BY 5 EXTEND BY 5

TNM6_EXP       = TNM6_EXP_45 OR TNM6_EXP_90

WDM6_PRE       = TNM6_EXP OR THIN_M6
WDM6_PRE_FILL  = EXT WDM6_PRE < 5 ABUT>0<90 INTERSECTING ONLY REGION

M6OVER10       = WDM6_PRE OR WDM6_PRE_FILL    // end of grow


//--* End of Finding Wide Metal *--//
*/

M6_1 {
@ M6 width is 0.2
  X= INT M6 < 0.20 ABUT<90 SINGULAR REGION
  X NOT INTERACT EXCLU
}
M6_2 {
@ Space between M6s is 0.2
  EXT M6 < 0.20 ABUT<90 SINGULAR REGION
}
M6_3a {
@ Space between M6s with one or both M6 width greater than 0.4um is 0.25
  X = M6 WITH WIDTH > 0.4
  EXT M6 X < 0.25 ABUT<90 REGION
}
M6_3b {
@ Space between M6s with one or both M6 width greater than 2um is 0.4
  X = M6 WITH WIDTH > 2
  EXT M6 X < 0.4 ABUT<90 REGION
}
M6_3c {
@ Space between M6s with one or both M6 width greater than 10um is 0.5
  X = M6 WITH WIDTH > 10
  EXT M6 X < 0.5 ABUT<89.9 REGION
}
M6_4 {
@ M6 area is 0.12
  X=  AREA M6 < 0.12
  X NOT INTERACT EXCLU
}
M6_5 {
@ Dielectric area enclosed by M6(um2) is 0.26
  X = HOLES M6 INNER EMPTY
  AREA X < 0.26 
}
M6_6a {
@ Dummy pattern is required in case M6 density is less than 16%
  DENSITY M6i < 0.16 WINDOW 200 STEP 100 PRINT density_report_M6a.log  //; Density check in each 200x200 square
}
M6_6b {
@ Max. Metal6 GDS pattern density is 82%
  DENSITY M6i > 0.82 WINDOW 200 STEP 100 PRINT density_report_M6b.log  //; Density check in each 200x200 square
}
M6_7 {
@ Space between metal line and 45 degree bent metal line longer than 0.5um is 0.22
  X = ANGLE M6 == 45
  Y = LENGTH X >= 0.5
  EXT M6 Y < 0.22 ABUT<90 REGION
}
M6_8 { 
@ Max. M6 width is 15. Metal wider than this size should apply slot .
  X = M6 WITH WIDTH > 15
  Y = X INTERACT MIM
  (X NOT Y) NOT INTERACT PA
} 

//========================
//  TM1 - Top Metal 1 DRC 
//========================


GROUP GTM1 TM1_?
/*
//--* Find Wide Metal *--//

WIDE_M7        = M7 INTERACT (SIZE M7 BY -5)   // wide metal

// begin shrink
WDM7_45        = ANGLE WIDE_M7 >0<90
WDM7_90        = NOT ANGLE WIDE_M7 >0<90

WDM7_EXP_45    = EXPAND EDGE WDM7_45 INSIDE BY 5*1.414213562
WDM7_EXP_90    = EXPAND EDGE WDM7_90 INSIDE BY 5 EXTEND BY 5

THIN_M7_TMP    = WIDE_M7 NOT (WDM7_EXP_45 OR WDM7_EXP_90)
THIN_M7        = SNAP THIN_M7_TMP 1            // end of shrink

// begin grow
TNM7_45        = ANGLE THIN_M7 >0<90
TNM7_90        = NOT ANGLE THIN_M7 >0<90

TNM7_EXP_45_TMP = EXPAND EDGE TNM7_45 OUTSIDE BY 5*1.414213562
TNM7_EXP_45    = SNAP TNM7_EXP_45_TMP 1
TNM7_EXP_90    = EXPAND EDGE TNM7_90 OUTSIDE BY 5 EXTEND BY 5

TNM7_EXP       = TNM7_EXP_45 OR TNM7_EXP_90

WDM7_PRE       = TNM7_EXP OR THIN_M7
WDM7_PRE_FILL  = EXT WDM7_PRE < 5 ABUT>0<90 INTERSECTING ONLY REGION

M7OVER10       = WDM7_PRE OR WDM7_PRE_FILL    // end of grow


//--* End of Finding Wide Metal *--//
*/

TM1_1 {
@ TM1 width is 0.42
  X = INT TM1 < 0.42 ABUT<90 SINGULAR REGION
  X NOT INTERACT EXCLU
}
TM1_2 {
@ Space between TM1s is 0.42
  EXT TM1 < 0.42 ABUT<90 SINGULAR REGION
}
TM1_4 {
@ Space between TM1s with one or both width greater than 2um is 0.5
  X = TM1 WITH WIDTH > 2
  EXT TM1 X < 0.5 ABUT<90 REGION
}
TM1_5 {
@ TM1 area (in um2) is 0.42
  X=  AREA TM1 < 0.42
  X NOT INTERACT EXCLU
}
TM1_6 {
@ Dielectric area enclosed by TM1(um2) is 1.0
  X = HOLES TM1 INNER EMPTY
  AREA X < 1.0
}
TM1_7a {
@ Min. Top Metal 1 GDS pattern density is 16%
  DENSITY TM1i < 0.16 WINDOW 200 STEP 100 PRINT density_report_TM1a.log //; Density check in each 200x200 square
}
TM1_7b {
@ Max. Top Metal 1  GDS pattern density is 82%
  DENSITY TM1i > 0.82 WINDOW 200 STEP 100 PRINT density_report_TM1b.log //; Density check in each 200x200 square
}
TM1_8 {
@ Max. TM1 width is 30. Metal wider than this size should apply slot .
  X = TM1 WITH WIDTH > 30
  Y = X INTERACT MIM
  (X NOT Y) NOT INTERACT PA
}

//========================
//  V2 - Via 2 DRC
//========================

GROUP GV2 V2_?

V2_1 {
@ Fixed V2 size is 0.19
  V2x = V2 NOT V2FUSE_RING
  X=  NOT RECTANGLE V2x == 0.19 BY == 0.19
  X NOT TOUCH EXCLU
}
V2_2a {
@ Space between V2s is 0.21
  EXT V2 < 0.21 ABUT<90 SINGULAR REGION
}
V2_2b {
@ Space between V2s within array greater or equal to 4*4 is 0.25um
@ Two via regions whose space is <= 0.27um are considered to be in the same array
  X = SIZE V2 BY 0.135 OVERUNDER
  Y = SIZE X BY 0.65 UNDEROVER
  Z = V2 INSIDE Y
  EXT Z < 0.25 ABUT<90 SINGULAR REGION
}
V2_3_4_7 {
@ V2 enclosure by M2 is 0.005
@ Extension of M2 line end outside of V2 is 0.05
@ For V2 enclosed at the 90 degree corner by M2, M2 extension at least along one direction outside V2 is 0.05
  X = RECTANGLE ENC (V2 NOT INTERACT EXCLU) (M2 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.05 OPPOSITE 0.005 OPPOSITE 0.05 OPPOSITE

  ENC X M2 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M2 < 0.05 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V2 NOT M2
}
V2_5_6_8 {
@ V2 enclosure by M3 is 0.005
@ Extension of M3 line end outside of V2 is 0.03
@ For V2 enclosed at the 90 degree corner by M3, M3 extension at least along one direction outside V2 is 0.03
  X = RECTANGLE ENC (V2 NOT INTERACT EXCLU) (M3 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.03 OPPOSITE 0.005 OPPOSITE 0.03 OPPOSITE

  ENC X M3 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M3 < 0.03 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V2 NOT M3
}
V2_10 {
@  When one or both M2 and M3 width > 0.9um and intersection area is large enough to enclose a rectangle of size >=0.2um*0.7um, there should be at least two V2s in this M2and M3 intersection area
 // X = AND M2 M3
 // Y = X WITH WIDTH > 0.9
 // Y INTERACT V2 < 2
   M2M3 = M2 AND M3
   M2M3_enc = ENCLOSE RECTANGLE M2M3 0.2 0.7
   X1_M2 = M2 WITH WIDTH > 0.9
   X2_M2 = (M2M3_enc INTERACT X1_M2) INTERACT V2
   X2_M2 INTERACT V2 <2
   X1_M3 = M3 WITH WIDTH > 0.9
   X2_M3 = (M2M3_enc INTERACT X1_M3) INTERACT V2
   X2_M3 INTERACT V2 <2
}
// V2_11 in V2_3&V2_5


//========================
//  V3 - Via 3 DRC
//========================

GROUP GV3 V3_?


V3_1 {
@ Fixed V3 size is 0.19
  V3x = V3 NOT V3FUSE_RING
  X= NOT RECTANGLE V3x == 0.19 BY == 0.19
  X NOT TOUCH EXCLU
}
V3_2a {
@ Space between V3s is 0.21
  EXT V3 < 0.21 ABUT<90 SINGULAR REGION
}
V3_2b {
@ Space between V3s within array greater or equal to 4*4 is 0.25um
@ Two via regions whose space is <= 0.27um are considered to be in the same array
  X = SIZE V3 BY 0.135 OVERUNDER
  Y = SIZE X BY 0.65 UNDEROVER
  Z = V3 INSIDE Y
  EXT Z < 0.25 ABUT<90 SINGULAR REGION
}
V3_3_4_7 {
@ V3 enclosure by M3 is 0.005
@ Extension of M3 line end outside of V3 is 0.05
@ For V3 enclosed at the 90 degree corner by M3, M3 extension at least along one direction outside V3 is 0.05
  X = RECTANGLE ENC (V3 NOT INTERACT EXCLU) (M3 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.05 OPPOSITE 0.005 OPPOSITE 0.05 OPPOSITE

  ENC X M3 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M3 < 0.05 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V3 NOT M3
}
V3_5_6_8 {
@ V3 enclosure by M4 is 0.005
@ Extension of M4 line end outside of V3 is 0.03
@ For V3 enclosed at the 90 degree corner by M4, M4 extension at least along one direction outside V3 is 0.03
  X = RECTANGLE ENC (V3 NOT INTERACT EXCLU) (M4 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.03 OPPOSITE 0.005 OPPOSITE 0.03 OPPOSITE

  ENC X M4 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M4 < 0.03 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V3 NOT M4
}
V3_10 {
@ When one or both M3 and M4 width > 0.9um and intersection area is large enough to enclose a rectangle of size >=0.2um*0.7um, there should be at least two V3s in this M3and M4 intersection area
 // X = AND M3 M4
 // Y = X WITH WIDTH > 0.9
 // Y INTERACT V3 < 2
   M3M4 = M3 AND M4
   M3M4_enc = ENCLOSE RECTANGLE M3M4 0.2 0.7
   X1_M3 = M3 WITH WIDTH > 0.9
   X2_M3 = (M3M4_enc INTERACT X1_M3) INTERACT V3
   X2_M3 INTERACT V3 <2
   X1_M4 = M4 WITH WIDTH > 0.9
   X2_M4 = (M3M4_enc INTERACT X1_M4) INTERACT V3
   X2_M4 INTERACT V3 <2
}
// V3_11 in V3_3&V3_5


//========================
//  V4 - Via 4 DRC
//========================

GROUP GV4 V4_?

V4_1 {
@ Fixed V4 size is 0.19
  V4x = V4 NOT V4FUSE_RING
  X=  NOT RECTANGLE V4x == 0.19 BY == 0.19
  X NOT TOUCH EXCLU
}
V4_2a {
@ Space between V4s is 0.21
  EXT V4 < 0.21 ABUT<90 SINGULAR REGION
}
V4_2b {
@ Space between V4s within array greater or equal to 4*4 is 0.25um
@ Two via regions whose space is <= 0.27um are considered to be in the same array
  X = SIZE V4 BY 0.135 OVERUNDER
  Y = SIZE X BY 0.65 UNDEROVER
  Z = V4 INSIDE Y
  EXT Z < 0.25 ABUT<90 SINGULAR REGION
}
V4_3_4_7 {
@ V4 enclosure by M4 is 0.005
@ Extension of M4 line end outside of V4 is 0.05
@ For V4 enclosed at the 90 degree corner by M4, M4 extension at least along one direction outside V4 is 0.05
  X = RECTANGLE ENC (V4 NOT INTERACT EXCLU) (M4 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.05 OPPOSITE 0.005 OPPOSITE 0.05 OPPOSITE

  ENC X M4 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M4 < 0.05 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V4 NOT M4
}
V4_5_6_8 {
@ V4 enclosure by M5 is 0.005
@ Extension of M5 line end outside of V4 is 0.03
@ For V4 enclosed at the 90 degree corner by M5, M5 extension at least along one direction outside V4 is 0.03
  X = RECTANGLE ENC (V4 NOT INTERACT EXCLU) (M5 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.03 OPPOSITE 0.005 OPPOSITE 0.03 OPPOSITE

  ENC X M5 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M5 < 0.03 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V4 NOT M5
}
V4_10 {
@ When one or both M4 and M5 width > 0.9um and intersection area is large enough to enclose a rectangle of size >=0.2um*0.7um, there should be at least two V4s in this M4and M5 intersection area
 // X = AND M4 M5
 // Y = X WITH WIDTH > 0.9
 // Y INTERACT V4 < 2
   M4M5 = M4 AND M5
   M4M5_enc = ENCLOSE RECTANGLE M4M5 0.2 0.7
   X1_M4 = M4 WITH WIDTH > 0.9
   X2_M4 = (M4M5_enc INTERACT X1_M4) INTERACT V4
   X2_M4 INTERACT V4 <2
   X1_M5 = M5 WITH WIDTH > 0.9
   X2_M5 = (M4M5_enc INTERACT X1_M5) INTERACT V4
   X2_M5 INTERACT V4 <2
}
// V4_11 in V4_3&V4_5


//========================
//  V5 - Via 5 DRC
//========================

GROUP GV5 V5_?

V5_1 {
@ Fixed V5 size is 0.19
  V5x = V5 NOT V5FUSE_RING
  X= NOT RECTANGLE V5x == 0.19 BY == 0.19
  X NOT TOUCH EXCLU
}
V5_2a {
@ Space between V5s is 0.21
  EXT V5 < 0.21 ABUT<90 SINGULAR REGION
}
V5_2b {
@ Space between V5s within array greater or equal to 4*4 is 0.25um
@ Two via regions whose space is <= 0.27um are considered to be in the same array
  X = SIZE V5 BY 0.135 OVERUNDER
  Y = SIZE X BY 0.65 UNDEROVER
  Z = V5 INSIDE Y
  EXT Z < 0.25 ABUT<90 SINGULAR REGION
}
V5_3_4_7 {
@ V5 enclosure by M5 is 0.005
@ Extension of M5 line end outside of V5 is 0.05
@ For V5 enclosed at the 90 degree corner by M5, M5 extension at least along one direction outside V5 is 0.05
  X = RECTANGLE ENC (V5 NOT INTERACT EXCLU) (M5 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.05 OPPOSITE 0.005 OPPOSITE 0.05 OPPOSITE

  ENC X M5 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M5 < 0.05 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  (V5 NOT INTERACT P2) NOT M5
}
V5_5_6_8 {
@ V5 enclosure by M6 is 0.005
@ Extension of M6 line end outside of V5 is 0.03
@ For V5 enclosed at the 90 degree corner by M6, M6 extension at least along one direction outside V5 is 0.03
  X = RECTANGLE ENC (V5 NOT INTERACT EXCLU) (M6 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.005 OPPOSITE 0.03 OPPOSITE 0.005 OPPOSITE 0.03 OPPOSITE

  ENC X M6 < 0.005 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M6 < 0.03 OPPOSITE
  INT Y < 0.005 ABUT==90 INTERSECTING ONLY
  V5 NOT M6
}
V5_10 {
@ When one or both M5 and M6 width > 0.9um and intersection area is large enough to enclose a rectangle of size >=0.2um*0.7um, there should be at least two V5s in this M5and M6 intersection area
 // X = AND M5 M6
 // Y = X WITH WIDTH > 0.9
 // Y INTERACT V5 < 2
   M5M6 = M5 AND M6
   M5M6_enc = ENCLOSE RECTANGLE M5M6 0.2 0.7
   X1_M5 = M5 WITH WIDTH > 0.9
   X2_M5 = (M5M6_enc INTERACT X1_M5) INTERACT V5
   X2_M5 INTERACT V5 <2
   X1_M6 = M6 WITH WIDTH > 0.9
   X2_M6 = (M5M6_enc INTERACT X1_M6) INTERACT V5
   X2_M6 INTERACT V5 <2
}
// V5_11 in V5_3&V5_5


//========================
//  TV1 - Top Via 1 DRC
//========================

GROUP GTV1 TV1_?

TV1_1 {
@ Fixed TV1 size is 0.36
  TV1x = TV1 NOT TV1FUSE_RING
  X=  NOT RECTANGLE TV1x == 0.36 BY == 0.36
  X NOT TOUCH EXCLU
}
TV1_2a {
@ Space between TV1s is 0.34
  EXT TV1 < 0.34 ABUT<90 SINGULAR REGION
}
TV1_2b {
@ Min space between TVs within array greater or equal to 3x3 is 0.50um;
@ Two via regions whose space is <= 0.52um are considered to be in the same array.
  X = SIZE TV1 BY 0.26 OVERUNDER
  Y = SIZE X BY 0.85 UNDEROVER
  Z = TV1 INSIDE Y
  EXT Z < 0.50 ABUT<90 SINGULAR REGION
}
TV1_4_5 {
@ TV1 enclosure by metal layer directly underneath TV1 is 0.01
@ Extension of Mn line end outside of TV1. Mn is a metal layer directly underneath TV1 is 0.05
  X = RECTANGLE ENC (TV1 NOT INTERACT EXCLU) (M6 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.01 OPPOSITE 0.05 OPPOSITE 0.01 OPPOSITE 0.05 OPPOSITE

  ENC X M6 < 0.01 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] M6 < 0.05 OPPOSITE
  INT Y < 0.01 ABUT==90 INTERSECTING ONLY
  (TV1 NOT INTERACT P2) NOT M6
}
TV1_6_7 {
@ TV1 enclosure by TM1 is 0.03
@ Extension of TM1 line end outside of TV1 is 0.07
  X = RECTANGLE ENC (TV1 NOT INTERACT EXCLU) (TM1 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.03 OPPOSITE 0.07 OPPOSITE 0.03 OPPOSITE 0.07 OPPOSITE

  ENC X TM1 < 0.03 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] TM1 < 0.07 OPPOSITE
  INT Y < 0.03 ABUT==90 INTERSECTING ONLY
  TV1 NOT TM1
}

// TV1_10 in TV1_4&TV1_6

//=====================
//  TV2 - Top Via 2 DRC
//=====================

GROUP GTV2 TV2_?

TV2_1 {
@ Fixed TV2 size is 0.36
  TV2x = TV2 NOT TV2FUSE_RING
  X = NOT RECTANGLE TV2x == 0.36 BY == 0.36
  X NOT TOUCH EXCLU
}
TV2_2a {
@ Space between TV2s is 0.34
  EXT TV2 < 0.34 ABUT<90 SINGULAR REGION
}
TV2_2b {
@ Min space between TVs within array greater or equal to 3x3 is 0.50um;
@ Two via regions whose space is <= 0.52um are considered to be in the same array.
  X = SIZE TV2 BY 0.26 OVERUNDER
  Y = SIZE X BY 0.85 UNDEROVER
  Z = TV2 INSIDE Y
  EXT Z < 0.50 ABUT<90 SINGULAR REGION
}
TV2_4_5 {
@ TV2 enclosure by TM1 directly underneath TV2 is 0.01
@ Extension of TM1 line end outside of TV2. TM1 is a metal layer directly underneath TV2 is 0.05 
  X = RECTANGLE ENC (TV2 NOT INTERACT EXCLU) (TM1 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.01 OPPOSITE 0.05 OPPOSITE 0.01 OPPOSITE 0.05 OPPOSITE

  ENC X TM1 < 0.01 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] TM1 < 0.05 OPPOSITE
  INT Y < 0.01 ABUT==90 INTERSECTING ONLY
  TV2 NOT TM1
}
/*
TV2_6_7 {
@ TV2 enclosure by TM2 is 0.03
@ Extension of TM2 line end outside of TV2 is 0.07
  X = RECTANGLE ENC (TV2 NOT INTERACT EXCLU) (TM2 NOT INTERACT EXCLU) ABUT<90
  GOOD 0.03 OPPOSITE 0.07 OPPOSITE 0.03 OPPOSITE 0.07 OPPOSITE

  ENC X TM2 < 0.03 ABUT<90 SINGULAR OVERLAP OUTSIDE ALSO

  Y = ENC [X] TM2 < 0.07 OPPOSITE
  INT Y < 0.03 ABUT==90 INTERSECTING ONLY
  TV2 NOT TM2
}
*/

// TV2_10 in TV2_4&TV2_6

//=======================
//  TM2 - Top Metal 2 DRC
//=======================


GROUP GTM2 TM2_?

/*
//--* Find Wide Metal *--//

WIDE_TM     = TM INTERACT (SIZE TM BY -8)   // wide metal

// begin shrink
WDTM_45     = ANGLE WIDE_TM >0<90
WDTM_90     = NOT ANGLE WIDE_TM >0<90

WDTM_EXP_45 = EXPAND EDGE WDTM_45 INSIDE BY 8*1.414213562
WDTM_EXP_90 = EXPAND EDGE WDTM_90 INSIDE BY 8 EXTEND BY 8

THIN_TM_TMP = WIDE_TM NOT (WDTM_EXP_45 OR WDTM_EXP_90)
THIN_TM     = SNAP THIN_TM_TMP 1            // end of shrink

// begin grow
TNTM_45     = ANGLE THIN_TM >0<90
TNTM_90     = NOT ANGLE THIN_TM >0<90

TNTM_EXP_45_TMP = EXPAND EDGE TNTM_45 OUTSIDE BY 8*1.414213562
TNTM_EXP_45 = SNAP TNTM_EXP_45_TMP 1
TNTM_EXP_90 = EXPAND EDGE TNTM_90 OUTSIDE BY 8 EXTEND BY 8

TNTM_EXP    = TNTM_EXP_45 OR TNTM_EXP_90

WDTM_PRE    = TNTM_EXP OR THIN_TM
WDTM_PRE_FILL = EXT WDTM_PRE < 8 ABUT>0<90 INTERSECTING ONLY REGION

TMOVER16    = WDTM_PRE OR WDTM_PRE_FILL    // end of grow


//--* End of Finding Wide Metal *--//
*/

TM2_1 {
@ TM2.1
@ TM2 width is 1.5
  INT TM2 < 1.5 ABUT<90 SINGULAR REGION
}
TM2_2a {
@ TM2.2a
@ Space between TM2s is 1.5
  EXT TM2 < 1.5 ABUT<90 SINGULAR REGION
}
TM2_2b {
@ TM2.2b
@ Space between TM2s which width larger than 16um is 3.0
  X = TM2 WITH WIDTH > 16
  EXT TM2 X < 3.0 ABUT<90 SINGULAR REGION
}
TM2_3 {
@ TM2.3
@ TM2 region extension outside of top Via 2 is 0.3
  ENC TV2 TM2< 0.3 ABUT<90 SINGULAR REGION OUTSIDE ALSO
}
TM2_4 {
@ TM2.4
@ TM2 region extension outside of top Via 2 at the end of TM2 is 0.42
  RECTANGLE ENCLOSURE TV2 TM2
  GOOD 0.3 0.42 0.3 0.42
}
DISCONNECT
CONNECT TM2 TM1 BY TV2
CONNECT TM1 M6 BY TV1
CONNECT M6 M5 BY V5
CONNECT M5 M4 BY V4
CONNECT M4 M3 BY V3
CONNECT M3 M2 BY V2
CONNECT M2 M1 BY V1
TM2_5 {
@ TM2.5
@ Clearance from TM2 used as Inductor to other TM2 region is 30
  X = TM2i AND INDMY
  EXT X TM2i < 30 ABUT<90 SINGULAR REGION NOT CONNECTED
}
TM2_6 {
@ TM2.6
@ TM2 area region is 2.25um
  X = AREA TM2 < 2.25
  X NOT INTERACT EXCLU
}
TM2_7a{
@ TM2.7a
@ Min.TM2 area density is 30 %
  DENSITY TM2i < 0.3
}
TM2_7b{
@ TM2.7b(GuideLine, could be controled by User)
@ Dummy TM2 pattern suggested 2*5 um*um
  DMY_TM2 NOT WITH EDGE LET_TM2Y
}
TM2_7c{
@ TM2.7c (GuideLine, could be controled by User)
@ Dummy TM2 pattern suggested 2*5 um*um with space 2 um
  Z = DMY_TM2 WITH EDGE LET_TM2Y
  EXT Z < 2 ABUT<90 SINGULAR REGION
}

TM2_8 {
@ TM2.8 (GuideLine, could be controled by User)
@ Extension of dummy layer "INDMY" beyond TM2 region used as Inductor is 30
  X = TM2 INTERACT INDMY
  ENC X INDMY < 30 ABUT<90 SINGULAR REGION
}
TM2_9 {
@ TM2.9
@ Both active and passive devices inside Inductor region are not allowed
  X = (GT OR AA) OR RESNW
  AND X INDMY
}
TM2_10a {
@ TM2.10a
@ No Via and metal layers inside "INDMY" is allowed except Inductor connection
X = INDMY WITH TEXT "diff_ind_rf"
DIFF_IND = INDMY INTERACT X
Mn AND DIFF_IND
Vn AND DIFF_IND
}
TM2_10b {
@ TM2.10b
@ No Via and metal layers inside "INDMY" is allowed except 3T_Inductor connection
X = INDMY WITH TEXT "diff_ind_3t_rf"
DIFF_IND_3T = INDMY INTERACT X
Mn_1 AND DIFF_IND_3T
Vn_1 AND DIFF_IND_3T
}
TM2_10c {
@TM2_10c
@No via & metal layers inside INDMY is allowed except SPRI_Inductor connection
X = INDMY NOT WITH TEXT "diff_ind_rf"
Y = X NOT WITH TEXT "diff_ind_3t_rf"
Z = Y NOT WITH TEXT "diff_ind_3t_alpa_rf"
SPRI_IND = INDMY INTERACT Z
Mn AND SPRI_IND
Vn AND SPRI_IND
}
TM2_10d {
@TM2_10d
@No via & metal layers inside INDMY is allowed except 3T inductor connection for 3.3V
X = INDMY WITH TEXT "diff_ind_3t_alpa_rf"
ALPA_IND = INDMY INTERACT X
Mn_1 AND ALPA_IND
Vn_1 AND ALPA_IND
}

//==========================================
//  PI - Guidline for polymide layout minima
//==========================================

GROUP GPI PI_?

PI_1 {
@ Space between two polymide open area is 15
  EXT PI < 15 ABUT<90 SINGULAR REGION
}
PI_2 {
@ Space between polyimide open area to chip edge is 15
  ENC PIi BORDER < 15 ABUT<90 SINGULAR REGION OUTSIDE ALSO
}

//===========================
// Find wide Al bump pad
//===========================

ALPA_MD = SIZE (ALPA INTERACT MD) BY 12 UNDEROVER
ALPA_PI = SIZE (ALPA INTERACT PI) BY 5 UNDEROVER
ALPA_BUMP = ALPA_MD OR ALPA_PI


//=========================================
//  ALPA - Guidline for ALPad layout minima
//=========================================

GROUP GALPA ALPA_?

ALPA_1 {
@ Space between two ALPads is 4.0
  EXT ALPA_BUMP < 4.0 ABUT<90 SINGULAR REGION
}


//=========================================
//  PA2 RDL - Guidline for PA2 RDL rules
//=========================================

GROUP GPA2_RDL RDL_PA2_?

ALPA_X = ALPA NOT ALPA_BUMP
PA_HOLE = PA INSIDE ALPA

RDL_PA2_1 {
@ Minimum RDL width is 3
  INT ALPA < 3 ABUT<90 SINGULAR REGION
}
RDL_PA2_2 {
@ Minimum RDL space is 3
  EXT ALPA < 3 ABUT<90 SINGULAR REGION
}
//RDL_PA2_3:RDL turning if MD(PA2) is polymide.
RDL_PA2_4 {
@ Minimum PA hole size is 4
  RECTANGLE PA_HOLE < 4
  NOT RECTANGLE PA_HOLE ASPECT == 1
}
RDL_PA2_5 {
@ Minimum space between two PA hole is 4
  EXT PA_HOLE < 4 ABUT<90 SINGULAR REGION
}
RDL_PA2_6 {
@ Minimum PA hole to RDL enclosure is 2
  ENC PA_HOLE ALPA < 2 ABUT<90 SINGULAR REGION OUTSIDE ALSO
}
RDL_PA2_7 {
@ Minimum PA hole to TM2 enclosue is 2
  ENC PA_HOLE TM2 < 2 ABUT<90 SINGULAR REGION OUTSIDE ALSO
}
RDL_PA2_8 {
@ Minimum enclosue of MD(PA2) to Al bump pad is 2.0
  ENC MD ALPA < 2.0 ABUT<90 SINGULAR REGION
}
//RDL_PA2_9:Minimum Al bump pad pitch is 150
RDL_PA2_10 {
@ Minimum space between RDL and Al bump pad is 10
  EXT (ALPA_X NOT TOUCH EDGE ALPA_MD) ALPA_MD < 10 ABUT<90 REGION
}
RDL_PA2_11 {
@ Minimum space between RDL and Fuse window is 10
  EXT ALPA_X FUSE < 10 ABUT<90 SINGULAR REGION
}
RDL_PA2_12 {
@ Minimum space between RDL and L mark window is 10
  EXT ALPA_X LOGO < 10 ABUT<90 SINGULAR REGION
}
RDL_PA2_13 {
@ Minimum space between Al bump pad and Fuse window is 50
  EXT ALPA_MD FUSE < 50 ABUT<90 SINGULAR REGION
}
RDL_PA2_14 {
@ Pattern density of RDL layer should be between 0~50%
 DENSITY ALPA > 0.50 PRINT density_report_RDL_PA2.log
}

//=========================================
//  PI RDL - Guidline for PI RDL rules
//=========================================

GROUP GPI_RDL RDL_PI_?

RDL_PI_1 {
@ Minimum RDL width is 3
  INT ALPA < 3 ABUT<90 SINGULAR REGION
}
RDL_PI_2 {
@ Minimum RDL space is 3
  EXT ALPA < 3 ABUT<90 SINGULAR REGION
}
RDL_PI_3 {
@ RDL turning is 135 if MD(PA2) is polymide.
  EXT ALPA_X < 0.01 ABUT < 135 REGION
}
RDL_PI_4 {
@ Minimum PA hole size is 10
  RECTANGLE PA_HOLE < 10
  NOT RECTANGLE PA_HOLE ASPECT == 1
}
RDL_PI_5 {
@ Minimum space between two PA hole is 10
  EXT PA_HOLE < 10 ABUT<90 SINGULAR REGION
}
RDL_PI_6 {
@ Minimum PA hole to RDL enclosure is 2
  ENC PA_HOLE ALPA < 2 ABUT<90 SINGULAR REGION OUTSIDE ALSO
}
RDL_PI_7 {
@ Minimum PA hole to TM2 enclosue is 2
  ENC PA_HOLE TM2 < 2 ABUT<90 SINGULAR REGION OUTSIDE ALSO
}
RDL_PI_8 {
@ Minimum enclosue of MD(PA2) to Al bump pad is 2
  ENC MD ALPA < 2 ABUT<90 SINGULAR REGION
}
//RDL_PI_9:Minimum Al bump pad pitch is 150
RDL_PI_10 {
@ Minimum space between RDL and Al bump pad is 10
  EXT (ALPA_X NOT TOUCH EDGE ALPA_PI) ALPA_PI < 10 ABUT<90 REGION
}
RDL_PI_11 {
@ Minimum space between RDL and Fuse window is 10
  EXT ALPA_X FUSE < 10 ABUT<90 SINGULAR REGION
}
RDL_PI_12 {
@ Minimum space between RDL and L mark window is 10
  EXT ALPA_X LOGO < 10 ABUT<90 SINGULAR REGION
}
RDL_PI_13 {
@ Minimum space between Al bump pad and Fuse window is 50
  EXT ALPA_PI FUSE < 50 ABUT<90 SINGULAR REGION
}
RDL_PI_14 {
@ Pattern density of RDL layer should be between 0~50%
 DENSITY ALPA > 0.50 PRINT density_report_RDL_PI.log
}

//====================================================
//  BD -  Border enclosure rules for CHIP Level Only
//====================================================

GROUP GBD BD_?

BORDER_ALL=BORDER OR (HOLES BORDER)

BD_1 {
@ Layout pattern (all chip design) minimum enclosure by chip edge (BORDER layer) is 0.73
 ENC   AAi BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   GTi BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   CTi BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   M1i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   M2i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   M3i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   M4i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   M5i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   M6i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC  TM1i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC  TM2i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   V1i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   V2i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   V3i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   V4i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC   V5i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC  TV1i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
 ENC  TV2i BORDER_ALL < 0.73 ABUT < 90 SINGULAR REGION OUTSIDE ALSO
}
BD_2 {
@ DNW minimum enclosure by chip edge(BORDER layer) is 6
 ENC DNWi BORDER_ALL < 6 ABUT<90 SINGULAR REGION OUTSIDE ALSO
}

// BD_3 is covered by BD_1 and BD_2 


//============================================              
//  SL -  Recommended Metal Slot design minima
//============================================              

GROUP GSL SL_? 

LM1_S = LM1 OR empty
LM2_S = LM2 OR empty
LM3_S = LM3 OR empty
LM4_S = LM4 OR empty
LM5_S = LM5 OR empty
LM6_S = LM6 OR empty
LTM1_S = LTM1 OR empty
LTM2_S = LTM2 OR empty

M1SLOT_S = M1SLOT OR empty
M2SLOT_S = M2SLOT OR empty
M3SLOT_S = M3SLOT OR empty
M4SLOT_S = M4SLOT OR empty
M5SLOT_S = M5SLOT OR empty
M6SLOT_S = M6SLOT OR empty
TM1SLOT_S = TM1SLOT OR empty
TM2SLOT_S = TM2SLOT OR empty

CONNECT M1SLOT_S LM1_S
CONNECT M2SLOT_S LM2_S
CONNECT M3SLOT_S LM3_S
CONNECT M4SLOT_S LM4_S
CONNECT M5SLOT_S LM5_S
CONNECT M6SLOT_S LM6_S
CONNECT TM1SLOT_S LTM1_S
CONNECT TM2SLOT_S LTM2_S

LM1    = SIZE (M1 OR M1SLOT1) BY 7 UNDEROVER
LM2    = SIZE (M2 OR M2SLOT1) BY 7.5 UNDEROVER
LM3    = SIZE (M3 OR M3SLOT1) BY 7.5 UNDEROVER
LM4    = SIZE (M4 OR M4SLOT1) BY 7.5 UNDEROVER
LM5    = SIZE (M5 OR M5SLOT1) BY 7.5 UNDEROVER
LM6    = SIZE (M6 OR M6SLOT1) BY 7.5 UNDEROVER
LTM1    = SIZE (TM1 OR TM1SLOT1) BY 7.5 UNDEROVER
LTM2    = SIZE (TM2 OR TM2SLOT1) BY 15 UNDEROVER

M1SLOT = M1SLOT1 INSIDE LM1
M2SLOT = M2SLOT1 INSIDE LM2
M3SLOT = M3SLOT1 INSIDE LM3
M4SLOT = M4SLOT1 INSIDE LM4
M5SLOT = M5SLOT1 INSIDE LM5
M6SLOT = M6SLOT1 INSIDE LM6
TM1SLOT = TM1SLOT1 INSIDE LTM1
TM2SLOT = TM2SLOT1 INSIDE LTM2

SL_1 {
@Min slot density of wide metal
NET AREA RATIO M1SLOT_S LM1_S <0.1 rdb  SL_1.out LM1_S
NET AREA RATIO M2SLOT_S LM2_S <0.1 rdb  SL_2.out LM2_S
NET AREA RATIO M3SLOT_S LM3_S <0.1 rdb  SL_3.out LM3_S
NET AREA RATIO M4SLOT_S LM4_S <0.1 rdb  SL_4.out LM4_S
NET AREA RATIO M5SLOT_S LM5_S <0.1 rdb  SL_5.out LM5_S
NET AREA RATIO M6SLOT_S LM6_S <0.1 rdb  SL_6.out LM6_S
NET AREA RATIO TM1SLOT_S LTM1_S <0.1 rdb  SL_7.out LTM1_S
NET AREA RATIO TM2SLOT_S LTM2_S <0.1 rdb  SL_8.out LTM2_S
}
SL_2 {
@ Slot dimension for each metal layer is 1.00
  INT M1SLOT < 1.0 ABUT<90 SINGULAR REGION
  INT M2SLOT < 1.0 ABUT<90 SINGULAR REGION
  INT M3SLOT < 1.0 ABUT<90 SINGULAR REGION
  INT M4SLOT < 1.0 ABUT<90 SINGULAR REGION
  INT M5SLOT < 1.0 ABUT<90 SINGULAR REGION
  INT M6SLOT < 1.0 ABUT<90 SINGULAR REGION
  INT TM1SLOT < 1.0 ABUT<90 SINGULAR REGION
  INT TM2SLOT < 1.0 ABUT<90 SINGULAR REGION
}
SL_3 {
@ Slots enclosure by metal is 2.00  
  ENC M1SLOT M1_SL < 2.0 ABUT<90 SINGULAR REGION
  ENC M2SLOT M2_SL < 2.0 ABUT<90 SINGULAR REGION
  ENC M3SLOT M3_SL < 2.0 ABUT<90 SINGULAR REGION
  ENC M4SLOT M4_SL < 2.0 ABUT<90 SINGULAR REGION
  ENC M5SLOT M5_SL < 2.0 ABUT<90 SINGULAR REGION
  ENC M6SLOT M6_SL < 2.0 ABUT<90 SINGULAR REGION
  ENC TM1SLOT TM1_SL < 2.0 ABUT<90 SINGULAR REGION
  ENC TM2SLOT TM2_SL < 2.0 ABUT<90 SINGULAR REGION
}
SL_4 {
@ Space between slots is 1.00
  EXT M1SLOT < 1.00 ABUT<90 SINGULAR REGION
  EXT M2SLOT < 1.00 ABUT<90 SINGULAR REGION
  EXT M3SLOT < 1.00 ABUT<90 SINGULAR REGION
  EXT M4SLOT < 1.00 ABUT<90 SINGULAR REGION
  EXT M5SLOT < 1.00 ABUT<90 SINGULAR REGION
  EXT M6SLOT < 1.00 ABUT<90 SINGULAR REGION
  EXT TM1SLOT < 1.00 ABUT<90 SINGULAR REGION
  EXT TM2SLOT < 1.00 ABUT<90 SINGULAR REGION
}

//=============================================
//  FUSE Guideline for metal fuse design minima
//=============================================

GROUP GFUSE FUSE_?

M6FUSE = FUSE AND M6

PIFUSE_RING = (PI INTERACT FUSE) NOT FUSE
M1FUSE_RING = PIFUSE_RING AND M1
AM1 = HOLES M1FUSE_RING
BM1 = M1FUSE_RING TOUCH AM1

M2FUSE_RING = PIFUSE_RING AND M2
AM2 = HOLES M2FUSE_RING
BM2 = M2FUSE_RING TOUCH AM2

M3FUSE_RING = PIFUSE_RING AND M3
AM3 = HOLES M3FUSE_RING
BM3 = M3FUSE_RING TOUCH AM3

M4FUSE_RING = PIFUSE_RING AND M4
AM4 = HOLES M4FUSE_RING
BM4 = M4FUSE_RING TOUCH AM4

M5FUSE_RING = PIFUSE_RING AND M5
AM5 = HOLES M5FUSE_RING
BM5 = M5FUSE_RING TOUCH AM5

M6FUSE_RING = PIFUSE_RING AND M6
AM6 = HOLES M6FUSE_RING
BM6 = M6FUSE_RING TOUCH AM6

TM1FUSE_RING = PIFUSE_RING AND TM1
ATM1 = HOLES TM1FUSE_RING
BTM1 = TM1FUSE_RING TOUCH AM1

TM2FUSE_RING = PIFUSE_RING AND TM2
ATM2 = HOLES TM2FUSE_RING
BTM2 = TM2FUSE_RING TOUCH ATM2

V1FUSE_RING = V1 INSIDE BM1 
V2FUSE_RING = V2 INSIDE BM2
V3FUSE_RING = V3 INSIDE BM3
V4FUSE_RING = V4 INSIDE BM4
V5FUSE_RING = V5 INSIDE BM5
TV1FUSE_RING = TV1 INSIDE BM6
TV2FUSE_RING = TV2 INSIDE BTM1

FUSE_1_6 {
@ Metal fuse width is 0.8
@ Metal fuse length is 4.0
  NOT ENCLOSE RECTANGLE M6FUSE 0.8 4.0
}
FUSE_2 {
@ V1 and Vn slot width in protection ring is 0.19
  INT V1FUSE_RING < 0.19 ABUT<90 SINGULAR REGION
  INT V2FUSE_RING < 0.19 ABUT<90 SINGULAR REGION
  INT V3FUSE_RING < 0.19 ABUT<90 SINGULAR REGION
  INT V4FUSE_RING < 0.19 ABUT<90 SINGULAR REGION
  INT V5FUSE_RING < 0.19 ABUT<90 SINGULAR REGION
}
FUSE_3 { 
@ TV slot width in protection ring is 0.21
  INT TV1FUSE_RING < 0.21 ABUT<90 SINGULAR REGION
  INT TV2FUSE_RING < 0.21 ABUT<90 SINGULAR REGION
}
FUSE_4 {
@ Polymide opening width is 30
  INT PI < 30 ABUT<90 SINGULAR REGION
}
FUSE_5 {
@ Fuse window width is 5
  INT FUSE < 5.0 ABUT<90 SINGULAR REGION
}
FUSE_7 {
@ Metal fuse space is 4
  EXT M6FUSE < 4 ABUT<90 SINGULAR REGION
}
FUSE_8 {
@ Space between fuse edge and PW edge is 8
  EXT FUSE NW < 8.0 ABUT<90 SINGULAR REGION
}
FUSE_9 {
@ Space between fuse edge and fuse window is 3.5
  ENC M6FUSE FUSE < 3.5 ABUT<90 SINGULAR REGION
}
FUSE_10 {
@ Space between fuse window and protection ring is 1.5
  EXT BM1 FUSE < 1.5 ABUT<90 SINGULAR REGION
  EXT BM2 FUSE < 1.5 ABUT<90 SINGULAR REGION
  EXT BM3 FUSE < 1.5 ABUT<90 SINGULAR REGION
  EXT BM4 FUSE < 1.5 ABUT<90 SINGULAR REGION
  EXT BM5 FUSE < 1.5 ABUT<90 SINGULAR REGION
  EXT BM6 FUSE < 1.5 ABUT<90 SINGULAR REGION
  EXT BTM1 FUSE < 1.5 ABUT<90 SINGULAR REGION
  EXT BTM2 FUSE < 1.5 ABUT<90 SINGULAR REGION
}
// Space between metal island and protection ring is 1.0
FUSE_12 {
@ CT enclosure by GT is 0.3
  CT_FUSE = (PI INTERACT FUSE) AND CT
  ENC CT_FUSE GT < 0.3 ABUT<90 SINGULAR REGION
}
FUSE_13 {
@ CT enclosure by M1 is 0.3
@ V1 enclosure by M1 is 0.3
  CT_FUSE = (PI INTERACT FUSE) AND CT
  V1_FUSE = (PI INTERACT FUSE) AND V1
  ENC CT_FUSE M1 < 0.3 ABUT<90 SINGULAR REGION
  ENC V1_FUSE M1 < 0.3 ABUT<90 SINGULAR REGION
}
FUSE_14 {
@ Vn enclosure by Mn for stacked via is 0.3
  V1_FUSE = (PI INTERACT FUSE) AND V1
  V2_FUSE = (PI INTERACT FUSE) AND V2
  V3_FUSE = (PI INTERACT FUSE) AND V3
  V4_FUSE = (PI INTERACT FUSE) AND V4
  V5_FUSE = (PI INTERACT FUSE) AND V5
  TV1_FUSE = (PI INTERACT FUSE) AND TV1
  TV2_FUSE = (PI INTERACT FUSE) AND TV2
  ENC V1_FUSE M2 < 0.3 ABUT<90 SINGULAR REGION
  ENC V2_FUSE M2 < 0.3 ABUT<90 SINGULAR REGION
  ENC V2_FUSE M3 < 0.3 ABUT<90 SINGULAR REGION
  ENC V3_FUSE M3 < 0.3 ABUT<90 SINGULAR REGION
  ENC V3_FUSE M4 < 0.3 ABUT<90 SINGULAR REGION
  ENC V4_FUSE M4 < 0.3 ABUT<90 SINGULAR REGION
  ENC V4_FUSE M5 < 0.3 ABUT<90 SINGULAR REGION
  ENC V5_FUSE M5 < 0.3 ABUT<90 SINGULAR REGION 
  ENC V5_FUSE M6 < 0.3 ABUT<90 SINGULAR REGION
  ENC TV1_FUSE M6 < 0.3 ABUT<90 SINGULAR REGION 
  ENC TV1_FUSE TM1 < 0.3 ABUT<90 SINGULAR REGION
  ENC TV2_FUSE TM1 < 0.3 ABUT<90 SINGULAR REGION
  ENC TV2_FUSE TM2 < 0.3 ABUT<90 SINGULAR REGION
} 
FUSE_15 {
@ V1 enclosure by M1 in protection ring is 0.4
  ENC V1FUSE_RING BM1 < 0.4 ABUT<90 SINGULAR REGION
}
FUSE_16 {
@ Vn enclosure by Mn in protection ring is 0.4
  ENC V1FUSE_RING BM2 < 0.4 ABUT<90 SINGULAR REGION
  ENC V2FUSE_RING BM2 < 0.4 ABUT<90 SINGULAR REGION
  ENC V2FUSE_RING BM3 < 0.4 ABUT<90 SINGULAR REGION
  ENC V3FUSE_RING BM3 < 0.4 ABUT<90 SINGULAR REGION
  ENC V3FUSE_RING BM4 < 0.4 ABUT<90 SINGULAR REGION
  ENC V4FUSE_RING BM4 < 0.4 ABUT<90 SINGULAR REGION
  ENC V4FUSE_RING BM5 < 0.4 ABUT<90 SINGULAR REGION
  ENC V5FUSE_RING BM5 < 0.4 ABUT<90 SINGULAR REGION
  ENC V5FUSE_RING BM6 < 0.4 ABUT<90 SINGULAR REGION
  ENC TV1FUSE_RING BM6 < 0.4 ABUT<90 SINGULAR REGION
}
FUSE_17 {
@ TV enclosure by TM in protection ring is 0.4
  ENC TV1FUSE_RING BTM1 < 0.4 ABUT<90 SINGULAR REGION
  ENC TV1FUSE_RING BTM2 < 0.4 ABUT<90 SINGULAR REGION
  ENC TV2FUSE_RING BTM2 < 0.4 ABUT<90 SINGULAR REGION
}
FUSE_18 {
@ Passivation window enclosure by polymide window is 12
  X = PI INTERACT FUSE
  ENC FUSE X < 12 ABUT<90 SINGULAR REGION
}

//==================================
//  PA - Passivasion DRC
//==================================

GROUP GPA PA_?

PA_1 {
@ PA.1
@ Guide line for a dimension of a passivasion window is 43
  INT (PA NOT ALPA) < 43 ABUT<90 SINGULAR REGION
}

PA_2 {
@ PA.2
@ Space between two passivasion windows is 7
  EXT (PA NOT ALPA) < 7 ABUT<90 SINGULAR REGION
}

PA_3 {
@ PA.3
@ Enclosure of top metal over a passavision window is 1.5
  X = TM2 OR (HOLES TM2)    
  ENC PA X < 1.5 ABUT<90 SINGULAR REGION OUTSIDE ALSO 
  Y = ((HOLES TM2) INTERACT PA) NOT PA
  AREA Y >0.5 
}


//==================================
//  HRP - High Resistance Poly DRC
//==================================

GROUP GHRP HRP_?

HRP_1 {
@ HRP.1
@ HRP min width is 0.30
  INT HRP < 0.30 ABUT<90 SINGULAR REGION
}
HRP_2 {
@ HRP.2
@ HRP min space is 0.30
  EXT HRP < 0.30 ABUT<90 SINGULAR REGION
}
HRP_3 {
@ HRP.3
@ Poly width for high resistance poly resistor is 2.0
  INT HR_GT < 2.0 ABUT<90 SINGULAR REGION
}
HRP_4 {
@ HRP.4
@ HRP extension outside of PO resistor region is 0.20
  ENC GT_HRP HRP < 0.20 ABUT<90 SINGULAR REGION
}
HRP_5_6 {
@ HRP.5 ; HRP.6
@ Clearance from HRP region to Poly gate of NMOS/PMOS is 0.20
  GT_MOS = GT INTERACT GATE
  EXT HRP GT_MOS < 0.20 ABUT<90 SINGULAR REGION
}
HRP_7 {
@ HRP.7
@ Min and Max P+ implant for pickup oerlap with SAB is 0.30
  SP_HRP = SP INTERACT HRP
  SAB_HRP = SAB INTERACT HRP
  INT SP_HRP SAB_HRP < 0.30 ABUT<90 SINGULAR REGION

  SIZE_SP = SIZE SP_HRP BY -0.30
  AND SIZE_SP SAB_HRP
}
HRP_8 {
@ HRP.8
@ SAB extension outside of PO resistor region is 0.22
  ENC GT_HRP SAB < 0.22 ABUT<90 SINGULAR REGION
}
HRP_9 {
@ HRP.9
@ Dummy layer "HRPDMY" is needed for DRC for high resistance Poly region
  SAB_HRP = SAB INTERACT HRP
  SAB_HRP NOT INTERACT HRPDMY
}
//HRP_10 {
//@Resistor square number > 5 for precision Rs
//}
HRP_11 {
@S/D-SN,SP and LDD is not allowed in the HRP region
 HR_GT AND SP
 HR_GT AND SN
 HR_GT AND NLL
 HR_GT AND NLH
 HR_GT AND PLL
 HR_GT AND PLH
}

//====================================
// BMIM - Top plate of two masks MIM
//====================================

GROUP GBMIM BMIM_?


BVn = (((V1 OR V2) OR V3) OR V4) OR V5
BMn = ((((M1 OR M2) OR M3) OR M4) OR M5) OR M6

CAP_BMIM  = (MIM INTERACT P2) INTERACT TV1
DMY_BMIM  = MIM NOT CAP_BMIM

TV1_BMIM = TV1 AND CAP_BMIM
TV1_BCAP = (TV1 AND P2) NOT TV1_BMIM

BMIM_1a {
@ BMIM.1a
@ Min MIM width as capacitor top plate is 2.00
  INT CAP_BMIM < 2.0 ABUT<90 SINGULAR REGION
}
BMIM_1b {
@ BMIM.1b
@ If capacitor lager than 50*50, please use combination of smaller capacitor is 50.0
@ Max.single MIM width is 50.0
  SIZE CAP_BMIM BY 25.0 UNDEROVER
}
BMIM_2 {
@ BMIM.2
@ Space between MIMs as top plate is 0.8
  EXT CAP_BMIM < 0.8 ABUT<90 SINGULAR REGION
}
BMIM_3 {
@ BMIM.3
@ MIM extension outside of Via which connect to this MIM is 0.24
  ENC TV1_BMIM CAP_BMIM < 0.24 ABUT<90 OUTSIDE ALSO SINGULAR REGION
  (TV1 INTERACT MIM) NOT MIM
}
BMIM_4 {
@ BMIM.4
@ Clearance of Via which connect with bottom plate (plate-2) to MIM region is 0.4um.
  EXT CAP_BMIM TV1_BCAP < 0.40 ABUT<90 SINGULAR REGION
}
BMIM_5 {
@ BMIM_5
@ Minimum space between two Via on MIM is 0.8
  X= EXT TV1_BMIM < 0.8 ABUT<90 SINGULAR REGION
  X INSIDE CAP_BMIM
}
BMIM_6 {
@ BMIM_6
@ To avoid strong coupling, Mn-1 is not allowed to put beneath MIM and Platte-2.
@ Min.clearance for Mn-1 to MIM is 1.0.
  EXT BMn CAP_BMIM < 1.00 ABUT<90 SINGULAR REGION
}
BMIM_7 {
@ BMIM_7
@ MiM pattern density. Dummy MIM pattern is required if MIM density is less than 3%.
@ Note that if dummy MIM is added, dummy plate-2 will be followed to add.
  DENSITY MIMi < 0.03
}
BMIM_8 {
@ BMIM_8
@ Min.dummy MIM width is 0.4
  INT DMY_BMIM < 0.4 ABUT<90 SINGULAR REGION
}
BMIM_9 {
@ BMIM_9
@ Space between dummy MIM and MIM region is 0.8
  EXT DMY_BMIM CAP_BMIM < 0.80 ABUT<90 SINGULAR REGION
}
BMIM_10 {
@ BMIM_10
@ Clearance between dummy MIM and Plate-2 is 0.8
  EXT DMY_BMIM P2 < 0.8 ABUT<90 SINGULAR REGION
}
BMIM_11 {
@ BMIM_11
@ MiM region cross PLate is not allowed.
  CAP_BMIM NOT INSIDE P2
  DMY_BMIM NOT INSIDE P2
}
BMIM_12 {
@ BMIM_12
@ Via and metal layers under MIM region are not allowed.
  AND BVn CAP_BMIM
  AND BMn CAP_BMIM
}
BMIM_13 {
@ BMIM_13
@ Both active and passive devices under MIM region are not allowed.
  X = (AA OR GT) OR RESNW
  AND X CAP_BMIM
}

//================
//  Plate 2 rule
//================

GROUP GP2 P2_?

P2_1 {
@ P2_1
@ Minimum extension of plate 2 region to MIM region is 0.4
  ENC MIM P2 < 0.4 ABUT<90 OUTSIDE ALSO SINGULAR REGION
  MIM NOT P2
}
P2_2 {
@ P2_2
@ Minimum space between two via on plate 2 is 0.36
  X= EXT TV1_BCAP < 0.36 ABUT<90 SINGULAR REGION
  X INSIDE P2
}
P2_3 {
@ P2_3
@ Min.P2 extension for Via which contact to this plate-2 is 0.2
  ENC TV1_BCAP P2 < 0.2 ABUT<90 OUTSIDE ALSO SINGULAR REGION
  ((TV1 INTERACT P2) NOT MIM) NOT P2
}
P2_4 {
@ P2_4
@ Min.space of P2 to P2 is 0.4
  EXT P2 < 0.4 ABUT<90 SINGULAR REGION
}
P2_5 {
@ P2_5
@ Mn-1 is not allowed to put beneath P2.Min.clearance for Mn-1 to P2 is 0.5.
  EXT BMn P2 < 0.5 ABUT<90 SINGULAR REGION
  BMn AND P2
  BVn AND P2
}
P2_6 {
@ P2_6
@ DUMBM layer is needed in P2 region to avoid dummy metal insertion.
@ Min.distance of P2 enclosure by DUMBM is 2.0
  ENC P2 DUMBM < 2.0 ABUT<90 OUTSIDE ALSO SINGULAR REGION
  (P2 INTERACT DUMBM ) NOT INSIDE DUMBM
}

//============================
//  Conventional Check
//============================

GROUP GConvention Convention_?

Convention_FLT_NW {
@ NW without N+ pick up
  NW NOT INTERACT NTAPi
}
Convention_BPMO {
@ Pmos in PW
  PW AND PGATE
}
Convention_BAD_IMP {
@ AA area without any implant
  X = (AA NOT AADUM) NOT INTERACT RESNW
  (X NOT SN) NOT SP
}
PMOS1 = (PMOSI NOT INTERACT CAPBP) NOT INTERACT RESP1
Convention_LAT.3a {
@ Maximum space from any point within Source/Drain region to the nearest pickup AA region inside the same well for I/O and internal circuits
A = INT NTAPi < 0.005 ABUT INTERSECTING ONLY REGION
B = (NTAPi INTERACT A) NOT A
C = INT B < 0.001 ABUT INTERSECTING ONLY REGION
D = (B INTERACT C) NOT C
NW_MINUS_NTAP = SIZE D BY 35 INSIDE OF NW STEP 0.6
X = PMOS1 AND NW
OUTSIDE EDGE X NW_MINUS_NTAP
}
NMOS1 = (NMOSI NOT INTERACT CAPBP) NOT INTERACT RESP1
Convention_LAT.3b {
@ Maximum space from any point within Source/Drain region to the nearest pickup AAregion inside the same well for I/O and internal circuits
A = INT PTAPi < 0.005 ABUT INTERSECTING ONLY REGION
B = (PTAPi INTERACT A) NOT A
C = INT B < 0.001 ABUT INTERSECTING ONLY REGION
D = (B INTERACT C) NOT C
PW_MINUS_PTAP = SIZE D BY 35 INSIDE OF PW STEP 0.6
X = NMOS1 AND PW
OUTSIDE EDGE X PW_MINUS_PTAP
}

